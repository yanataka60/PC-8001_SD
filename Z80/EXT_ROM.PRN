			  Z80 ASSEMBLER - ZASM VER 1.6
                      	;2022.7.24 FILESでSYNTAX ERRORとなる事象を回避、SERCH FILEと聞いてくる仕様を削除
                      	
  0257                	CONOUT		EQU		0257H
  03A9                	CSR			EQU		03A9H
  03F3                	CSRADR		EQU		03F3H
  0BE2                	DSPCSR		EQU		0BE2H
  0FAC                	KYSCAN		EQU		0FACH
  1B7E                	LINPUT		EQU		1B7EH
  1F8B                	AFTLOAD		EQU		1F8BH
  40A6                	CHROUT		EQU		40A6H
  52ED                	MSGOUT		EQU		52EDH
  5B86                	RENUM9		EQU		5B86H
  5E39                	HEXCHK		EQU		5E39H
  5E4B                	BINCV4		EQU		5E4BH
  5E83                	MONBHX		EQU		5E83H
  5EBD                	MONDME		EQU		5EBDH
  5EC0                	MONDHL		EQU		5EC0H
  5FC1                	AZLCNV		EQU		5FC1H
  5FCA                	MONCLF		EQU		5FCAH
  5FD4                	MONSPC		EQU		5FD4H
                      	
  EA63                	CUSPOS		EQU		0EA63H
  EB54                	BASSRT		EQU		0EB54H
  EC96                	IBUF		EQU		0EC96H
  EF3E                	FILNAM		EQU		0EF3EH
  EFA0                	VARBGN		EQU		0EFA0H
  F139                	TBLOAD		EQU		0F139H
  F142                	TBKILL		EQU		0F142H
  F14B                	TBSAVE		EQU		0F14BH
  F14E                	TBFILES		EQU		0F14EH
  FF34                	MONHL		EQU		0FF34H
  FF36                	MONSP		EQU		0FF36H
  FF3D                	SADRS		EQU		0FF3DH
  FF41                	EADRS		EQU		0FF41H
  FF66                	LBUF		EQU		0FF66H
                      	
                      	
                      	;8255 PORT アドレス FCH〜FFH
                      	;0FCH PORTA 送信データ(下位4ビット)
                      	;0FDH PORTB 受信データ(8ビット)
                      	;
                      	;0FEH PORTC Bit
                      	;7 IN  CHK
                      	;6 IN
                      	;5 IN
                      	;4 IN 
                      	;3 OUT
                      	;2 OUT FLG
                      	;1 OUT
                      	;0 OUT
                      	;
                      	;0FFH コントロールレジスタ
                      	
                      	
  6000                	        ORG		6000H
                      	
  6000  4142          			DB		041H,042H         ;拡張ROM認識コード
                      	
  6002  CD0960        			CALL	INIT              ;POWER ONで8255を初期化、LOAD、SAVE、FILES、KILLのジャンプ先を設定
  6005  C9            			RET
                      	
                      	;*********** OPENしているファイルから1BYTE読み出し転送 ****************
                      	;5F9EH代替ルーチン
  6006  C3AE62        			JP		D_5F9E
                      	
                      	;**** 8255初期化 ****
                      	;PORTC下位BITをOUTPUT、上位BITをINPUT、PORTBをINPUT、PORTAをOUTPUT
  6009  3E8A          	INIT:	LD		A,8AH
  600B  D3FF          			OUT		(0FFH),A
                      	;出力BITをリセット
  600D  AF            			XOR		A                 ;PORTA <- 0
  600E  D3FC          			OUT		(0FCH),A
  6010  D3FE          			OUT		(0FEH),A          ;PORTC <- 0
                      	
                      	;LOAD、SAVE、FILES、KILLのジャンプ先を設定
  6012  21E963        			LD		HL,CMDLOAD
  6015  2239F1        			LD		(TBLOAD),HL
                      	
  6018  215064        			LD		HL,CMDSAVE
  601B  224BF1        			LD		(TBSAVE),HL
                      	
  601E  219C64        			LD		HL,CMDFILES
  6021  224EF1        			LD		(TBFILES),HL
                      	
  6024  21B064        			LD		HL,CMDKILL
  6027  2242F1        			LD		(TBKILL),HL
                      	
  602A  C9            			RET
                      	
                      	;**** 1BYTE受信 ****
                      	;受信DATAをAレジスタにセットしてリターン
  602B                	RCVBYTE:
  602B  CD6060        			CALL	F1CHK             ;PORTC BIT7が1になるまでLOOP
  602E  DBFD          			IN		A,(0FDH)          ;PORTB -> A
  6030  F5            			PUSH 	AF
  6031  3E05          			LD		A,05H
  6033  D3FF          			OUT		(0FFH),A          ;PORTC BIT2 <- 1
  6035  CD6760        			CALL	F2CHK             ;PORTC BIT7が0になるまでLOOP
  6038  3E04          			LD		A,04H
  603A  D3FF          			OUT		(0FFH),A          ;PORTC BIT2 <- 0
  603C  F1            			POP 	AF
  603D  C9            			RET
                      			
                      	;**** 1BYTE送信 ****
                      	;Aレジスタの内容をPORTA下位4BITに4BITずつ送信
  603E                	SNDBYTE:
  603E  F5            			PUSH	AF
  603F  1F            			RRA
  6040  1F            			RRA
  6041  1F            			RRA
  6042  1F            			RRA
  6043  E60F          			AND		0FH
  6045  CD4F60        			CALL	SND4BIT
  6048  F1            			POP		AF
  6049  E60F          			AND		0FH
  604B  CD4F60        			CALL	SND4BIT
  604E  C9            			RET
                      	
                      	;**** 4BIT送信 ****
                      	;Aレジスタ下位4ビットを送信する
  604F                	SND4BIT:
  604F  D3FC          			OUT		(0FCH),A
  6051  3E05          			LD		A,05H
  6053  D3FF          			OUT		(0FFH),A          ;PORTC BIT2 <- 1
  6055  CD6060        			CALL	F1CHK             ;PORTC BIT7が1になるまでLOOP
  6058  3E04          			LD		A,04H
  605A  D3FF          			OUT		(0FFH),A          ;PORTC BIT2 <- 0
  605C  CD6760        			CALL	F2CHK
  605F  C9            			RET
                      			
                      	;**** BUSYをCHECK(1) ****
                      	; 82H BIT7が1になるまでLOP
  6060  DBFE          	F1CHK:	IN		A,(0FEH)
  6062  E680          			AND		80H               ;PORTC BIT7 = 1?
  6064  28FA          			JR		Z,F1CHK
  6066  C9            			RET
                      	
                      	;**** BUSYをCHECK(0) ****
                      	; 82H BIT7が0になるまでLOOP
  6067  DBFE          	F2CHK:	IN		A,(0FEH)
  6069  E680          			AND		80H               ;PORTC BIT7 = 0?
  606B  20FA          			JR		NZ,F2CHK
  606D  C9            			RET
                      	
                      	;************* 代替MONITOR START *************************
  606E  2234FF        	MONINI:	LD		(MONHL),HL
  6071  ED7336FF      			LD		(MONSP),SP
                      			
                      	;		PUSH	HL                ;タイトルを表示するとBASICから戻ったときに自動実行が途切れてしまうため削除
                      	;		LD		HL,MONMSG
                      	;		CALL	MSGOUT
                      	;		POP		HL
                      			
  6075  01C960        			LD		BC,MONERR         ;BASICに戻るときPOPしているのでお約束らしい
  6078  C5            			PUSH	BC
  6079                	CMD1:
  6079  3E2A          			LD		A,'*'
  607B  CD5702        			CALL	CONOUT            ;プロンプト表示
  607E  CDE20B        			CALL	DSPCSR
  6081  CD7E1B        			CALL	LINPUT            ;スクリーンエディットを使いたいために使用、このためCTRL+BではBASICに戻れない
                      									  ;HLにIBUF-1が入って戻っている LD HL,IBUF-1
  6084                	CMD2:
  6084  3832          			JR		C,MONCTB          ;CTRL+Bの代替、CTRL+CでBASICに復帰
  6086  23            			INC		HL
  6087  7E            			LD		A,(HL)
  6088  FE2A          			CP		'*'               ;入力時とスクリーンエディット時でIBUFに入る位置が変わるための対処、「*」ならポインタを進める
  608A  28F8          			JR		Z,CMD2
  608C  7E            			LD		A,(HL)
  608D  CDC15F        			CALL	AZLCNV            ;入力文字を大文字へ変換
  6090  FE42          			CP		'B'
  6092  2824          			JR		Z,MONCTB          ;CTRL+Bの代替、BコマンドでもBASICに復帰
  6094  FE44          			CP		'D'
  6096  CAD160        			JP		Z,STMD            ;Dコマンド
  6099  FE53          			CP		'S'
  609B  CA4D61        			JP		Z,STMW            ;Sコマンド
  609E  FE46          			CP		'F'
  60A0  CA8E61        			JP		Z,STLT            ;Fコマンド
  60A3  FE4C          			CP		'L'
  60A5  CA4E62        			JP		Z,MONLOAD         ;Lコマンド
  60A8  FE57          			CP		'W'
  60AA  CACF62        			JP		Z,MONSAVE         ;Wコマンド
  60AD  FE47          			CP		'G'
  60AF  280D          			JR		Z,GOCMD           ;Gコマンド
  60B1  3EF4          			LD		A,0F4H
  60B3  CD9C63        			CALL	SDERR             ;コマンドエラー
  60B6  18C1          			JR		CMD1
                      			
                      	;MONMSG:	DEFB	0CH,'**PC-8001_SD Monitor **',0DH,0AH,00H
                      	
                      	;************ Bコマンド BASIC復帰 ***********************
  60B8  C1            	MONCTB:	POP		BC
  60B9  2A34FF        			LD		HL,(MONHL)
  60BC  FB            			EI
  60BD  C9            			RET
                      	
                      	;************ Gコマンド アドレスxxxxへジャンプ ***************
  60BE  23            	GOCMD:	INC		HL
  60BF  CD6E63        			CALL	STFN
  60C2  CD3C63        			CALL	HLHEX             ;4桁の16進数であればHLにセットして続行
  60C5  DAC960        			JP		C,MONERR
  60C8  E9            			JP		(HL)              ;HLの示すアドレスにジャンプ
                      	
                      	;パラメータエラー処理
  60C9  3EF6          	MONERR:	LD		A,0F6H
  60CB  CD9C63        			CALL	SDERR
  60CE  C37960        			JP		CMD1
                      	
                      	;************ Dコマンド アドレスxxxxからのMEMORYをDUMP **********************
  60D1  23            	STMD:	INC		HL
  60D2  CD6E63        			CALL	STFN
  60D5  CD3C63        			CALL	HLHEX             ;4桁の16進数であればSADRSにセットして続行
  60D8  DAC960        			JP		C,MONERR
  60DB  223DFF        			LD		(SADRS),HL        ;SARDS保存
                      	
  60DE  21F264        	STMD6:	LD		HL,MSG_AD1        ;DUMP TITLE表示
  60E1  CDED52        			CALL	MSGOUT
  60E4  0E10          			LD		C,10H             ;16行(128Byte)を表示
  60E6  2A3DFF        	STMD7:	LD		HL,(SADRS)        ;アドレス表示
  60E9  CDC05E        			CALL	MONDHL
  60EC  CDD45F        			CALL	MONSPC
                      	
                      			
  60EF  0608          			LD		B,08H             ;一行(8Byte)のデータを16進数表示
  60F1  CDBD5E        	STMD0:	CALL	MONDME
  60F4  CDD45F        			CALL	MONSPC
  60F7  CDAC0F        			CALL	KYSCAN
  60FA  FE1B          			CP		1BH               ;表示途中でもESCで打ち切り
  60FC  284C          			JR		Z,STMD4
  60FE  23            			INC		HL
  60FF  05            			DEC		B
  6100  20EF          			JR		NZ,STMD0
                      	
  6102  2A3DFF        			LD		HL,(SADRS)
  6105  0608          			LD		B,08H             ;一行(8Byte)のデータをキャラクタ表示
  6107  7E            	STMD2:	LD		A,(HL)
  6108  FE20          			CP		20H               ;文字化けに対処 20H未満なら20Hに置き換え
  610A  3002          			JR		NC,STMD8
  610C  3E20          			LD		A,20H
  610E  CD5702        	STMD8:	CALL	CONOUT
  6111  CDAC0F        			CALL	KYSCAN
  6114  FE1B          			CP		1BH               ;表示途中でもESCで打ち切り
  6116  2832          			JR		Z,STMD4
  6118  23            			INC		HL
  6119  05            			DEC		B
  611A  20EB          			JR		NZ,STMD2
                      	
  611C  223DFF        			LD		(SADRS),HL
  611F  CDCA5F        			CALL	MONCLF
                      	
  6122  0D            			DEC		C
  6123  20C1          			JR		NZ,STMD7
                      			
  6125  211A65        			LD		HL,MSG_AD2        ;入力待ちメッセージ表示
  6128  CDED52        			CALL	MSGOUT
  612B  CDAC0F        	STMD3:	CALL	KYSCAN            ;1文字入力待ち
  612E  A7            			AND		A
  612F  28FA          			JR		Z,STMD3
  6131  FE1B          			CP		1BH               ;ESCで打ち切り
  6133  2815          			JR		Z,STMD4
  6135  CDC15F        			CALL	AZLCNV
  6138  FE42          			CP		42H               ;BからBACK
  613A  200B          			JR		NZ,STMD5
  613C  2A3DFF        			LD		HL,(SADRS)
  613F  110001        			LD		DE,0100H
  6142  ED52          			SBC		HL,DE
  6144  223DFF        			LD		(SADRS),HL
  6147  C3DE60        	STMD5:	JP		STMD6
  614A  C37960        	STMD4:	JP		CMD1
                      	
                      	;************ Sコマンド アドレスxxxxからMEMORYに書き込み **********************
  614D  23            	STMW:	INC		HL
  614E  CD6E63        			CALL	STFN
  6151  CD3C63        			CALL	HLHEX             ;4桁の16進数であればHLにセットして続行
  6154  DAC960        			JP		C,MONERR
                      	
  6157  1A            	STSP1:	LD		A,(DE)
  6158  A7            			AND		A
  6159  2830          			JR		Z,STMW9           ;アドレスのみなら終了
  615B  FE20          			CP		20H
  615D  2003          			JR		NZ,STMW1
  615F  13            			INC		DE                ;空白は飛ばす
  6160  18F5          			JR		STSP1
                      	
  6162                	STMW1:
  6162  CD5463        			CALL	TWOHEX
  6165  380D          			JR		C,STMW8
  6167  77            			LD		(HL),A            ;2桁の16進数があれば(HL)に書き込み
  6168  23            			INC		HL
                      	
  6169  1A            	STSP2:	LD		A,(DE)
  616A  A7            			AND		A               ;一行終了
  616B  2807          			JR		Z,STMW8
  616D  FE20          			CP		20H
  616F  20F1          			JR		NZ,STMW1
  6171  13            			INC		DE                ;空白は飛ばす
  6172  18F5          			JR		STSP2
                      	
  6174  E5            	STMW8:	PUSH	HL
  6175  21B566        			LD		HL,MSG_FDW        ;行頭に'*S '
  6178  CDED52        			CALL	MSGOUT
  617B  E1            			POP		HL
  617C  E5            			PUSH	HL
  617D  CDC05E        			CALL	MONDHL            ;アドレス表示
  6180  CDD45F        			CALL	MONSPC
  6183  CD7E1B        			CALL	LINPUT            ;行入力繰り返し
                      	
  6186  D1            			POP		DE
  6187  23            			INC		HL
  6188  EB            			EX		DE,HL
  6189  18CC          			JR		STSP1
  618B  C37960        	STMW9:	JP		CMD1
                      	
                      	;************ Fコマンド DIRLIST **********************
  618E  23            	STLT:	INC		HL
  618F  CD6E63        			CALL	STFN              ;検索文字列を送信
  6192  EB            			EX		DE,HL
  6193  21B866        			LD		HL,DEFDIR         ;行頭に'*L 'を付けることでカーソルを移動させリターンで実行できるように
  6196  010300        			LD		BC,DEND-DEFDIR
  6199  CDA361        			CALL	DIRLIST           ;DIRLIST本体をコール
  619C  A7            			AND		A                 ;00以外ならERROR
  619D  C49C63        			CALL	NZ,SDERR
  61A0  C37960        			JP		CMD1
                      	
                      	
                      	;**** DIRLIST本体 (HL=行頭に付加する文字列の先頭アドレス BC=行頭に付加する文字列の長さ) ****
                      	;****              戻り値 A=エラーコード ****
  61A3                	DIRLIST:
  61A3  3E83          			LD		A,83H             ;DIRLISTコマンド83Hを送信
  61A5  CD3563        			CALL	STCD              ;コマンドコード送信
  61A8  A7            			AND		A                 ;00以外ならERROR
  61A9  C24D62        			JP		NZ,DLRET
                      			
  61AC  C5            			PUSH	BC
  61AD  0621          			LD		B,21H             ;ファイルネーム検索文字列33文字分を送信
  61AF  1A            	STLT1:	LD		A,(DE)
  61B0  A7            			AND		A
  61B1  2001          			JR		NZ,STLT2
  61B3  AF            			XOR		A
  61B4  CDC15F        	STLT2:	CALL	AZLCNV            ;大文字に変換
  61B7  FE22          			CP		22H               ;ダブルコーテーション読み飛ばし
  61B9  2003          			JR		NZ,STLT3
  61BB  13            			INC		DE
  61BC  18F1          			JR		STLT1
  61BE  CD3E60        	STLT3:	CALL	SNDBYTE           ;ファイルネーム検索文字列を送信
  61C1  13            			INC		DE
  61C2  05            			DEC		B
  61C3  20EA          			JR		NZ,STLT1
  61C5  C1            			POP		BC
                      	
  61C6  CD2B60        			CALL	RCVBYTE           ;状態取得(00H=OK)
  61C9  A7            			AND		A                 ;00以外ならERROR
  61CA  C24D62        			JP		NZ,DLRET
                      	
  61CD                	DL1:
  61CD  E5            			PUSH	HL
  61CE  C5            			PUSH	BC
  61CF  1166FF        			LD		DE,LBUF
  61D2  EDB0          			LDIR
  61D4  EB            			EX		DE,HL
  61D5  CD2B60        	DL2:	CALL	RCVBYTE           ;'00H'を受信するまでを一行とする
  61D8  A7            			AND		A
  61D9  280C          			JR		Z,DL3
  61DB  FEFF          			CP		0FFH              ;'0FFH'を受信したら終了
  61DD  281E          			JR		Z,DL4
  61DF  FEFE          			CP		0FEH              ;'0FEH'を受信したら一時停止して一文字入力待ち
  61E1  2821          			JR		Z,DL5
  61E3  77            			LD		(HL),A
  61E4  23            			INC		HL
  61E5  18EE          			JR		DL2
  61E7  3600          	DL3:	LD		(HL),00H
  61E9  2166FF        			LD		HL,LBUF           ;'00H'を受信したら一行分を表示して改行
  61EC  7E            	DL31:	LD		A,(HL)
  61ED  A7            			AND		A
  61EE  2806          			JR		Z,DL33
  61F0  CDA640        			CALL	CHROUT
  61F3  23            			INC		HL
  61F4  18F6          			JR		DL31
  61F6  CDCA5F        	DL33:	CALL	MONCLF
  61F9  C1            			POP		BC
  61FA  E1            			POP		HL
  61FB  18D0          			JR		DL1
  61FD  CD2B60        	DL4:	CALL	RCVBYTE           ;状態取得(00H=OK)
  6200  C1            			POP		BC
  6201  E1            			POP		HL
  6202  1849          			JR		DLRET
                      	
  6204  E5            	DL5:	PUSH	HL
  6205  213665        			LD		HL,MSG_KEY1       ;HIT ANT KEY表示
  6208  CDED52        			CALL	MSGOUT
  620B  2A63EA        			LD		HL,(CUSPOS)
  620E  CDF303        			CALL	CSRADR
  6211  3E1E          			LD		A,1EH
  6213  77            			LD		(HL),A
  6214  3E1C          			LD		A,1CH
  6216  CD5702        			CALL	CONOUT
  6219  214D65        			LD		HL,MSG_KEY2       ;HIT ANT KEY表示
  621C  CDED52        			CALL	MSGOUT
  621F  CDCA5F        			CALL	MONCLF
  6222  E1            			POP		HL
  6223  CDAC0F        	DL6:	CALL	KYSCAN            ;1文字入力待ち
  6226  CDC15F        			CALL	AZLCNV
  6229  A7            			AND		A
  622A  28F7          			JR		Z,DL6
  622C  FE1B          			CP		1BH               ;ESCで打ち切り
  622E  2815          			JR		Z,DL7
  6230  FE1E          			CP		1EH               ;カーソル↑で打ち切り
  6232  2807          			JR		Z,DL9
  6234  FE42          			CP		42H               ;「B」で前ページ
  6236  280F          			JR		Z,DL8
  6238  AF            			XOR		A                 ;それ以外で継続
  6239  180C          			JR		DL8
  623B  3E1E          	DL9:	LD		A,1EH
  623D  CD5702        			CALL	CONOUT
  6240  3E1E          			LD		A,1EH
  6242  CD5702        			CALL	CONOUT            ;カーソル↑で打ち切ったときにカーソル2行上へ
  6245  3EFF          	DL7:	LD		A,0FFH            ;0FFH中断コードを送信
  6247  CD3E60        	DL8:	CALL	SNDBYTE
  624A  C3D561        			JP		DL2
                      			
  624D  C9            	DLRET:	RET
                      			
                      	
                      	;************ Lコマンド .CMT LOAD *************************
  624E  3E71          	MONLOAD:LD		A,71H            ;Lコマンド71Hを送信
  6250  CDB762        			CALL	STCMD
  6253  C27960        			JP		NZ,CMD1
                      	
  6256  E5            			PUSH	HL
  6257  211801        			LD		HL,0118H         ;1文字目、24行目へカーソルを移動
  625A  CDA903        			CALL	CSR
  625D  CDCA5F        			CALL	MONCLF
  6260  E1            			POP		HL
  6261  CD2B60        			CALL	RCVBYTE          ;ヘッダー受信
  6264  FE3A          			CP		3AH              ;3AHであれば続行
  6266  280C          			JR		Z,MCNLOAD
  6268  217466        			LD		HL,MSG_F2        ;3AH以外ならエラー
  626B  CDED52        			CALL	MSGOUT
  626E  CDCA5F        			CALL	MONCLF
  6271  C37960        			JP		CMD1
                      			
  6274  21CA64        	MCNLOAD:LD		HL,MSG_LD        ;LOADING表示
  6277  CDED52        			CALL	MSGOUT
                      	
  627A  213EFF        			LD		HL,SADRS+1       ;SADRS取得
  627D  CD2B60        			CALL	RCVBYTE
  6280  77            			LD		(HL),A
  6281  2B            			DEC		HL
  6282  CD2B60        			CALL	RCVBYTE
  6285  77            			LD		(HL),A
  6286  CD2B60        			CALL	RCVBYTE          ;チェックサム廃棄
                      			
  6289  2A3DFF        			LD		HL,(SADRS)
  628C  CD2B60        	MCLD1:	CALL	RCVBYTE          ;ヘッダー3AH受信、廃棄
  628F  CD2B60        			CALL	RCVBYTE          ;データ長
  6292  A7            			AND		A
  6293  280D          			JR		Z,MCLD3          ;データ長が0なら終了
  6295  47            			LD		B,A
  6296  CD2B60        	MCLD2:	CALL	RCVBYTE          ;実データ受信
  6299  77            			LD		(HL),A
  629A  23            			INC		HL
  629B  10F9          			DJNZ	MCLD2
  629D  CD2B60        			CALL	RCVBYTE          ;チェックサム廃棄
  62A0  18EA          			JR		MCLD1
  62A2  CD2B60        	MCLD3:	CALL	RCVBYTE          ;チェックサム廃棄
  62A5  21D364        			LD		HL,MSG_OK        ;OK表示
  62A8  CDED52        			CALL	MSGOUT
  62AB  C37960        			JP		CMD1
                      	
                      	;********** 5F9EH READ ONE BYTE FROM CMTの代替 *********
  62AE  3E72          	D_5F9E:	LD		A,72H            ;コマンド72Hを送信
  62B0  CD3563        			CALL	STCD
  62B3  CD2B60        			CALL	RCVBYTE          ;1Byteのみ受信
  62B6  C9            			RET
                      	
                      	;**** コマンド、ファイル名送信 (IN:A コマンドコード HL:ファイルネームの先頭)****
  62B7  23            	STCMD:	INC		HL
  62B8  CD6E63        			CALL	STFN             ;空白除去
  62BB  E5            			PUSH	HL
  62BC  CD3563        			CALL	STCD             ;コマンドコード送信
  62BF  E1            			POP		HL
  62C0  A7            			AND		A                ;00以外ならERROR
  62C1  2008          			JR		NZ,STCMD2
  62C3  CD8263        			CALL	STFS             ;ファイルネーム送信
  62C6  A7            			AND		A                ;00以外ならERROR
  62C7  C2CB62        			JP		NZ,STCMD2
  62CA  C9            			RET
  62CB  CD9C63        	STCMD2:	CALL	SDERR
  62CE  C9            	SRCMD3:	RET
                      	
                      	;************ Wコマンド .CMT SAVE ********************************
  62CF  23            	MONSAVE:INC		HL
  62D0  CD6E63        			CALL	STFN
  62D3  CD3C63        			CALL	HLHEX            ;4桁の16進数であればSADRSにセットして続行
  62D6  DAC960        			JP		C,MONERR
  62D9  223DFF        			LD		(SADRS),HL       ;SARDS保存
  62DC  EB            			EX		DE,HL
                      	
  62DD  CD6E63        			CALL	STFN
  62E0  CD3C63        			CALL	HLHEX            ;4桁の16進数であればEADRSにセットして続行
  62E3  DAC960        			JP		C,MONERR
  62E6  2241FF        			LD		(EADRS),HL       ;EARDS保存
                      	
  62E9  D5            			PUSH	DE
  62EA  E5            			PUSH	HL
  62EB  ED5B3DFF      			LD		DE,(SADRS)
  62EF  ED52          			SBC		HL,DE
  62F1  E1            			POP		HL
  62F2  D1            			POP		DE
  62F3  DAC960        			JP		C,MONERR         ;EADRSがSADRSより小さければエラー
                      	
  62F6  EB            			EX		DE,HL
  62F7  2B            			DEC		HL
                      			
  62F8  3E70          			LD		A,70H            ;コマンド70Hを送信
  62FA  CDB762        			CALL	STCMD
  62FD  C27960        			JP		NZ,CMD1
                      	
  6300  21D964        			LD		HL,MSG_WR        ;WRITING表示
  6303  CDED52        			CALL	MSGOUT
                      	
  6306  2A3DFF        			LD		HL,(SADRS)       ;SADRSを送信
  6309  7D            			LD		A,L
  630A  CD3E60        			CALL	SNDBYTE
  630D  7C            			LD		A,H
  630E  CD3E60        			CALL	SNDBYTE
  6311  ED5B41FF      			LD		DE,(EADRS)       ;送信する全体バイト数を算出するためにEADRSを送信
  6315  7B            			LD		A,E
  6316  CD3E60        			CALL	SNDBYTE
  6319  7A            			LD		A,D
  631A  CD3E60        			CALL	SNDBYTE
  631D  7E            	MONSV1:	LD		A,(HL)           ;SADRSからEADRSまでを送信
  631E  CD3E60        			CALL	SNDBYTE
  6321  7C            			LD		A,H
  6322  BA            			CP		D
  6323  2004          			JR		NZ,MONSV2
  6325  7D            			LD		A,L
  6326  BB            			CP		E
  6327  2803          			JR		Z,MONSV3         ;HL = DE までLOOP
  6329  23            	MONSV2:	INC		HL
  632A  18F1          			JR		MONSV1
  632C  21D364        	MONSV3:	LD		HL,MSG_OK        ;OK表示
  632F  CDED52        			CALL	MSGOUT
  6332  C37960        			JP		CMD1
                      	
                      	;**** コマンド送信 (IN:A コマンドコード)****
  6335  CD3E60        	STCD:	CALL	SNDBYTE          ;Aレジスタのコマンドコードを送信
  6338  CD2B60        			CALL	RCVBYTE          ;状態取得(00H=OK)
  633B  C9            			RET
                      	
                      	;**** HLからの4Byteが16進数を表すアスキーコードであれば16進数に変換してHLに代入 **************
  633C  EB            	HLHEX:	EX		DE,HL
  633D  210000        			LD		HL,0000H
  6340  0604          			LD		B,04H
  6342  1A            	HLHEX1:	LD		A,(DE)
  6343  13            			INC		DE
  6344  CDC15F        			CALL	AZLCNV
  6347  CD395E        			CALL	HEXCHK
  634A  DA5363        			JP		C,HLHEX2
  634D  CD4B5E        			CALL	BINCV4
  6350  10F0          			DJNZ	HLHEX1
  6352  AF            			XOR		A
  6353  C9            	HLHEX2:	RET
                      	
                      	;**** HLからの2Byteが16進数を表すアスキーコードであれば16進数に変換してAに代入 **************
  6354  E5            	TWOHEX:	PUSH	HL
  6355  210000        			LD		HL,0000H
  6358  0602          			LD		B,02H
  635A  1A            	TWHEX1:	LD		A,(DE)
  635B  13            			INC		DE
  635C  CDC15F        			CALL	AZLCNV
  635F  CD395E        			CALL	HEXCHK
  6362  DA6C63        			JP		C,TWHEX2
  6365  CD4B5E        			CALL	BINCV4
  6368  10F0          			DJNZ	TWHEX1
  636A  AF            			XOR		A
  636B  7D            			LD		A,L
  636C                	TWHEX2:
  636C  E1            			POP		HL
  636D  C9            			RET
                      	
                      	;****** FILE NAMEが取得できるまでスペース、ダブルコーテーションを読み飛ばし (IN:HL コマンド文字の次の文字 OUT:HL ファイルネームの先頭)*********
  636E  F5            	STFN:	PUSH	AF
  636F  7E            	STFN1:	LD		A,(HL)
  6370  FE20          			CP		20H
  6372  2804          			JR		Z,STFN2
  6374  FE22          			CP		22H
  6376  2003          			JR		NZ,STFN3
  6378  23            	STFN2:	INC		HL               ;ファイルネームまでスペース読み飛ばし
  6379  18F4          			JR		STFN1
  637B  F1            	STFN3:	POP		AF
  637C  C9            			RET
                      	
  637D                	STSV2:                           ;ファイルネームの取得に失敗
  637D  215565        			LD		HL,MSG_FNAME
  6380  185F          			JR		ERRMSG
                      	
                      	;**** ファイルネーム送信(IN:HL ファイルネームの先頭) ******
  6382  0620          	STFS:	LD		B,20H
  6384  7E            	STFS1:	LD		A,(HL)           ;FNAME送信
  6385  FE22          			CP		22H
  6387  2003          			JR		NZ,STFS2
  6389  23            			INC		HL
  638A  18F8          			JR		STFS1
  638C  CD3E60        	STFS2:	CALL	SNDBYTE
  638F  23            			INC		HL
  6390  05            			DEC		B
  6391  20F1          			JR		NZ,STFS1
  6393  3E0D          			LD		A,0DH
  6395  CD3E60        			CALL	SNDBYTE
  6398  CD2B60        			CALL	RCVBYTE          ;状態取得(00H=OK)
  639B  C9            			RET
                      	
                      	;************** エラー内容表示 *****************************
  639C                	SDERR:
  639C  F5            			PUSH	AF
  639D  FEF0          			CP		0F0H
  639F  2005          			JR		NZ,ERR3
  63A1  214D66        			LD		HL,MSG_F0        ;SD-CARD INITIALIZE ERROR
  63A4  183B          			JR		ERRMSG
  63A6  FEF1          	ERR3:	CP		0F1H
  63A8  2005          			JR		NZ,ERR4
  63AA  216666        			LD		HL,MSG_F1        ;NOT FIND FILE
  63AD  1832          			JR		ERRMSG
  63AF  FEF3          	ERR4:	CP		0F3H
  63B1  2005          			JR		NZ,ERR5
  63B3  218466        			LD		HL,MSG_F3        ;FILE EXIST
  63B6  1829          			JR		ERRMSG
  63B8  FEF4          	ERR5:	CP		0F4H
  63BA  2005          			JR		NZ,ERR6
  63BC  216765        			LD		HL,MSG_CMD       ;COMMAND FAILED
  63BF  1820          			JR		ERRMSG
  63C1  FEF5          	ERR6:	CP		0F5H
  63C3  2005          			JR		NZ,ERR7
  63C5  218F66        			LD		HL,MSG_F5        ;NO BASIC PROGRAM
  63C8  1817          			JR		ERRMSG
  63CA  FEF6          	ERR7:	CP		0F6H
  63CC  2005          			JR		NZ,ERR99
  63CE  215565        			LD		HL,MSG_FNAME     ;PARAMETER FAILED
  63D1  180E          			JR		ERRMSG
  63D3  CD835E        	ERR99:	CALL	MONBHX
  63D6  7A            			LD		A,D
  63D7  CD5702        			CALL	CONOUT
  63DA  7B            			LD		A,E
  63DB  CD5702        			CALL	CONOUT
  63DE  21AE66        			LD		HL,MSG99         ;その他ERROR
  63E1  CDED52        	ERRMSG:	CALL	MSGOUT
  63E4  CDCA5F        			CALL	MONCLF
  63E7  F1            			POP		AF
  63E8  C9            			RET
                      	
                      	;************** BASIC CMT LOAD *********************
  63E9                	CMDLOAD:
  63E9  2B            			DEC		HL
  63EA  3E73          			LD		A,73H            ;コマンド73Hを送信
  63EC  CDB762        			CALL	STCMD
  63EF  205E          			JR		NZ,CMDLD8
                      	
  63F1  211801        			LD		HL,0118H         ;1文字目、24行目へカーソルを移動
  63F4  CDA903        			CALL	CSR
  63F7  CDCA5F        			CALL	MONCLF
                      			
  63FA  CD2B60        			CALL	RCVBYTE          ;ヘッダー受信
  63FD  FED3          			CP		0D3H             ;D3Hでなければエラー
  63FF  280A          			JR		Z,CMDLD
  6401  219C66        			LD		HL,MSG_F6        ;NOT BASIC PROGRAM
  6404  CDED52        			CALL	MSGOUT
  6407  CDCA5F        			CALL	MONCLF
  640A  C9            			RET
                      			
  640B                	CMDLD:	
  640B  213EEF        			LD		HL,FILNAM        ;CMTファイル中に記載のファイルネーム6文字を受信
  640E  0606          			LD		B,06H
  6410  CD2B60        	CMDLD2:	CALL	RCVBYTE
  6413  77            			LD		(HL),A
  6414  23            			INC		HL
  6415  10F9          			DJNZ	CMDLD2
                      	
  6417  21CA64        			LD		HL,MSG_LD        ;LOADING表示
  641A  CDED52        			CALL	MSGOUT
                      	
  641D  213EEF        			LD		HL,FILNAM        ;ファイルネーム表示
  6420  1196EC        			LD		DE,IBUF
  6423  010600        			LD		BC,0006H
  6426  EDB0          			LDIR
  6428  AF            			XOR		A
  6429  12            			LD		(DE),A
  642A  2196EC        			LD		HL,IBUF
  642D  CDED52        			CALL	MSGOUT
  6430  CDCA5F        			CALL	MONCLF
                      			
  6433  2A54EB        			LD		HL,(BASSRT)      ;BASICプログラム格納開始位置をHLに設定
                      	
  6436  060C          	CMDLD3:	LD		B,0CH            ;00Hを12個連続で受信するまでループ
  6438  CD2B60        	CMDLD4:	CALL	RCVBYTE
  643B  77            			LD		(HL),A
  643C  23            			INC		HL
                      	
  643D  A7            			AND		A
  643E  2802          			JR		Z,CMDLD5
  6440  18F4          			JR		CMDLD3
  6442  05            	CMDLD5:	DEC		B
  6443  2802          			JR		Z,CMDLD6
  6445  18F1          			JR		CMDLD4
                      	
  6447  010700        	CMDLD6:	LD		BC,0007H         ;HLの位置を7つ戻してBASICプログラム終了位置とする
  644A  ED42          			SBC		HL,BC
                      			
  644C  C38B1F        			JP		AFTLOAD          ;BASICプログラムLOAD後処理
  644F                	CMDLD8:
  644F  C9            			RET
                      			
                      	;********************** BASIC CMT SAVE **********************
  6450                	CMDSAVE:
  6450  E5            			PUSH	HL
  6451  2A54EB        			LD		HL,(BASSRT)      ;BASICプログラム格納開始位置をHLに設定
  6454  7E            			LD		A,(HL)
  6455  23            			INC		HL
  6456  B6            			OR		(HL)
  6457  E1            			POP		HL
  6458  3EF5          			LD		A,0F5H           ;BASICプログラムが1行もなければエラー
  645A  CA9C63        			JP		Z,SDERR
                      	
  645D  2B            			DEC		HL
  645E  3E74          			LD		A,74H            ;コマンド74Hを送信
  6460  CDB762        			CALL	STCMD
  6463  2030          			JR		NZ,CMDSV3
                      	
  6465  21D964        			LD		HL,MSG_WR        ;WRITING表示
  6468  CDED52        			CALL	MSGOUT
                      	
  646B  CD865B        			CALL	RENUM9           ;BASICプログラムSAVE前処理
  646E  2A54EB        			LD		HL,(BASSRT)      ;BASSRTからVARBGN-1までを送信
  6471  7D            			LD		A,L
  6472  CD3E60        			CALL	SNDBYTE
  6475  7C            			LD		A,H
  6476  CD3E60        			CALL	SNDBYTE
  6479  ED5BA0EF      			LD		DE,(VARBGN)
  647D  1B            			DEC		DE
  647E  7B            			LD		A,E
  647F  CD3E60        			CALL	SNDBYTE
  6482  7A            			LD		A,D
  6483  CD3E60        			CALL	SNDBYTE
  6486  7E            	CMDSV1:	LD		A,(HL)
  6487  CD3E60        			CALL	SNDBYTE
  648A  7C            			LD		A,H
  648B  BA            			CP		D
  648C  2004          			JR		NZ,CMDSV2
  648E  7D            			LD		A,L
  648F  BB            			CP		E
  6490  2803          			JR		Z,CMDSV3         ;HL = DE までLOOP
  6492  23            	CMDSV2:	INC		HL
  6493  18F1          			JR		CMDSV1
                      			
  6495                	CMDSV3:
  6495  21D364        			LD		HL,MSG_OK        ;OK表示
  6498  CDED52        			CALL	MSGOUT
  649B  C9            			RET
                      	
                      	;************ BASIC FILES ********************
  649C                	CMDFILES:
                      	;2022.7.24 SYNTAX ERROR回避により、廃止
                      	;		PUSH	HL
                      	;		LD		HL,FMSG          ;FILESの後ろにファイル名検索文字を指定すると構文エラーになるため、別行として入力
                      	;		CALL	MSGOUT
                      	;		CALL	LINPUT
                      	;		INC		HL
  649C  CD6E63        			CALL	STFN             ;入力文字列取得
  649F  EB            			EX		DE,HL
  64A0  21BB66        			LD		HL,DEFDIR2       ;行頭に'LOAD 'を付けることでカーソルを移動させリターンで実行できるように
  64A3  010600        			LD		BC,D2END-DEFDIR2
  64A6  CDA361        			CALL	DIRLIST          ;DIRLIST本体をコール
                      	;2022.7.24 SYNTAX ERROR回避
  64A9  21C964        			LD		HL,FMSG         ;SYNTAX ERROR回避
  64AC  CDED52        			CALL	MSGOUT
                      	;		POP		HL
  64AF  C9            			RET
                      			
                      	;************ BASIC KILL ***************************
  64B0                	CMDKILL:
  64B0  3E75          			LD		A,75H            ;コマンド75Hを送信
  64B2  CDB762        			CALL	STCMD
  64B5  2011          			JR		NZ,CMDKL4        ;ファイル名が送信できなかった。
  64B7  CD2B60        			CALL	RCVBYTE
  64BA  A7            			AND		A
  64BB  2008          			JR		NZ,CMDKL3        ;ファイルが存在しない
  64BD  21E264        			LD		HL,MSG_KL
  64C0  CDED52        			CALL	MSGOUT
  64C3  1803          			JR		CMDKL4
  64C5  CD9C63        	CMDKL3:	CALL	SDERR
  64C8                	CMDKL4:
  64C8  C9            			RET
                      			
                      	;FMSG	DEFB	'FILE SEARCH:'
  64C9  00            	FMSG:	DEFB	00H
                      	
  64CA                	MSG_LD:
  64CA  4C4F4144494E47			DB		'LOADING '
  64D2  00            			DB		00H
                      	
  64D3                	MSG_OK:
  64D3  4F4B21        			DB		'OK!'
  64D6  0D0A00        			DB		0DH,0AH,00H
                      	
  64D9                	MSG_WR:
  64D9  57524954494E47			DB		'WRITING '
  64E1  00            			DB		00H
                      	
  64E2                	MSG_KL:
  64E2  46494C45204445			DB		'FILE DELETED!'
  64EF  0D0A00        			DB		0DH,0AH,00H
                      	
  64F2                	MSG_AD1:
  64F2  41445253202B30			DB		'ADRS +0 +1 +2 +3 +4 +5 +6 +7 01234567'
  6517  0D0A00        			DB		0DH,0AH,00H
                      			
  651A                	MSG_AD2:
  651A  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:ESC'
  6533  0D0A00        			DB		0DH,0AH,00H
                      			
  6536                	MSG_KEY1:
  6536  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:'
  654C  00            			DB		00H
  654D                	MSG_KEY2:
  654D  204F5220455343			DB		' OR ESC'
  6554  00            			DB		00H
                      	
  6555                	MSG_FNAME:
  6555  504152414D4554			DB		'PARAMETAR FAILED!'
  6566  00            			DB		00H
                      			
  6567                	MSG_CMD:
  6567  434F4D4D414E44			DB		'COMMAND FAILED!',0DH,0AH
  6578  20422020202020			DB		' B             : Return Basic',0DH,0AH
  6597  2044206E6E6E6E			DB		' D nnnn        : Memory Dump',0DH,0AH
  65B5  20462078202020			DB		' F x           : Find SD File',0DH,0AH
  65D4  2047206E6E6E6E			DB		' G nnnn        : Exec Program',0DH,0AH
  65F3  204C2078202020			DB		' L x           : Load From SD',0DH,0AH
  6612  2053206E6E6E6E			DB		' S nnnn nn..   : Memory Write',0DH,0AH
  6631  2057206E6E6E6E			DB		' W nnnn nnnn x : Save To SD'
  664C  00            			DB		00H
                      			
  664D                	MSG_F0:
  664D  53442D43415244			DB		'SD-CARD INITIALIZE ERROR'
  6665  00            			DB		00H
                      			
  6666                	MSG_F1:
  6666  4E4F542046494E			DB		'NOT FIND FILE'
  6673  00            			DB		00H
                      			
  6674                	MSG_F2:
  6674  4E4F54204F424A			DB		'NOT OBJECT FILE'
  6683  00            			DB		00H
                      			
  6684                	MSG_F3:
  6684  46494C45204558			DB		'FILE EXIST'
  668E  00            			DB		00H
                      			
  668F                	MSG_F5:
  668F  4E4F2050524F47			DB		'NO PROGRAM!!'
  669B  00            			DB		00H
                      			
  669C                	MSG_F6:
  669C  4E4F5420424153			DB		'NOT BASIC PROGRAM'
  66AD  00            			DB		00H
                      			
  66AE                	MSG99:
  66AE  204552524F52  			DB		' ERROR'
  66B4  00            			DB		00H
                      			
  66B5  2A53          	MSG_FDW:	DB		'*S'
  66B7  00            			DB		00H
                      	
  66B8                	DEFDIR:
  66B8  2A4C20        			DB		'*L '
  66BB                	DEND:
                      	
  66BB                	DEFDIR2:
  66BB  4C4F41442022  			DB		'LOAD ',22H
  66C1                	D2END:
                      	
                      	
  7FFC                			ORG		7FFCH
                      	
                      	;************* 代替MONITORへジャンプするための設定 *************
  7FFC  C36E60        			JP		MONINI
  7FFF  55            			DB		55H
                      			
  8000                			END
