			  Z80 ASSEMBLER - ZASM VER 1.6
                      	;2022.7.24 FILESでSYNTAX ERRORとなる事象を回避、SERCH FILEと聞いてくる仕様を削除
                      	;2022.7.25 FILESを↑キーで打ち切ったとき時々Syntax Errorが発生するため2行戻す動きを廃止
                      	;2022.8.4  BASIC戻り時のSyntax Error対策を修正
                      	;2022.8.14 MONITOR GコマンドでPOP BCが抜けていたので追加。
                      	;          mk2用と統合
                      	;            拡張ROM認識コードを041H,042H,00Hとした。
                      	;            SHIFTキーを押しながら起動することで拡張ROMを有効にするか選択できるようにした。(mk2用)
                      	;            mk2においてもfiles、save、killコマンドでSyntaxErrorが出ないように修正
                      	;2022.8.21 MONITOR Lコマンドで読み込み時にファンクションキーエリアであればCTRL+B、CLOADをSD用に書き換えるようにした
                      	
  0257                	CONOUT		EQU		0257H		;CRTへの1バイト出力
  0350                	DISPBL		EQU		0350H		;ベルコードの出力
  03A9                	CSR			EQU		03A9H		;カーソルの移動
  03F3                	CSRADR		EQU		03F3H		;キャラクタ座標->VRAMアドレス変換
  0BE2                	DSPCSR		EQU		0BE2H		;カーソル表示の開始
  0FAC                	KYSCAN		EQU		0FACH		;リアルタイム・キーボード・スキャニング
  1B7E                	LINPUT		EQU		1B7EH		;スクリーン・エディタ
  1F8B                	AFTLOAD		EQU		1F8BH		;BASICテキストの終了アドレス設定
  40A6                	CHROUT		EQU		40A6H		;デバイスへの1バイト出力
  52ED                	MSGOUT		EQU		52EDH		;文字列の出力2
  5B86                	RENUM9		EQU		5B86H		;BASICプログラムSAVE前処理
  5C3C                	MONBGN		EQU		5C3CH		;MONITOR開始アドレス
  5E39                	HEXCHK		EQU		5E39H		;16進コード・チェック
  5E4B                	BINCV4		EQU		5E4BH		;16進コードからバイナリ形式への変換
  5E83                	MONBHX		EQU		5E83H		;8ビット数値から16進コードへの変換
  5EBD                	MONDME		EQU		5EBDH		;16進データ2桁表示
  5EC0                	MONDHL		EQU		5EC0H		;16進4桁表示
  5FC1                	AZLCNV		EQU		5FC1H		;小文字->大文字変換
  5FCA                	MONCLF		EQU		5FCAH		;CRコード及びLFコードの表示
  5FD4                	MONSPC		EQU		5FD4H		;スペースの表示
                      	
  EA63                	CUSPOS		EQU		0EA63H		;カーソル位置
  EAC0                	FKDEF		EQU		0EAC0H		;AUTO START キー定義場所初期値
  EB54                	BASSRT		EQU		0EB54H		;プログラムテキスト開始位置
  EC96                	IBUF		EQU		0EC96H		;キー入力バッファ
  EDC0                	FKPINT		EQU		0EDC0H		;キーポインタ
  EF3E                	FILNAM		EQU		0EF3EH		;現在ロード中ファイルネーム
  EFA0                	VARBGN		EQU		0EFA0H		;変数領域の始まりアドレス
  F139                	TBLOAD		EQU		0F139H		;LOADコマンドジャンプアドレス
  F142                	TBKILL		EQU		0F142H		;KILLコマンドジャンプアドレス
  F14B                	TBSAVE		EQU		0F14BH		;SAVEコマンドジャンプアドレス
  F14E                	TBFILES		EQU		0F14EH		;FILESコマンドジャンプアドレス
  FF34                	MONHL		EQU		0FF34H
  FF36                	MONSP		EQU		0FF36H
  FF3D                	SADRS		EQU		0FF3DH
  FF3F                	EADRS		EQU		0FF3FH
  FF40                	ACFLGD		EQU		0FF40H      ;オートラン機能ダブルコーテーション検出フラグ
  FF41                	ACFLGC		EQU		0FF41H      ;オートラン機能CLOAD検出フラグ
  FF66                	LBUF		EQU		0FF66H
                      	
                      	;PC-8001
  00FC                	PPI_A		EQU		0FCH
                      	;PC-8001mk2
                      	;PPI_A		EQU		07CH
                      	
  00FD                	PPI_B		EQU		PPI_A+1
  00FE                	PPI_C		EQU		PPI_A+2
  00FF                	PPI_R		EQU		PPI_A+3
                      	
                      	;PC-8001
                      	;8255 PORT アドレス FCH〜FFH
                      	;0FCH PORTA 送信データ(下位4ビット)
                      	;0FDH PORTB 受信データ(8ビット)
                      	;PC-8001mk2
                      	;8255 PORT アドレス 7CH〜7FH
                      	;7CH PORTA 送信データ(下位4ビット)
                      	;7DH PORTB 受信データ(8ビット)
                      	
                      	;
                      	;PC-8001
                      	;0FEH PORTC Bit
                      	;PC-8001mk2
                      	;7EH PORTC Bit
                      	
                      	;7 IN  CHK
                      	;6 IN
                      	;5 IN
                      	;4 IN 
                      	;3 OUT
                      	;2 OUT FLG
                      	;1 OUT
                      	;0 OUT
                      	;
                      	;PC-8001
                      	;0FFH コントロールレジスタ
                      	;PC-8001mk2
                      	;7FH コントロールレジスタ
                      	
                      	
                      	
  6000                	        ORG		6000H
                      	
  6000  4142          			DB		041H,042H         ;拡張ROM認識コード
                      	
  6002  00            			NOP
  6003  180A          			JR		INIT              ;POWER ONで8255を初期化、LOAD、SAVE、FILES、KILLのジャンプ先を設定
  6005  00            			NOP
                      	;PC-8001mk2の作法に統一
                      	;		CALL	INIT              ;POWER ONで8255を初期化、LOAD、SAVE、FILES、KILLのジャンプ先を設定
                      	;		RET
                      	
                      	;*********** OPENしているファイルから1BYTE読み出し転送 ****************
                      	;5F9EH代替ルーチン
  6006  C38563        			JP		D_5F9E
                      	
                      	;*********** CMT LOAD 直呼び出し (EMI PLAYER用) *****************************
                      	;5F3AH代替ルーチン
  6009  C3A363        			JP		D_5F3A
                      	
                      	;*********** MONITOR HOT START  (EMI PLAYER用) ********************
                      	;5C66H代替ルーチン
  600C  C3CE60        			JP		MONHOT
                      			
                      	;**** 8255初期化 ****
                      	;PORTC下位BITをOUTPUT、上位BITをINPUT、PORTBをINPUT、PORTAをOUTPUT
  600F  3E8A          	INIT:	LD		A,8AH
  6011  D3FF          			OUT		(PPI_R),A
                      	;出力BITをリセット
  6013  AF            			XOR		A                 ;PORTA <- 0
  6014  D3FC          			OUT		(PPI_A),A
  6016  D3FE          			OUT		(PPI_C),A         ;PORTC <- 0
                      	
  6018  DB08          			IN		A,(08H)
  601A  E640          			AND		40H
                      	;キースキャンしてSHIFTきーが押されていなければ通常起動
                      	;		RET		NZ
                      	
                      	;キースキャンしてSHIFTきーが押されていなければ拡張ROM起動
  601C  C8            			RET		Z
                      	
                      	;LOAD、SAVE、FILES、KILLのジャンプ先を設定
  601D  21C364        	INI2:	LD		HL,CMDLOAD
  6020  2239F1        			LD		(TBLOAD),HL
                      	
  6023  212B65        			LD		HL,CMDSAVE
  6026  224BF1        			LD		(TBSAVE),HL
                      	
  6029  217E65        			LD		HL,CMDFILES
  602C  224EF1        			LD		(TBFILES),HL
                      	
  602F  218D65        			LD		HL,CMDKILL
  6032  2242F1        			LD		(TBKILL),HL
                      	
  6035  C9            	INI3:	RET
                      	
                      	;**** 1BYTE受信 ****
                      	;受信DATAをAレジスタにセットしてリターン
  6036                	RCVBYTE:
  6036  CD6B60        			CALL	F1CHK             ;PORTC BIT7が1になるまでLOOP
  6039  DBFD          			IN		A,(PPI_B)         ;PORTB -> A
  603B  F5            			PUSH 	AF
  603C  3E05          			LD		A,05H
  603E  D3FF          			OUT		(PPI_R),A         ;PORTC BIT2 <- 1
  6040  CD7260        			CALL	F2CHK             ;PORTC BIT7が0になるまでLOOP
  6043  3E04          			LD		A,04H
  6045  D3FF          			OUT		(PPI_R),A         ;PORTC BIT2 <- 0
  6047  F1            			POP 	AF
  6048  C9            			RET
                      			
                      	;**** 1BYTE送信 ****
                      	;Aレジスタの内容をPORTA下位4BITに4BITずつ送信
  6049                	SNDBYTE:
  6049  F5            			PUSH	AF
  604A  1F            			RRA
  604B  1F            			RRA
  604C  1F            			RRA
  604D  1F            			RRA
  604E  E60F          			AND		0FH
  6050  CD5A60        			CALL	SND4BIT
  6053  F1            			POP		AF
  6054  E60F          			AND		0FH
  6056  CD5A60        			CALL	SND4BIT
  6059  C9            			RET
                      	
                      	;**** 4BIT送信 ****
                      	;Aレジスタ下位4ビットを送信する
  605A                	SND4BIT:
  605A  D3FC          			OUT		(PPI_A),A
  605C  3E05          			LD		A,05H
  605E  D3FF          			OUT		(PPI_R),A          ;PORTC BIT2 <- 1
  6060  CD6B60        			CALL	F1CHK             ;PORTC BIT7が1になるまでLOOP
  6063  3E04          			LD		A,04H
  6065  D3FF          			OUT		(PPI_R),A          ;PORTC BIT2 <- 0
  6067  CD7260        			CALL	F2CHK
  606A  C9            			RET
                      			
                      	;**** BUSYをCHECK(1) ****
                      	; 82H BIT7が1になるまでLOP
  606B  DBFE          	F1CHK:	IN		A,(PPI_C)
  606D  E680          			AND		80H               ;PORTC BIT7 = 1?
  606F  28FA          			JR		Z,F1CHK
  6071  C9            			RET
                      	
                      	;**** BUSYをCHECK(0) ****
                      	; 82H BIT7が0になるまでLOOP
  6072  DBFE          	F2CHK:	IN		A,(PPI_C)
  6074  E680          			AND		80H               ;PORTC BIT7 = 0?
  6076  20FA          			JR		NZ,F2CHK
  6078  C9            			RET
                      	
                      	;************* 代替MONITOR START *************************
  6079  2234FF        	MONINI:	LD		(MONHL),HL
  607C  ED7336FF      			LD		(MONSP),SP
                      			
                      	;************ (TBLOAD)にCMDLOADがセットされていなければMONBGNへジャンプして通常MONITOR起動 ***************
  6080  2A39F1        			LD		HL,(TBLOAD)
  6083  11C364        			LD		DE,CMDLOAD
  6086  ED52          			SBC		HL,DE
  6088  C23C5C        			JP		NZ,MONBGN
                      	
                      	;		PUSH	HL                ;タイトルを表示するとBASICから戻ったときに自動実行が途切れてしまうため削除
                      	;		LD		HL,MONMSG
                      	;		CALL	MSGOUT
                      	;		POP		HL
                      			
  608B  01EC60        	CMD0:	LD		BC,MONERR         ;BASICに戻るときPOPしているのでお約束らしい
  608E  C5            			PUSH	BC
  608F                	CMD1:
  608F  3E2A          			LD		A,'*'
  6091  CD5702        			CALL	CONOUT            ;プロンプト表示
  6094  CDE20B        			CALL	DSPCSR
  6097  CD7E1B        			CALL	LINPUT            ;スクリーンエディットを使いたいために使用、このためCTRL+BではBASICに戻れない
                      									  ;HLにIBUF-1が入って戻っている LD HL,IBUF-1
  609A                	CMD2:
  609A  383B          			JR		C,MONCTB          ;CTRL+Bの代替、CTRL+CでBASICに復帰
  609C  23            			INC		HL
  609D  7E            			LD		A,(HL)
  609E  FE2A          			CP		'*'               ;入力時とスクリーンエディット時でIBUFに入る位置が変わるための対処、「*」ならポインタを進める
  60A0  28F8          			JR		Z,CMD2
  60A2  7E            			LD		A,(HL)
  60A3  CDC15F        			CALL	AZLCNV            ;入力文字を大文字へ変換
  60A6  FE42          			CP		'B'
  60A8  282D          			JR		Z,MONCTB          ;CTRL+Bの代替、BコマンドでもBASICに復帰
  60AA  FE44          			CP		'D'
  60AC  CAF460        			JP		Z,STMD            ;Dコマンド
  60AF  FE53          			CP		'S'
  60B1  CA7061        			JP		Z,STMW            ;Sコマンド
  60B4  FE46          			CP		'F'
  60B6  CAB161        			JP		Z,STLT            ;Fコマンド
  60B9  FE4C          			CP		'L'
  60BB  CA6762        			JP		Z,MONLOAD         ;Lコマンド
  60BE  FE57          			CP		'W'
  60C0  CAAF63        			JP		Z,MONSAVE         ;Wコマンド
  60C3  FE47          			CP		'G'
  60C5  2816          			JR		Z,GOCMD           ;Gコマンド
  60C7  3EF4          			LD		A,0F4H
  60C9  CD7C64        			CALL	SDERR             ;コマンドエラー
  60CC  18C1          			JR		CMD1
                      			
                      	;MONMSG:	DEFB	0CH,'**PC-8001_SD Monitor **',0DH,0AH,00H
                      	
                      	;*********** MONITOR HOT START  (EMI PLAYER用)  *******************************
                      	;5C66H代替ルーチン
  60CE                	MONHOT:
  60CE  ED7B36FF      			LD		SP,(MONSP)
  60D2  CDCA5F        			CALL	MONCLF
  60D5  18B4          			JR		CMD0
                      	
                      	;************ Bコマンド BASIC復帰 ***********************
  60D7                	MONCTB:
                      	;***** 2022.8.10 BUG修正 ***********
  60D7  C1            			POP		BC
                      	;***********************************
  60D8  2A34FF        			LD		HL,(MONHL)
  60DB  FB            			EI
  60DC  C9            			RET
                      	
                      	;************ Gコマンド アドレスxxxxへジャンプ ***************
  60DD  23            	GOCMD:	INC		HL
  60DE  CD4E64        			CALL	STFN
  60E1  CD1C64        			CALL	HLHEX             ;4桁の16進数であればHLにセットして続行
  60E4  DAEC60        			JP		C,MONERR
  60E7  CDCA5F        			CALL	MONCLF
  60EA  C1            			POP		BC
  60EB  E9            			JP		(HL)              ;HLの示すアドレスにジャンプ
                      	
                      	;パラメータエラー処理
  60EC  3EF6          	MONERR:	LD		A,0F6H
  60EE  CD7C64        			CALL	SDERR
  60F1  C38F60        			JP		CMD1
                      	
                      	;************ Dコマンド アドレスxxxxからのMEMORYをDUMP **********************
  60F4  23            	STMD:	INC		HL
  60F5  CD4E64        			CALL	STFN
  60F8  CD1C64        			CALL	HLHEX             ;4桁の16進数であればSADRSにセットして続行
  60FB  DAEC60        			JP		C,MONERR
  60FE  223DFF        			LD		(SADRS),HL        ;SARDS保存
                      	
  6101  21D965        	STMD6:	LD		HL,MSG_AD1        ;DUMP TITLE表示
  6104  CDED52        			CALL	MSGOUT
  6107  0E10          			LD		C,10H             ;16行(128Byte)を表示
  6109  2A3DFF        	STMD7:	LD		HL,(SADRS)        ;アドレス表示
  610C  CDC05E        			CALL	MONDHL
  610F  CDD45F        			CALL	MONSPC
                      	
                      			
  6112  0608          			LD		B,08H             ;一行(8Byte)のデータを16進数表示
  6114  CDBD5E        	STMD0:	CALL	MONDME
  6117  CDD45F        			CALL	MONSPC
  611A  CDAC0F        			CALL	KYSCAN
  611D  FE1B          			CP		1BH               ;表示途中でもESCで打ち切り
  611F  284C          			JR		Z,STMD4
  6121  23            			INC		HL
  6122  05            			DEC		B
  6123  20EF          			JR		NZ,STMD0
                      	
  6125  2A3DFF        			LD		HL,(SADRS)
  6128  0608          			LD		B,08H             ;一行(8Byte)のデータをキャラクタ表示
  612A  7E            	STMD2:	LD		A,(HL)
  612B  FE20          			CP		20H               ;文字化けに対処 20H未満なら20Hに置き換え
  612D  3002          			JR		NC,STMD8
  612F  3E20          			LD		A,20H
  6131  CD5702        	STMD8:	CALL	CONOUT
  6134  CDAC0F        			CALL	KYSCAN
  6137  FE1B          			CP		1BH               ;表示途中でもESCで打ち切り
  6139  2832          			JR		Z,STMD4
  613B  23            			INC		HL
  613C  05            			DEC		B
  613D  20EB          			JR		NZ,STMD2
                      	
  613F  223DFF        			LD		(SADRS),HL
  6142  CDCA5F        			CALL	MONCLF
                      	
  6145  0D            			DEC		C
  6146  20C1          			JR		NZ,STMD7
                      			
  6148  210166        			LD		HL,MSG_AD2        ;入力待ちメッセージ表示
  614B  CDED52        			CALL	MSGOUT
  614E  CDAC0F        	STMD3:	CALL	KYSCAN            ;1文字入力待ち
  6151  A7            			AND		A
  6152  28FA          			JR		Z,STMD3
  6154  FE1B          			CP		1BH               ;ESCで打ち切り
  6156  2815          			JR		Z,STMD4
  6158  CDC15F        			CALL	AZLCNV
  615B  FE42          			CP		42H               ;BからBACK
  615D  200B          			JR		NZ,STMD5
  615F  2A3DFF        			LD		HL,(SADRS)
  6162  110001        			LD		DE,0100H
  6165  ED52          			SBC		HL,DE
  6167  223DFF        			LD		(SADRS),HL
  616A  C30161        	STMD5:	JP		STMD6
  616D  C38F60        	STMD4:	JP		CMD1
                      	
                      	;************ Sコマンド アドレスxxxxからMEMORYに書き込み **********************
  6170  23            	STMW:	INC		HL
  6171  CD4E64        			CALL	STFN
  6174  CD1C64        			CALL	HLHEX             ;4桁の16進数であればHLにセットして続行
  6177  DAEC60        			JP		C,MONERR
                      	
  617A  1A            	STSP1:	LD		A,(DE)
  617B  A7            			AND		A
  617C  2830          			JR		Z,STMW9           ;アドレスのみなら終了
  617E  FE20          			CP		20H
  6180  2003          			JR		NZ,STMW1
  6182  13            			INC		DE                ;空白は飛ばす
  6183  18F5          			JR		STSP1
                      	
  6185                	STMW1:
  6185  CD3464        			CALL	TWOHEX
  6188  380D          			JR		C,STMW8
  618A  77            			LD		(HL),A            ;2桁の16進数があれば(HL)に書き込み
  618B  23            			INC		HL
                      	
  618C  1A            	STSP2:	LD		A,(DE)
  618D  A7            			AND		A               ;一行終了
  618E  2807          			JR		Z,STMW8
  6190  FE20          			CP		20H
  6192  20F1          			JR		NZ,STMW1
  6194  13            			INC		DE                ;空白は飛ばす
  6195  18F5          			JR		STSP2
                      	
  6197  E5            	STMW8:	PUSH	HL
  6198  219E67        			LD		HL,MSG_FDW        ;行頭に'*S '
  619B  CDED52        			CALL	MSGOUT
  619E  E1            			POP		HL
  619F  E5            			PUSH	HL
  61A0  CDC05E        			CALL	MONDHL            ;アドレス表示
  61A3  CDD45F        			CALL	MONSPC
  61A6  CD7E1B        			CALL	LINPUT            ;行入力繰り返し
                      	
  61A9  D1            			POP		DE
  61AA  23            			INC		HL
  61AB  EB            			EX		DE,HL
  61AC  18CC          			JR		STSP1
  61AE  C38F60        	STMW9:	JP		CMD1
                      	
                      	;************ Fコマンド DIRLIST **********************
  61B1  23            	STLT:	INC		HL
  61B2  CD4E64        			CALL	STFN              ;検索文字列を送信
  61B5  EB            			EX		DE,HL
  61B6  21A167        			LD		HL,DEFDIR         ;行頭に'*L 'を付けることでカーソルを移動させリターンで実行できるように
  61B9  010300        			LD		BC,DEND-DEFDIR
  61BC  CDC661        			CALL	DIRLIST           ;DIRLIST本体をコール
  61BF  A7            			AND		A                 ;00以外ならERROR
  61C0  C47C64        			CALL	NZ,SDERR
  61C3  C38F60        			JP		CMD1
                      	
                      	
                      	;**** DIRLIST本体 (HL=行頭に付加する文字列の先頭アドレス BC=行頭に付加する文字列の長さ) ****
                      	;****              戻り値 A=エラーコード ****
  61C6                	DIRLIST:
  61C6  3E83          			LD		A,83H             ;DIRLISTコマンド83Hを送信
  61C8  CD1564        			CALL	STCD              ;コマンドコード送信
  61CB  A7            			AND		A                 ;00以外ならERROR
  61CC  C26662        			JP		NZ,DLRET
                      			
  61CF  C5            			PUSH	BC
  61D0  0621          			LD		B,21H             ;ファイルネーム検索文字列33文字分を送信
  61D2  1A            	STLT1:	LD		A,(DE)
  61D3  A7            			AND		A
  61D4  2001          			JR		NZ,STLT2
  61D6  AF            			XOR		A
  61D7  CDC15F        	STLT2:	CALL	AZLCNV            ;大文字に変換
  61DA  FE22          			CP		22H               ;ダブルコーテーション読み飛ばし
  61DC  2003          			JR		NZ,STLT3
  61DE  13            			INC		DE
  61DF  18F1          			JR		STLT1
  61E1  CD4960        	STLT3:	CALL	SNDBYTE           ;ファイルネーム検索文字列を送信
  61E4  13            			INC		DE
  61E5  05            			DEC		B
  61E6  20EA          			JR		NZ,STLT1
  61E8  C1            			POP		BC
                      	
  61E9  CD3660        			CALL	RCVBYTE           ;状態取得(00H=OK)
  61EC  A7            			AND		A                 ;00以外ならERROR
  61ED  C26662        			JP		NZ,DLRET
                      	
  61F0                	DL1:
  61F0  E5            			PUSH	HL
  61F1  C5            			PUSH	BC
  61F2  1166FF        			LD		DE,LBUF
  61F5  EDB0          			LDIR
  61F7  EB            			EX		DE,HL
  61F8  CD3660        	DL2:	CALL	RCVBYTE           ;'00H'を受信するまでを一行とする
  61FB  A7            			AND		A
  61FC  280C          			JR		Z,DL3
  61FE  FEFF          			CP		0FFH              ;'0FFH'を受信したら終了
  6200  281E          			JR		Z,DL4
  6202  FEFE          			CP		0FEH              ;'0FEH'を受信したら一時停止して一文字入力待ち
  6204  2821          			JR		Z,DL5
  6206  77            			LD		(HL),A
  6207  23            			INC		HL
  6208  18EE          			JR		DL2
  620A  3600          	DL3:	LD		(HL),00H
  620C  2166FF        			LD		HL,LBUF           ;'00H'を受信したら一行分を表示して改行
  620F  7E            	DL31:	LD		A,(HL)
  6210  A7            			AND		A
  6211  2806          			JR		Z,DL33
  6213  CDA640        			CALL	CHROUT
  6216  23            			INC		HL
  6217  18F6          			JR		DL31
  6219  CDCA5F        	DL33:	CALL	MONCLF
  621C  C1            			POP		BC
  621D  E1            			POP		HL
  621E  18D0          			JR		DL1
  6220  CD3660        	DL4:	CALL	RCVBYTE           ;状態取得(00H=OK)
  6223  C1            			POP		BC
  6224  E1            			POP		HL
  6225  183F          			JR		DLRET
                      	
  6227  E5            	DL5:	PUSH	HL
  6228  211D66        			LD		HL,MSG_KEY1       ;HIT ANT KEY表示
  622B  CDED52        			CALL	MSGOUT
  622E  2A63EA        			LD		HL,(CUSPOS)
  6231  CDF303        			CALL	CSRADR
  6234  3E1E          			LD		A,1EH
  6236  77            			LD		(HL),A
  6237  3E1C          			LD		A,1CH
  6239  CD5702        			CALL	CONOUT
  623C  213466        			LD		HL,MSG_KEY2       ;HIT ANT KEY表示
  623F  CDED52        			CALL	MSGOUT
  6242  CDCA5F        			CALL	MONCLF
  6245  E1            			POP		HL
  6246  CDAC0F        	DL6:	CALL	KYSCAN            ;1文字入力待ち
  6249  CDC15F        			CALL	AZLCNV
  624C  A7            			AND		A
  624D  28F7          			JR		Z,DL6
  624F  FE1B          			CP		1BH               ;ESCで打ち切り
  6251  280B          			JR		Z,DL7
  6253  FE1E          			CP		1EH               ;カーソル↑で打ち切り
  6255  2807          			JR		Z,DL7
  6257  FE42          			CP		42H               ;「B」で前ページ
  6259  2805          			JR		Z,DL8
  625B  AF            			XOR		A                 ;それ以外で継続
  625C  1802          			JR		DL8
                      	;DL9:
                      	;		LD		A,1EH             ;時々Syntax Errorが発生するため廃止
                      	;		CALL	CONOUT
                      	;		LD		A,1EH
                      	;		CALL	CONOUT            ;カーソル↑で打ち切ったときにカーソル2行上へ
  625E  3EFF          	DL7:	LD		A,0FFH            ;0FFH中断コードを送信
  6260  CD4960        	DL8:	CALL	SNDBYTE
  6263  C3F861        			JP		DL2
                      			
  6266  C9            	DLRET:	RET
                      			
                      	
                      	;************ Lコマンド .CMT LOAD *************************
  6267  3E71          	MONLOAD:LD		A,71H            ;Lコマンド71Hを送信
  6269  CD8E63        			CALL	STCMD
  626C  C28F60        			JP		NZ,CMD1
                      	
  626F  E5            			PUSH	HL
                      	; *********** オートラン書き換え用フラグクリア *** 2022.8.21 ********
  6270  AF            			XOR		A
  6271  2140FF        			LD		HL,ACFLGD
  6274  77            			LD		(HL),A
  6275  23            			INC		HL
  6276  77            			LD		(HL),A
                      	; ********************************************************
  6277  211801        			LD		HL,0118H         ;1文字目、24行目へカーソルを移動
  627A  CDA903        			CALL	CSR
  627D  CDCA5F        			CALL	MONCLF
  6280  E1            			POP		HL
  6281  CD3660        			CALL	RCVBYTE          ;ヘッダー受信
  6284  FE3A          			CP		3AH              ;3AHであれば続行
  6286  280F          			JR		Z,MCNLOAD
  6288  215B67        			LD		HL,MSG_F2        ;3AH以外ならエラー
  628B  CDED52        			CALL	MSGOUT
  628E  CDCA5F        			CALL	MONCLF
  6291  CD5003        			CALL	DISPBL
  6294  C38F60        			JP		CMD1
                      			
  6297  21B365        	MCNLOAD:LD		HL,MSG_LD        ;LOADING表示
  629A  CDED52        			CALL	MSGOUT
                      	
  629D  213EFF        			LD		HL,SADRS+1       ;SADRS取得
  62A0  CD3660        			CALL	RCVBYTE
  62A3  77            			LD		(HL),A
  62A4  2B            			DEC		HL
  62A5  CD3660        			CALL	RCVBYTE
  62A8  77            			LD		(HL),A
  62A9  CD3660        			CALL	RCVBYTE          ;チェックサム廃棄
                      			
  62AC  2A3DFF        			LD		HL,(SADRS)
  62AF  CD3660        	MCLD1:	CALL	RCVBYTE          ;ヘッダー3AH受信、廃棄
  62B2  CD3660        			CALL	RCVBYTE          ;データ長
  62B5  A7            			AND		A
  62B6  280F          			JR		Z,MCLD3          ;データ長が0なら終了
  62B8  47            			LD		B,A
  62B9  CD3660        	MCLD2:	CALL	RCVBYTE          ;実データ受信
                      	; ************* オートラン機能の読み替えを追加 ****** 2022.8.21 ************
                      	;		LD		(HL),A
  62BC  CDD362        			CALL	AUTOCHK
                      	;****************************************************************
  62BF  23            			INC		HL
  62C0  10F7          			DJNZ	MCLD2
  62C2  CD3660        			CALL	RCVBYTE          ;チェックサム廃棄
  62C5  18E8          			JR		MCLD1
  62C7  CD3660        	MCLD3:	CALL	RCVBYTE          ;チェックサム廃棄
  62CA  21BC65        			LD		HL,MSG_OK        ;OK表示
  62CD  CDED52        			CALL	MSGOUT
  62D0  C38F60        			JP		CMD1
                      	
                      	; ************ ファンクションキーエリアへの書込みなら書き換え ** 2022.8.21 **
                      	; HL : 書き込みアドレス
                      	; A  : 書き込みデータ
                      	; CTRL+B -> CTRL+C
                      	; CLOAD  -> LOAD
                      	; "xxxx" -> ""
  62D3  D5            	AUTOCHK:PUSH	DE
  62D4  C5            			PUSH	BC
  62D5  EB            			EX		DE,HL
  62D6  214015        			LD		HL,1540H         ;-(EAC0H)
  62D9  19            			ADD		HL,DE
  62DA  D27C63        			JP		NC,ACRET         ;EAC0H未満なら通常書込み
  62DD  21E414        			LD		HL,14E4H         ;-(EB1BH+1)
  62E0  19            			ADD		HL,DE
  62E1  DA7C63        			JP		C,ACRET          ;EB1CH以上なら通常書込み
  62E4  FE02          			CP		02H
  62E6  2005          			JR		NZ,ACHK1         ;CTRL+B -> CTRL+C
  62E8  3E03          			LD		A,03H
  62EA  C37C63        			JP		ACRET            ;CRTL+Cに書き換えてリターン
                      			
  62ED  47            	ACHK1:	LD		B,A
  62EE  2140FF        			LD		HL,ACFLGD        ;ダブルコーテーションフラグをCHK
  62F1  7E            			LD		A,(HL)
  62F2  A7            			AND		A
  62F3  280E          			JR		Z,ACHK11         ;フラグOFFなら次のCHKへ
  62F5  78            			LD		A,B
  62F6  FE22          			CP		'"'              ;フラグONならダブルコーテーションかCHK
  62F8  2004          			JR		NZ,ACHK10        ;ダブルコーテーションではないなら書き込まずにリターン
  62FA  AF            			XOR		A                ;ダブルコーテーションならフラグをリセット
  62FB  77            			LD		(HL),A
  62FC  187D          			JR		ACHK40           ;ダブルコーテーションを書き込んでリターン
                      	
  62FE  EB            	ACHK10:	EX		DE,HL
  62FF  2B            			DEC		HL               ;フラグONでダブルコーテーションでないなら書き込まない。書き込みアドレスもINCしないでリターン
  6300  C37E63        			JP		ACRET2
                      	
  6303  78            	ACHK11:	LD		A,B
  6304  FE22          			CP		'"'              ;フラグOFFならダブルコーテーションかCHK
  6306  2008          			JR		NZ,ACHK20        ;ダブルコーテーションでないなら次のCHKへ
  6308  3E01          			LD		A,01H            ;ダブルコーテーションならフラグON
  630A  77            			LD		(HL),A
  630B  AF            			XOR		A
  630C  23            			INC		HL
  630D  77            			LD		(HL),A           ;CLOADフラグをリセット
  630E  186B          			JR		ACHK40           ;ダブルコーテーションを書き込んでリターン
                      	
  6310  2141FF        	ACHK20:	LD		HL,ACFLGC        ;CLOADフラグをCHK
  6313  7E            			LD		A,(HL)
  6314  FE00          			CP		00H              ;一致無し
  6316  282D          			JR		Z,ACHK30
  6318  FE01          			CP		01H              ;'C'まで一致
  631A  2837          			JR		Z,ACHK31
  631C  FE02          			CP		02H              ;'L'まで一致
  631E  2841          			JR		Z,ACHK32
  6320  FE03          			CP		03H              ;'0'まで一致
  6322  284B          			JR		Z,ACHK33
  6324  78            			LD		A,B              ;'A'まで一致
  6325  FE44          			CP		'D'
  6327  2804          			JR		Z,ACHK39
  6329  FE64          			CP		'd'
  632B  204E          			JR		NZ,ACHK40
  632D  AF            	ACHK39:	XOR		A                ;'CLOAD'が一致
  632E  77            			LD		(HL),A           ;CLOADフラグをリセット
  632F  EB            			EX		DE,HL            ;書き込みポインタを4つ戻して'LOAD'に上書き
  6330  2B            			DEC		HL
  6331  2B            			DEC		HL
  6332  2B            			DEC		HL
  6333  2B            			DEC		HL
  6334  3E4C          			LD		A,'L'
  6336  77            			LD		(HL),A
  6337  23            			INC		HL
  6338  3E4F          			LD		A,'O'
  633A  77            			LD		(HL),A
  633B  23            			INC		HL
  633C  3E41          			LD		A,'A'
  633E  77            			LD		(HL),A
  633F  23            			INC		HL
  6340  3E44          			LD		A,'D'
  6342  77            			LD		(HL),A
  6343  1839          			JR		ACRET2
                      	
  6345  78            	ACHK30:	LD		A,B
  6346  FE43          			CP		'C'
  6348  2804          			JR		Z,ACHK301
  634A  FE63          			CP		'c'
  634C  2033          			JR		NZ,ACHK41
  634E  3E01          	ACHK301:LD		A,01H            ;'C'が一致
  6350  77            			LD		(HL),A
  6351  1828          			JR		ACHK40
                      	
  6353  78            	ACHK31:	LD		A,B
  6354  FE4C          			CP		'L'
  6356  2804          			JR		Z,ACHK311
  6358  FE6C          			CP		'l'
  635A  2025          			JR		NZ,ACHK41
  635C  3E02          	ACHK311:LD		A,02H            ;'L'が一致
  635E  77            			LD		(HL),A
  635F  181A          			JR		ACHK40
                      	
  6361  78            	ACHK32:	LD		A,B
  6362  FE4F          			CP		'O'
  6364  2804          			JR		Z,ACHK321
  6366  FE6F          			CP		'o'
  6368  2017          			JR		NZ,ACHK41
  636A  3E03          	ACHK321:LD		A,03H            ;'O'が一致
  636C  77            			LD		(HL),A
  636D  180C          			JR		ACHK40
                      	
  636F  78            	ACHK33:	LD		A,B
  6370  FE41          			CP		'A'
  6372  2804          			JR		Z,ACHK331
  6374  FE61          			CP		'a'
  6376  2009          			JR		NZ,ACHK41
  6378  3E04          	ACHK331:LD		A,04H            ;'A'が一致
  637A  77            			LD		(HL),A
                      			
  637B  78            	ACHK40:	LD		A,B
                      	
  637C  EB            	ACRET:	EX		DE,HL
  637D  77            			LD		(HL),A           ;取り敢えず書き込み
  637E  C1            	ACRET2:	POP		BC
  637F  D1            			POP		DE
  6380  C9            			RET
                      	
  6381  AF            	ACHK41:	XOR		A                ;'CLOAD'と一致しなかったのでフラグをリセットして書き込み
  6382  77            			LD		(HL),A
  6383  18F6          			JR		ACHK40
                      	
                      	;********** 5F9EH READ ONE BYTE FROM CMTの代替 *********
  6385  3E72          	D_5F9E:	LD		A,72H            ;コマンド72Hを送信
  6387  CD1564        			CALL	STCD
  638A  CD3660        			CALL	RCVBYTE          ;1Byteのみ受信
  638D  C9            			RET
                      	
                      	;**** コマンド、ファイル名送信 (IN:A コマンドコード HL:ファイルネームの先頭)****
  638E  23            	STCMD:	INC		HL
  638F  CD4E64        			CALL	STFN             ;空白除去
  6392  E5            			PUSH	HL
  6393  CD1564        			CALL	STCD             ;コマンドコード送信
  6396  E1            			POP		HL
  6397  A7            			AND		A                ;00以外ならERROR
  6398  C27C64        			JP		NZ,SDERR
  639B  CD6264        			CALL	STFS             ;ファイルネーム送信
  639E  A7            			AND		A                ;00以外ならERROR
  639F  C27C64        			JP		NZ,SDERR
  63A2  C9            			RET
                      	
                      	;************ 5F3AH READ FROM TAPEの代替 (EMI PLAYER用)*******************
  63A3                	D_5F3A:
                      	;************ 連続してAUTO STARTをするためにフラグポイント再設定 (正解なのかは自信なし)************
  63A3  21C0EA        			LD		HL,FKDEF
  63A6  22C0ED        			LD		(FKPINT),HL
                      	;**************************************************************************************
                      			
                      	;ファイル名指定なしとしてMONITOR Lコマンド実行 *********************
  63A9  21BA65        			LD		HL,DEFCR-1
  63AC  C36762        			JP		MONLOAD
                      	
                      	;************ Wコマンド .CMT SAVE ********************************
  63AF  23            	MONSAVE:INC		HL
  63B0  CD4E64        			CALL	STFN
  63B3  CD1C64        			CALL	HLHEX            ;4桁の16進数であればSADRSにセットして続行
  63B6  DAEC60        			JP		C,MONERR
  63B9  223DFF        			LD		(SADRS),HL       ;SARDS保存
  63BC  EB            			EX		DE,HL
                      	
  63BD  CD4E64        			CALL	STFN
  63C0  CD1C64        			CALL	HLHEX            ;4桁の16進数であればEADRSにセットして続行
  63C3  DAEC60        			JP		C,MONERR
  63C6  223FFF        			LD		(EADRS),HL       ;EARDS保存
                      	
  63C9  D5            			PUSH	DE
  63CA  E5            			PUSH	HL
  63CB  ED5B3DFF      			LD		DE,(SADRS)
  63CF  ED52          			SBC		HL,DE
  63D1  E1            			POP		HL
  63D2  D1            			POP		DE
  63D3  DAEC60        			JP		C,MONERR         ;EADRSがSADRSより小さければエラー
                      	
  63D6  EB            			EX		DE,HL
  63D7  2B            			DEC		HL
                      			
  63D8  3E70          			LD		A,70H            ;コマンド70Hを送信
  63DA  CD8E63        			CALL	STCMD
  63DD  C28F60        			JP		NZ,CMD1
                      	
  63E0  21C265        			LD		HL,MSG_WR        ;WRITING表示
  63E3  CDED52        			CALL	MSGOUT
                      	
  63E6  2A3DFF        			LD		HL,(SADRS)       ;SADRSを送信
  63E9  7D            			LD		A,L
  63EA  CD4960        			CALL	SNDBYTE
  63ED  7C            			LD		A,H
  63EE  CD4960        			CALL	SNDBYTE
  63F1  ED5B3FFF      			LD		DE,(EADRS)       ;送信する全体バイト数を算出するためにEADRSを送信
  63F5  7B            			LD		A,E
  63F6  CD4960        			CALL	SNDBYTE
  63F9  7A            			LD		A,D
  63FA  CD4960        			CALL	SNDBYTE
  63FD  7E            	MONSV1:	LD		A,(HL)           ;SADRSからEADRSまでを送信
  63FE  CD4960        			CALL	SNDBYTE
  6401  7C            			LD		A,H
  6402  BA            			CP		D
  6403  2004          			JR		NZ,MONSV2
  6405  7D            			LD		A,L
  6406  BB            			CP		E
  6407  2803          			JR		Z,MONSV3         ;HL = DE までLOOP
  6409  23            	MONSV2:	INC		HL
  640A  18F1          			JR		MONSV1
  640C  21BC65        	MONSV3:	LD		HL,MSG_OK        ;OK表示
  640F  CDED52        			CALL	MSGOUT
  6412  C38F60        			JP		CMD1
                      	
                      	;**** コマンド送信 (IN:A コマンドコード)****
  6415  CD4960        	STCD:	CALL	SNDBYTE          ;Aレジスタのコマンドコードを送信
  6418  CD3660        			CALL	RCVBYTE          ;状態取得(00H=OK)
  641B  C9            			RET
                      	
                      	;**** HLからの4Byteが16進数を表すアスキーコードであれば16進数に変換してHLに代入 **************
  641C  EB            	HLHEX:	EX		DE,HL
  641D  210000        			LD		HL,0000H
  6420  0604          			LD		B,04H
  6422  1A            	HLHEX1:	LD		A,(DE)
  6423  13            			INC		DE
  6424  CDC15F        			CALL	AZLCNV
  6427  CD395E        			CALL	HEXCHK
  642A  DA3364        			JP		C,HLHEX2
  642D  CD4B5E        			CALL	BINCV4
  6430  10F0          			DJNZ	HLHEX1
  6432  AF            			XOR		A
  6433  C9            	HLHEX2:	RET
                      	
                      	;**** HLからの2Byteが16進数を表すアスキーコードであれば16進数に変換してAに代入 **************
  6434  E5            	TWOHEX:	PUSH	HL
  6435  210000        			LD		HL,0000H
  6438  0602          			LD		B,02H
  643A  1A            	TWHEX1:	LD		A,(DE)
  643B  13            			INC		DE
  643C  CDC15F        			CALL	AZLCNV
  643F  CD395E        			CALL	HEXCHK
  6442  DA4C64        			JP		C,TWHEX2
  6445  CD4B5E        			CALL	BINCV4
  6448  10F0          			DJNZ	TWHEX1
  644A  AF            			XOR		A
  644B  7D            			LD		A,L
  644C                	TWHEX2:
  644C  E1            			POP		HL
  644D  C9            			RET
                      	
                      	;****** FILE NAMEが取得できるまでスペース、ダブルコーテーションを読み飛ばし (IN:HL コマンド文字の次の文字 OUT:HL ファイルネームの先頭)*********
  644E  F5            	STFN:	PUSH	AF
  644F  7E            	STFN1:	LD		A,(HL)
  6450  FE20          			CP		20H
  6452  2804          			JR		Z,STFN2
  6454  FE22          			CP		22H
  6456  2003          			JR		NZ,STFN3
  6458  23            	STFN2:	INC		HL               ;ファイルネームまでスペース読み飛ばし
  6459  18F4          			JR		STFN1
  645B  F1            	STFN3:	POP		AF
  645C  C9            			RET
                      	
  645D                	STSV2:                           ;ファイルネームの取得に失敗
  645D  213C66        			LD		HL,MSG_FNAME
  6460  1856          			JR		ERRMSG
                      	
                      	;**** ファイルネーム送信(IN:HL ファイルネームの先頭) ******
  6462  0620          	STFS:	LD		B,20H
  6464  7E            	STFS1:	LD		A,(HL)           ;FNAME送信
  6465  FE22          			CP		22H
  6467  2003          			JR		NZ,STFS2
  6469  23            			INC		HL
  646A  18F8          			JR		STFS1
  646C  CD4960        	STFS2:	CALL	SNDBYTE
  646F  23            			INC		HL
  6470  05            			DEC		B
  6471  20F1          			JR		NZ,STFS1
  6473  3E0D          			LD		A,0DH
  6475  CD4960        			CALL	SNDBYTE
  6478  CD3660        			CALL	RCVBYTE          ;状態取得(00H=OK)
  647B  C9            			RET
                      	
                      	;************** エラー内容表示 *****************************
  647C                	SDERR:
  647C  F5            			PUSH	AF
  647D  FEF0          			CP		0F0H
  647F  2005          			JR		NZ,ERR3
  6481  213467        			LD		HL,MSG_F0        ;SD-CARD INITIALIZE ERROR
  6484  1832          			JR		ERRMSG
  6486  FEF1          	ERR3:	CP		0F1H
  6488  2005          			JR		NZ,ERR4
  648A  214D67        			LD		HL,MSG_F1        ;NOT FIND FILE
  648D  1829          			JR		ERRMSG
  648F  FEF3          	ERR4:	CP		0F3H
  6491  2005          			JR		NZ,ERR5
  6493  216B67        			LD		HL,MSG_F3        ;FILE EXIST
  6496  1820          			JR		ERRMSG
  6498  FEF4          	ERR5:	CP		0F4H
  649A  2005          			JR		NZ,ERR6
  649C  214E66        			LD		HL,MSG_CMD       ;COMMAND FAILED
  649F  1817          			JR		ERRMSG
  64A1  FEF6          	ERR6:	CP		0F6H
  64A3  2005          			JR		NZ,ERR99
  64A5  213C66        			LD		HL,MSG_FNAME     ;PARAMETER FAILED
  64A8  180E          			JR		ERRMSG
  64AA  CD835E        	ERR99:	CALL	MONBHX
  64AD  7A            			LD		A,D
  64AE  CD5702        			CALL	CONOUT
  64B1  7B            			LD		A,E
  64B2  CD5702        			CALL	CONOUT
  64B5  219767        			LD		HL,MSG99         ;その他ERROR
  64B8  CDED52        	ERRMSG:	CALL	MSGOUT
  64BB  CDCA5F        			CALL	MONCLF
  64BE  CD5003        			CALL	DISPBL
  64C1  F1            			POP		AF
  64C2  C9            			RET
                      	
                      	;************** BASIC CMT LOAD *********************
  64C3                	CMDLOAD:
  64C3  2B            			DEC		HL
  64C4  3E73          			LD		A,73H            ;コマンド73Hを送信
  64C6  CD8E63        			CALL	STCMD
  64C9  C2A565        			JP		NZ,RETBC
                      	
  64CC  211801        			LD		HL,0118H         ;1文字目、24行目へカーソルを移動
  64CF  CDA903        			CALL	CSR
  64D2  CDCA5F        			CALL	MONCLF
                      			
  64D5  CD3660        			CALL	RCVBYTE          ;ヘッダー受信
  64D8  FED3          			CP		0D3H             ;D3Hでなければエラー
  64DA  280C          			JR		Z,CMDLD
  64DC  218367        			LD		HL,MSG_F6        ;NOT BASIC PROGRAM
  64DF  CDED52        			CALL	MSGOUT
  64E2  CD5003        			CALL	DISPBL
  64E5  C3A565        			JP		RETBC
                      			
  64E8                	CMDLD:	
  64E8  213EEF        			LD		HL,FILNAM        ;CMTファイル中に記載のファイルネーム6文字を受信
  64EB  0606          			LD		B,06H
  64ED  CD3660        	CMDLD2:	CALL	RCVBYTE
  64F0  77            			LD		(HL),A
  64F1  23            			INC		HL
  64F2  10F9          			DJNZ	CMDLD2
                      	
  64F4  21B365        			LD		HL,MSG_LD        ;LOADING表示
  64F7  CDED52        			CALL	MSGOUT
                      	
  64FA  213EEF        			LD		HL,FILNAM        ;ファイルネーム表示
  64FD  1196EC        			LD		DE,IBUF
  6500  010600        			LD		BC,0006H
  6503  EDB0          			LDIR
  6505  AF            			XOR		A
  6506  12            			LD		(DE),A
  6507  2196EC        			LD		HL,IBUF
  650A  CDED52        			CALL	MSGOUT
  650D  CDCA5F        			CALL	MONCLF
                      			
  6510  2A54EB        			LD		HL,(BASSRT)      ;BASICプログラム格納開始位置をHLに設定
                      	
  6513  060C          	CMDLD3:	LD		B,0CH            ;00Hを12個連続で受信するまでループ
  6515  CD3660        	CMDLD4:	CALL	RCVBYTE
  6518  77            			LD		(HL),A
  6519  23            			INC		HL
                      	
  651A  A7            			AND		A
  651B  2802          			JR		Z,CMDLD5
  651D  18F4          			JR		CMDLD3
  651F  05            	CMDLD5:	DEC		B
  6520  2802          			JR		Z,CMDLD6
  6522  18F1          			JR		CMDLD4
                      	
  6524  010700        	CMDLD6:	LD		BC,0007H         ;HLの位置を7つ戻してBASICプログラム終了位置とする
  6527  ED42          			SBC		HL,BC
                      			
  6529  187D          			JR		RETBC2           ;BASICプログラムLOAD後処理
                      			
                      	;********************** BASIC CMT SAVE **********************
  652B                	CMDSAVE:
  652B  E5            			PUSH	HL
  652C  2A54EB        			LD		HL,(BASSRT)      ;BASICプログラム格納開始位置をHLに設定
  652F  7E            			LD		A,(HL)
  6530  23            			INC		HL
  6531  B6            			OR		(HL)
  6532  E1            			POP		HL
  6533  CA7665        			JP		Z,CMDSV5         ;BASICプログラムが1行もなければエラー
                      	
  6536  2B            			DEC		HL
  6537  3E74          			LD		A,74H            ;コマンド74Hを送信
  6539  CD8E63        			CALL	STCMD
  653C  2036          			JR		NZ,CMDSV4
                      	
  653E  21C265        			LD		HL,MSG_WR        ;WRITING表示
  6541  CDED52        			CALL	MSGOUT
                      	
  6544  CD865B        			CALL	RENUM9           ;BASICプログラムSAVE前処理
  6547  2A54EB        			LD		HL,(BASSRT)      ;BASSRTからVARBGN-1までを送信
  654A  7D            			LD		A,L
  654B  CD4960        			CALL	SNDBYTE
  654E  7C            			LD		A,H
  654F  CD4960        			CALL	SNDBYTE
  6552  ED5BA0EF      			LD		DE,(VARBGN)
  6556  1B            			DEC		DE
  6557  7B            			LD		A,E
  6558  CD4960        			CALL	SNDBYTE
  655B  7A            			LD		A,D
  655C  CD4960        			CALL	SNDBYTE
  655F  7E            	CMDSV1:	LD		A,(HL)
  6560  CD4960        			CALL	SNDBYTE
  6563  7C            			LD		A,H
  6564  BA            			CP		D
  6565  2004          			JR		NZ,CMDSV2
  6567  7D            			LD		A,L
  6568  BB            			CP		E
  6569  2803          			JR		Z,CMDSV3         ;HL = DE までLOOP
  656B  23            	CMDSV2:	INC		HL
  656C  18F1          			JR		CMDSV1
                      			
  656E                	CMDSV3:
  656E  21BC65        			LD		HL,MSG_OK        ;OK表示
  6571  CDED52        			CALL	MSGOUT
                      	;2022.8.4  SYNTAX ERROR回避
  6574  182F          	CMDSV4:	JR		RETBC
                      	
                      	;2022.8.4  SYNTAX ERROR回避
  6576                	CMDSV5:
  6576  CD5003        			CALL	DISPBL
  6579  217667        			LD		HL,MSG_F5        ;NO BASIC PROGRAM
  657C  1821          			JR		RETBC3
                      	
                      	;************ BASIC FILES ********************
  657E                	CMDFILES:
                      	;2022.7.24 SYNTAX ERROR回避により、廃止
                      	;		PUSH	HL
                      	;		LD		HL,FMSG          ;FILESの後ろにファイル名検索文字を指定すると構文エラーになるため、別行として入力
                      	;		CALL	MSGOUT
                      	;		CALL	LINPUT
                      	;		INC		HL
  657E  CD4E64        			CALL	STFN             ;入力文字列取得
  6581  EB            			EX		DE,HL
  6582  21A467        			LD		HL,DEFDIR2       ;行頭に'LOAD 'を付けることでカーソルを移動させリターンで実行できるように
  6585  010600        			LD		BC,D2END-DEFDIR2
  6588  CDC661        			CALL	DIRLIST          ;DIRLIST本体をコール
                      	;2022.8.4  SYNTAX ERROR回避
  658B  1818          			JR		RETBC
                      			
                      	;************ BASIC KILL ***************************
  658D                	CMDKILL:
  658D  2B            			DEC		HL
  658E  3E75          			LD		A,75H            ;コマンド75Hを送信
  6590  CD8E63        			CALL	STCMD
  6593  2010          			JR		NZ,RETBC         ;ファイル名が送信できなかった。
  6595  CD3660        			CALL	RCVBYTE
  6598  A7            			AND		A
  6599  C2AB65        			JP		NZ,CMDKL1         ;ファイルが存在しない
  659C  21CB65        			LD		HL,MSG_KL
  659F  CDED52        	RETBC3:	CALL	MSGOUT
  65A2  CDCA5F        			CALL	MONCLF
                      	
                      	;2022.8.4 SYNTAX ERROR回避 
                      	;FILES SAVE KILLからBASICへ正しい戻り方が判らなかったため、LOAD後処理で戻ることとする。
  65A5                	RETBC:
  65A5  2AA0EF        			LD		HL,(VARBGN)
                      	;LOAD後処理
  65A8                	RETBC2:
  65A8  C38B1F        			JP		AFTLOAD          ;BASICプログラムLOAD後処理
                      			
                      	;2022.8.4  SYNTAX ERROR回避
  65AB                	CMDKL1:
  65AB  CD5003        			CALL	DISPBL
  65AE  214D67        			LD		HL,MSG_F1        ;NOT FIND FILE
  65B1  18EC          			JR		RETBC3
                      	
  65B3                	MSG_LD:
  65B3  4C4F4144494E47			DB		'LOADING '
  65BB  00            	DEFCR:	DB		00H
                      	
  65BC                	MSG_OK:
  65BC  4F4B21        			DB		'OK!'
  65BF  0D0A00        			DB		0DH,0AH,00H
                      	
  65C2                	MSG_WR:
  65C2  57524954494E47			DB		'WRITING '
  65CA  00            			DB		00H
                      	
  65CB                	MSG_KL:
  65CB  46494C45204445			DB		'FILE DELETED!'
  65D8  00            			DB		00H
                      	
  65D9                	MSG_AD1:
  65D9  41445253202B30			DB		'ADRS +0 +1 +2 +3 +4 +5 +6 +7 01234567'
  65FE  0D0A00        			DB		0DH,0AH,00H
                      			
  6601                	MSG_AD2:
  6601  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:ESC'
  661A  0D0A00        			DB		0DH,0AH,00H
                      			
  661D                	MSG_KEY1:
  661D  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:'
  6633  00            			DB		00H
  6634                	MSG_KEY2:
  6634  204F5220455343			DB		' OR ESC'
  663B  00            			DB		00H
                      	
  663C                	MSG_FNAME:
  663C  504152414D4554			DB		'PARAMETAR FAILED!'
  664D  00            			DB		00H
                      			
  664E                	MSG_CMD:
  664E  434F4D4D414E44			DB		'COMMAND FAILED!',0DH,0AH
  665F  20422020202020			DB		' B             : Return Basic',0DH,0AH
  667E  2044206E6E6E6E			DB		' D nnnn        : Memory Dump',0DH,0AH
  669C  20462078202020			DB		' F x           : Find SD File',0DH,0AH
  66BB  2047206E6E6E6E			DB		' G nnnn        : Exec Program',0DH,0AH
  66DA  204C2078202020			DB		' L x           : Load From SD',0DH,0AH
  66F9  2053206E6E6E6E			DB		' S nnnn nn..   : Memory Write',0DH,0AH
  6718  2057206E6E6E6E			DB		' W nnnn nnnn x : Save To SD'
  6733  00            			DB		00H
                      			
  6734                	MSG_F0:
  6734  53442D43415244			DB		'SD-CARD INITIALIZE ERROR'
  674C  00            			DB		00H
                      			
  674D                	MSG_F1:
  674D  4E4F542046494E			DB		'NOT FIND FILE'
  675A  00            			DB		00H
                      			
  675B                	MSG_F2:
  675B  4E4F54204F424A			DB		'NOT OBJECT FILE'
  676A  00            			DB		00H
                      			
  676B                	MSG_F3:
  676B  46494C45204558			DB		'FILE EXIST'
  6775  00            			DB		00H
                      			
  6776                	MSG_F5:
  6776  4E4F2050524F47			DB		'NO PROGRAM!!'
  6782  00            			DB		00H
                      			
  6783                	MSG_F6:
  6783  4E4F5420424153			DB		'NOT BASIC PROGRAM'
  6794  0D0A00        			DB		0DH,0AH,00H
                      			
  6797                	MSG99:
  6797  204552524F52  			DB		' ERROR'
  679D  00            			DB		00H
                      			
  679E  2A53          	MSG_FDW:	DB		'*S'
  67A0  00            			DB		00H
                      	
  67A1                	DEFDIR:
  67A1  2A4C20        			DB		'*L '
  67A4                	DEND:
                      	
  67A4                	DEFDIR2:
  67A4  4C4F41442022  			DB		'LOAD ',22H
  67AA                	D2END:
                      	
                      	
  7FFC                			ORG		7FFCH
                      	
                      	;************* 代替MONITORへジャンプするための設定 *************
  7FFC  C37960        			JP		MONINI
  7FFF  55            			DB		55H
                      			
  8000                			END
