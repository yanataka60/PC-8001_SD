			  Z80 ASSEMBLER - ZASM VER 1.6
                      	;2022.7.24 FILESでSYNTAX ERRORとなる事象を回避、SERCH FILEと聞いてくる仕様を削除
                      	;2022.7.25 FILESを↑キーで打ち切ったとき時々Syntax Errorが発生するため2行戻す動きを廃止
                      	;2022.8.4  BASIC戻り時のSyntax Error対策を修正
                      	;2022.8.14 MONITOR GコマンドでPOP BCが抜けていたので追加。
                      	;          mk2用と統合
                      	;            拡張ROM認識コードを041H,042H,00Hとした。
                      	;            SHIFTキーを押しながら起動することで拡張ROMを有効にするか選択できるようにした。(mk2用)
                      	;            mk2においてもfiles、save、killコマンドでSyntaxErrorが出ないように修正
                      	;2022.8.21 MONITOR Lコマンドで読み込み時にファンクションキーエリアであればCTRL+B、CLOADをSD用に書き換えるようにした
                      	;2023.5.30 MONHOTの扱い方を修正、MONERRを移動
                      	;          LOAD中専用のスタックポインタとすることでCLEAR文でSPを変更していなくてもE800H以降に正常にLOAD出来るよう対処
                      	
  0257                	CONOUT		EQU		0257H		;CRTへの1バイト出力
  0350                	DISPBL		EQU		0350H		;ベルコードの出力
  03A9                	CSR			EQU		03A9H		;カーソルの移動
  03F3                	CSRADR		EQU		03F3H		;キャラクタ座標->VRAMアドレス変換
  0BE2                	DSPCSR		EQU		0BE2H		;カーソル表示の開始
  0FAC                	KYSCAN		EQU		0FACH		;リアルタイム・キーボード・スキャニング
  1B7E                	LINPUT		EQU		1B7EH		;スクリーン・エディタ
  1F8B                	AFTLOAD		EQU		1F8BH		;BASICテキストの終了アドレス設定
  40A6                	CHROUT		EQU		40A6H		;デバイスへの1バイト出力
  52ED                	MSGOUT		EQU		52EDH		;文字列の出力2
  5B86                	RENUM9		EQU		5B86H		;BASICプログラムSAVE前処理
  5C3C                	MONBGN		EQU		5C3CH		;MONITOR開始アドレス
  5E39                	HEXCHK		EQU		5E39H		;16進コード・チェック
  5E4B                	BINCV4		EQU		5E4BH		;16進コードからバイナリ形式への変換
  5E83                	MONBHX		EQU		5E83H		;8ビット数値から16進コードへの変換
  5EBD                	MONDME		EQU		5EBDH		;16進データ2桁表示
  5EC0                	MONDHL		EQU		5EC0H		;16進4桁表示
  5FC1                	AZLCNV		EQU		5FC1H		;小文字->大文字変換
  5FCA                	MONCLF		EQU		5FCAH		;CRコード及びLFコードの表示
  5FD4                	MONSPC		EQU		5FD4H		;スペースの表示
                      	
  EA63                	CUSPOS		EQU		0EA63H		;カーソル位置
  EAC0                	FKDEF		EQU		0EAC0H		;AUTO START キー定義場所初期値
  EB54                	BASSRT		EQU		0EB54H		;プログラムテキスト開始位置
  EC96                	IBUF		EQU		0EC96H		;キー入力バッファ
  EDC0                	FKPINT		EQU		0EDC0H		;キーポインタ
  EF3E                	FILNAM		EQU		0EF3EH		;現在ロード中ファイルネーム
  EFA0                	VARBGN		EQU		0EFA0H		;変数領域の始まりアドレス
  F139                	TBLOAD		EQU		0F139H		;LOADコマンドジャンプアドレス
  F142                	TBKILL		EQU		0F142H		;KILLコマンドジャンプアドレス
  F14B                	TBSAVE		EQU		0F14BH		;SAVEコマンドジャンプアドレス
  F14E                	TBFILES		EQU		0F14EH		;FILESコマンドジャンプアドレス
  FF34                	MONHL		EQU		0FF34H
  FF36                	MONSP		EQU		0FF36H
  FF3D                	SADRS		EQU		0FF3DH
  FF3F                	EADRS		EQU		0FF3FH
  FF40                	ACFLGD		EQU		0FF40H      ;オートラン機能ダブルコーテーション検出フラグ
  FF41                	ACFLGC		EQU		0FF41H      ;オートラン機能CLOAD検出フラグ
  FF66                	LBUF		EQU		0FF66H
                      	
                      	;PC-8001
  00FC                	PPI_A		EQU		0FCH
                      	;PC-8001mk2
                      	;PPI_A		EQU		07CH
                      	
  00FD                	PPI_B		EQU		PPI_A+1
  00FE                	PPI_C		EQU		PPI_A+2
  00FF                	PPI_R		EQU		PPI_A+3
                      	
                      	;PC-8001
                      	;8255 PORT アドレス FCH〜FFH
                      	;0FCH PORTA 送信データ(下位4ビット)
                      	;0FDH PORTB 受信データ(8ビット)
                      	;PC-8001mk2
                      	;8255 PORT アドレス 7CH〜7FH
                      	;7CH PORTA 送信データ(下位4ビット)
                      	;7DH PORTB 受信データ(8ビット)
                      	
                      	;
                      	;PC-8001
                      	;0FEH PORTC Bit
                      	;PC-8001mk2
                      	;7EH PORTC Bit
                      	
                      	;7 IN  CHK
                      	;6 IN
                      	;5 IN
                      	;4 IN 
                      	;3 OUT
                      	;2 OUT FLG
                      	;1 OUT
                      	;0 OUT
                      	;
                      	;PC-8001
                      	;0FFH コントロールレジスタ
                      	;PC-8001mk2
                      	;7FH コントロールレジスタ
                      	
                      	
                      	
  6000                	        ORG		6000H
                      	
  6000  4142          			DB		041H,042H         ;拡張ROM認識コード
                      	
  6002  00            			NOP
  6003  180A          			JR		INIT              ;POWER ONで8255を初期化、LOAD、SAVE、FILES、KILLのジャンプ先を設定
  6005  00            			NOP
                      	;PC-8001mk2の作法に統一
                      	;		CALL	INIT              ;POWER ONで8255を初期化、LOAD、SAVE、FILES、KILLのジャンプ先を設定
                      	;		RET
                      	
                      	;*********** OPENしているファイルから1BYTE読み出し転送 ****************
                      	;5F9EH代替ルーチン
  6006  C38363        			JP		D_5F9E
                      	
                      	;*********** CMT LOAD 直呼び出し (EMI PLAYER用) *****************************
                      	;5F3AH代替ルーチン
  6009  C3A163        			JP		D_5F3A
                      	
                      	;*********** MONITOR HOT START  (EMI PLAYER用) ********************
                      	;5C66H代替ルーチン
  600C  C3D360        			JP		MONHOT
                      			
                      	;**** 8255初期化 ****
                      	;PORTC下位BITをOUTPUT、上位BITをINPUT、PORTBをINPUT、PORTAをOUTPUT
  600F  3E8A          	INIT:	LD		A,8AH
  6011  D3FF          			OUT		(PPI_R),A
                      	;出力BITをリセット
  6013  AF            			XOR		A                 ;PORTA <- 0
  6014  D3FC          			OUT		(PPI_A),A
  6016  D3FE          			OUT		(PPI_C),A         ;PORTC <- 0
                      	
  6018  DB08          			IN		A,(08H)
  601A  E640          			AND		40H
                      	;キースキャンしてSHIFTきーが押されていなければ通常起動
                      	;		RET		NZ
                      	
                      	;キースキャンしてSHIFTきーが押されていなければ拡張ROM起動
  601C  C8            			RET		Z
                      	
                      	;LOAD、SAVE、FILES、KILLのジャンプ先を設定
  601D  21BB64        	INI2:	LD		HL,CMDLOAD
  6020  2239F1        			LD		(TBLOAD),HL
                      	
  6023  212365        			LD		HL,CMDSAVE
  6026  224BF1        			LD		(TBSAVE),HL
                      	
  6029  217665        			LD		HL,CMDFILES
  602C  224EF1        			LD		(TBFILES),HL
                      	
  602F  218565        			LD		HL,CMDKILL
  6032  2242F1        			LD		(TBKILL),HL
                      	
  6035  C9            	INI3:	RET
                      	
                      	;**** 1BYTE受信 ****
                      	;受信DATAをAレジスタにセットしてリターン
  6036                	RCVBYTE:
  6036  CD6B60        			CALL	F1CHK             ;PORTC BIT7が1になるまでLOOP
  6039  DBFD          			IN		A,(PPI_B)         ;PORTB -> A
  603B  F5            			PUSH 	AF
  603C  3E05          			LD		A,05H
  603E  D3FF          			OUT		(PPI_R),A         ;PORTC BIT2 <- 1
  6040  CD7260        			CALL	F2CHK             ;PORTC BIT7が0になるまでLOOP
  6043  3E04          			LD		A,04H
  6045  D3FF          			OUT		(PPI_R),A         ;PORTC BIT2 <- 0
  6047  F1            			POP 	AF
  6048  C9            			RET
                      			
                      	;**** 1BYTE送信 ****
                      	;Aレジスタの内容をPORTA下位4BITに4BITずつ送信
  6049                	SNDBYTE:
  6049  F5            			PUSH	AF
  604A  1F            			RRA
  604B  1F            			RRA
  604C  1F            			RRA
  604D  1F            			RRA
  604E  E60F          			AND		0FH
  6050  CD5A60        			CALL	SND4BIT
  6053  F1            			POP		AF
  6054  E60F          			AND		0FH
  6056  CD5A60        			CALL	SND4BIT
  6059  C9            			RET
                      	
                      	;**** 4BIT送信 ****
                      	;Aレジスタ下位4ビットを送信する
  605A                	SND4BIT:
  605A  D3FC          			OUT		(PPI_A),A
  605C  3E05          			LD		A,05H
  605E  D3FF          			OUT		(PPI_R),A          ;PORTC BIT2 <- 1
  6060  CD6B60        			CALL	F1CHK             ;PORTC BIT7が1になるまでLOOP
  6063  3E04          			LD		A,04H
  6065  D3FF          			OUT		(PPI_R),A          ;PORTC BIT2 <- 0
  6067  CD7260        			CALL	F2CHK
  606A  C9            			RET
                      			
                      	;**** BUSYをCHECK(1) ****
                      	; 82H BIT7が1になるまでLOP
  606B  DBFE          	F1CHK:	IN		A,(PPI_C)
  606D  E680          			AND		80H               ;PORTC BIT7 = 1?
  606F  28FA          			JR		Z,F1CHK
  6071  C9            			RET
                      	
                      	;**** BUSYをCHECK(0) ****
                      	; 82H BIT7が0になるまでLOOP
  6072  DBFE          	F2CHK:	IN		A,(PPI_C)
  6074  E680          			AND		80H               ;PORTC BIT7 = 0?
  6076  20FA          			JR		NZ,F2CHK
  6078  C9            			RET
                      	
                      	;************* 代替MONITOR START *************************
  6079  2234FF        	MONINI:	LD		(MONHL),HL
  607C  ED7336FF      			LD		(MONSP),SP
                      			
                      	;************ (TBLOAD)にCMDLOADがセットされていなければMONBGNへジャンプして通常MONITOR起動 ***************
  6080  2A39F1        			LD		HL,(TBLOAD)
  6083  11BB64        			LD		DE,CMDLOAD
  6086  ED52          			SBC		HL,DE
  6088  C23C5C        			JP		NZ,MONBGN
                      	
                      	;		PUSH	HL                ;タイトルを表示するとBASICから戻ったときに自動実行が途切れてしまうため削除
                      	;		LD		HL,MONMSG
                      	;		CALL	MSGOUT
                      	;		POP		HL
                      			
  608B  01CE60        	CMD0:	LD		BC,MONERR         ;パラメータエラーをRET Cで戻りる先をセット
  608E  C5            			PUSH	BC
  608F                	CMD1:
  608F  3E2A          			LD		A,'*'
  6091  CD5702        			CALL	CONOUT            ;プロンプト表示
  6094  CDE20B        			CALL	DSPCSR
  6097  CD7E1B        			CALL	LINPUT            ;スクリーンエディットを使いたいために使用、このためCTRL+BではBASICに戻れない
                      									  ;HLにIBUF-1が入って戻っている LD HL,IBUF-1
  609A                	CMD2:
  609A  3840          			JR		C,MONCTB          ;CTRL+Bの代替、CTRL+CでBASICに復帰
  609C  23            			INC		HL
  609D  7E            			LD		A,(HL)
  609E  FE2A          			CP		'*'               ;入力時とスクリーンエディット時でIBUFに入る位置が変わるための対処、「*」ならポインタを進める
  60A0  28F8          			JR		Z,CMD2
  60A2  7E            			LD		A,(HL)
  60A3  CDC15F        			CALL	AZLCNV            ;入力文字を大文字へ変換
  60A6  FE42          			CP		'B'
  60A8  2832          			JR		Z,MONCTB          ;CTRL+Bの代替、BコマンドでもBASICに復帰
  60AA  FE44          			CP		'D'
  60AC  CAEF60        			JP		Z,STMD            ;Dコマンド
  60AF  FE53          			CP		'S'
  60B1  CA6961        			JP		Z,STMW            ;Sコマンド
  60B4  FE46          			CP		'F'
  60B6  CAA861        			JP		Z,STLT            ;Fコマンド
  60B9  FE4C          			CP		'L'
  60BB  CA5E62        			JP		Z,MONLOAD         ;Lコマンド
  60BE  FE57          			CP		'W'
  60C0  CAAD63        			JP		Z,MONSAVE         ;Wコマンド
  60C3  FE47          			CP		'G'
  60C5  281B          			JR		Z,GOCMD           ;Gコマンド
  60C7  3EF4          			LD		A,0F4H
  60C9  CD7464        			CALL	SDERR             ;コマンドエラー
  60CC  18C1          			JR		CMD1
                      			
                      	;MONMSG:	DEFB	0CH,'**PC-8001_SD Monitor **',0DH,0AH,00H
                      	
                      	;************************* MONHOTの扱い方を修正、MONERRを移動 2023.5.30 ****************************
                      	;パラメータエラー処理
  60CE  3EF6          	MONERR:	LD		A,0F6H
  60D0  CD7464        			CALL	SDERR
                      	;************************* MONHOTの扱い方を修正 2023.5.30 ****************************
                      	;		JP		CMD1
                      	
                      	;*********** MONITOR HOT START  (EMI PLAYER用)  *******************************
                      	;5C66H代替ルーチン
  60D3                	MONHOT:
  60D3  ED7B36FF      			LD		SP,(MONSP)        ;スタックポインタを復帰
  60D7  CDCA5F        			CALL	MONCLF
  60DA  18AF          			JR		CMD0
                      	
                      	;************ Bコマンド BASIC復帰 ***********************
  60DC                	MONCTB:
                      	;***** 2022.8.10 BUG修正 ***********
  60DC  C1            			POP		BC                ;パラメータエラー戻り先を破棄
                      	;***********************************
  60DD  2A34FF        			LD		HL,(MONHL)
  60E0  FB            			EI
  60E1  C9            			RET
                      	
                      	;************ Gコマンド アドレスxxxxへジャンプ ***************
  60E2  23            	GOCMD:	INC		HL
  60E3  CD4664        			CALL	STFN
  60E6  CD1464        			CALL	HLHEX             ;4桁の16進数であればHLにセットして続行
  60E9  D8            			RET		C
                      	;		JP		C,MONERR
  60EA  CDCA5F        			CALL	MONCLF
  60ED  C1            			POP		BC
  60EE  E9            			JP		(HL)              ;HLの示すアドレスにジャンプ
                      	
                      	;************ Dコマンド アドレスxxxxからのMEMORYをDUMP **********************
  60EF  23            	STMD:	INC		HL
  60F0  CD4664        			CALL	STFN
  60F3  CD1464        			CALL	HLHEX             ;4桁の16進数であればSADRSにセットして続行
  60F6  D8            			RET		C
                      	;		JP		C,MONERR
  60F7  223DFF        			LD		(SADRS),HL        ;SARDS保存
                      	
  60FA  21D165        	STMD6:	LD		HL,MSG_AD1        ;DUMP TITLE表示
  60FD  CDED52        			CALL	MSGOUT
  6100  0E10          			LD		C,10H             ;16行(128Byte)を表示
  6102  2A3DFF        	STMD7:	LD		HL,(SADRS)        ;アドレス表示
  6105  CDC05E        			CALL	MONDHL
  6108  CDD45F        			CALL	MONSPC
                      	
                      			
  610B  0608          			LD		B,08H             ;一行(8Byte)のデータを16進数表示
  610D  CDBD5E        	STMD0:	CALL	MONDME
  6110  CDD45F        			CALL	MONSPC
  6113  CDAC0F        			CALL	KYSCAN
  6116  FE1B          			CP		1BH               ;表示途中でもESCで打ち切り
  6118  284C          			JR		Z,STMD4
  611A  23            			INC		HL
  611B  05            			DEC		B
  611C  20EF          			JR		NZ,STMD0
                      	
  611E  2A3DFF        			LD		HL,(SADRS)
  6121  0608          			LD		B,08H             ;一行(8Byte)のデータをキャラクタ表示
  6123  7E            	STMD2:	LD		A,(HL)
  6124  FE20          			CP		20H               ;文字化けに対処 20H未満なら20Hに置き換え
  6126  3002          			JR		NC,STMD8
  6128  3E20          			LD		A,20H
  612A  CD5702        	STMD8:	CALL	CONOUT
  612D  CDAC0F        			CALL	KYSCAN
  6130  FE1B          			CP		1BH               ;表示途中でもESCで打ち切り
  6132  2832          			JR		Z,STMD4
  6134  23            			INC		HL
  6135  05            			DEC		B
  6136  20EB          			JR		NZ,STMD2
                      	
  6138  223DFF        			LD		(SADRS),HL
  613B  CDCA5F        			CALL	MONCLF
                      	
  613E  0D            			DEC		C
  613F  20C1          			JR		NZ,STMD7
                      			
  6141  21F965        			LD		HL,MSG_AD2        ;入力待ちメッセージ表示
  6144  CDED52        			CALL	MSGOUT
  6147  CDAC0F        	STMD3:	CALL	KYSCAN            ;1文字入力待ち
  614A  A7            			AND		A
  614B  28FA          			JR		Z,STMD3
  614D  FE1B          			CP		1BH               ;ESCで打ち切り
  614F  2815          			JR		Z,STMD4
  6151  CDC15F        			CALL	AZLCNV
  6154  FE42          			CP		42H               ;BからBACK
  6156  200B          			JR		NZ,STMD5
  6158  2A3DFF        			LD		HL,(SADRS)
  615B  110001        			LD		DE,0100H
  615E  ED52          			SBC		HL,DE
  6160  223DFF        			LD		(SADRS),HL
  6163  C3FA60        	STMD5:	JP		STMD6
  6166                	STMD4:
                      	;************************* MONHOTの扱い方を修正 2023.5.30 ****************************
  6166  C3D360        			JP		MONHOT
                      	;		JP		CMD1
                      	
                      	;************ Sコマンド アドレスxxxxからMEMORYに書き込み **********************
  6169  23            	STMW:	INC		HL
  616A  CD4664        			CALL	STFN
  616D  CD1464        			CALL	HLHEX             ;4桁の16進数であればHLにセットして続行
  6170  D8            			RET		C
                      	;		JP		C,MONERR
                      	
  6171  1A            	STSP1:	LD		A,(DE)
  6172  A7            			AND		A
  6173  2830          			JR		Z,STMW9           ;アドレスのみなら終了
  6175  FE20          			CP		20H
  6177  2003          			JR		NZ,STMW1
  6179  13            			INC		DE                ;空白は飛ばす
  617A  18F5          			JR		STSP1
                      	
  617C                	STMW1:
  617C  CD2C64        			CALL	TWOHEX
  617F  380D          			JR		C,STMW8
  6181  77            			LD		(HL),A            ;2桁の16進数があれば(HL)に書き込み
  6182  23            			INC		HL
                      	
  6183  1A            	STSP2:	LD		A,(DE)
  6184  A7            			AND		A               ;一行終了
  6185  2807          			JR		Z,STMW8
  6187  FE20          			CP		20H
  6189  20F1          			JR		NZ,STMW1
  618B  13            			INC		DE                ;空白は飛ばす
  618C  18F5          			JR		STSP2
                      	
  618E  E5            	STMW8:	PUSH	HL
  618F  219667        			LD		HL,MSG_FDW        ;行頭に'*S '
  6192  CDED52        			CALL	MSGOUT
  6195  E1            			POP		HL
  6196  E5            			PUSH	HL
  6197  CDC05E        			CALL	MONDHL            ;アドレス表示
  619A  CDD45F        			CALL	MONSPC
  619D  CD7E1B        			CALL	LINPUT            ;行入力繰り返し
                      	
  61A0  D1            			POP		DE
  61A1  23            			INC		HL
  61A2  EB            			EX		DE,HL
  61A3  18CC          			JR		STSP1
  61A5                	STMW9:
                      	;************************* MONHOTの扱い方を修正 2023.5.30 ****************************
  61A5  C3D360        			JP		MONHOT
                      	;		JP		CMD1
                      	
                      	;************ Fコマンド DIRLIST **********************
  61A8  23            	STLT:	INC		HL
  61A9  CD4664        			CALL	STFN              ;検索文字列を送信
  61AC  EB            			EX		DE,HL
  61AD  219967        			LD		HL,DEFDIR         ;行頭に'*L 'を付けることでカーソルを移動させリターンで実行できるように
  61B0  010300        			LD		BC,DEND-DEFDIR
  61B3  CDBD61        			CALL	DIRLIST           ;DIRLIST本体をコール
  61B6  A7            			AND		A                 ;00以外ならERROR
  61B7  C47464        			CALL	NZ,SDERR
                      	;************************* MONHOTの扱い方を修正 2023.5.30 ****************************
  61BA  C3D360        			JP		MONHOT
                      	;		JP		CMD1
                      	
                      	
                      	;**** DIRLIST本体 (HL=行頭に付加する文字列の先頭アドレス BC=行頭に付加する文字列の長さ) ****
                      	;****              戻り値 A=エラーコード ****
  61BD                	DIRLIST:
  61BD  3E83          			LD		A,83H             ;DIRLISTコマンド83Hを送信
  61BF  CD0D64        			CALL	STCD              ;コマンドコード送信
  61C2  A7            			AND		A                 ;00以外ならERROR
  61C3  C25D62        			JP		NZ,DLRET
                      			
  61C6  C5            			PUSH	BC
  61C7  0621          			LD		B,21H             ;ファイルネーム検索文字列33文字分を送信
  61C9  1A            	STLT1:	LD		A,(DE)
  61CA  A7            			AND		A
  61CB  2001          			JR		NZ,STLT2
  61CD  AF            			XOR		A
  61CE  CDC15F        	STLT2:	CALL	AZLCNV            ;大文字に変換
  61D1  FE22          			CP		22H               ;ダブルコーテーション読み飛ばし
  61D3  2003          			JR		NZ,STLT3
  61D5  13            			INC		DE
  61D6  18F1          			JR		STLT1
  61D8  CD4960        	STLT3:	CALL	SNDBYTE           ;ファイルネーム検索文字列を送信
  61DB  13            			INC		DE
  61DC  05            			DEC		B
  61DD  20EA          			JR		NZ,STLT1
  61DF  C1            			POP		BC
                      	
  61E0  CD3660        			CALL	RCVBYTE           ;状態取得(00H=OK)
  61E3  A7            			AND		A                 ;00以外ならERROR
  61E4  C25D62        			JP		NZ,DLRET
                      	
  61E7                	DL1:
  61E7  E5            			PUSH	HL
  61E8  C5            			PUSH	BC
  61E9  1166FF        			LD		DE,LBUF
  61EC  EDB0          			LDIR
  61EE  EB            			EX		DE,HL
  61EF  CD3660        	DL2:	CALL	RCVBYTE           ;'00H'を受信するまでを一行とする
  61F2  A7            			AND		A
  61F3  280C          			JR		Z,DL3
  61F5  FEFF          			CP		0FFH              ;'0FFH'を受信したら終了
  61F7  281E          			JR		Z,DL4
  61F9  FEFE          			CP		0FEH              ;'0FEH'を受信したら一時停止して一文字入力待ち
  61FB  2821          			JR		Z,DL5
  61FD  77            			LD		(HL),A
  61FE  23            			INC		HL
  61FF  18EE          			JR		DL2
  6201  3600          	DL3:	LD		(HL),00H
  6203  2166FF        			LD		HL,LBUF           ;'00H'を受信したら一行分を表示して改行
  6206  7E            	DL31:	LD		A,(HL)
  6207  A7            			AND		A
  6208  2806          			JR		Z,DL33
  620A  CDA640        			CALL	CHROUT
  620D  23            			INC		HL
  620E  18F6          			JR		DL31
  6210  CDCA5F        	DL33:	CALL	MONCLF
  6213  C1            			POP		BC
  6214  E1            			POP		HL
  6215  18D0          			JR		DL1
  6217  CD3660        	DL4:	CALL	RCVBYTE           ;状態取得(00H=OK)
  621A  C1            			POP		BC
  621B  E1            			POP		HL
  621C  183F          			JR		DLRET
                      	
  621E  E5            	DL5:	PUSH	HL
  621F  211566        			LD		HL,MSG_KEY1       ;HIT ANT KEY表示
  6222  CDED52        			CALL	MSGOUT
  6225  2A63EA        			LD		HL,(CUSPOS)
  6228  CDF303        			CALL	CSRADR
  622B  3E1E          			LD		A,1EH
  622D  77            			LD		(HL),A
  622E  3E1C          			LD		A,1CH
  6230  CD5702        			CALL	CONOUT
  6233  212C66        			LD		HL,MSG_KEY2       ;HIT ANT KEY表示
  6236  CDED52        			CALL	MSGOUT
  6239  CDCA5F        			CALL	MONCLF
  623C  E1            			POP		HL
  623D  CDAC0F        	DL6:	CALL	KYSCAN            ;1文字入力待ち
  6240  CDC15F        			CALL	AZLCNV
  6243  A7            			AND		A
  6244  28F7          			JR		Z,DL6
  6246  FE1B          			CP		1BH               ;ESCで打ち切り
  6248  280B          			JR		Z,DL7
  624A  FE1E          			CP		1EH               ;カーソル↑で打ち切り
  624C  2807          			JR		Z,DL7
  624E  FE42          			CP		42H               ;「B」で前ページ
  6250  2805          			JR		Z,DL8
  6252  AF            			XOR		A                 ;それ以外で継続
  6253  1802          			JR		DL8
                      	;DL9:
                      	;		LD		A,1EH             ;時々Syntax Errorが発生するため廃止
                      	;		CALL	CONOUT
                      	;		LD		A,1EH
                      	;		CALL	CONOUT            ;カーソル↑で打ち切ったときにカーソル2行上へ
  6255  3EFF          	DL7:	LD		A,0FFH            ;0FFH中断コードを送信
  6257  CD4960        	DL8:	CALL	SNDBYTE
  625A  C3EF61        			JP		DL2
                      			
  625D  C9            	DLRET:	RET
                      			
                      	
                      	;************ Lコマンド .CMT LOAD *************************
  625E  3E71          	MONLOAD:LD		A,71H            ;Lコマンド71Hを送信
  6260  CD8C63        			CALL	STCMD
                      	;************************* MONHOTの扱い方を修正 2023.5.30 ****************************
  6263  C2D360        			JP		NZ,MONHOT
                      	;		JP		NZ,CMD1
                      	
  6266  E5            			PUSH	HL
                      	; *********** オートラン書き換え用フラグクリア *** 2022.8.21 ********
  6267  AF            			XOR		A
  6268  2140FF        			LD		HL,ACFLGD
  626B  77            			LD		(HL),A
  626C  23            			INC		HL
  626D  77            			LD		(HL),A
                      	; ********************************************************
  626E  211801        			LD		HL,0118H         ;1文字目、24行目へカーソルを移動
  6271  CDA903        			CALL	CSR
  6274  CDCA5F        			CALL	MONCLF
  6277  E1            			POP		HL
  6278  CD3660        			CALL	RCVBYTE          ;ヘッダー受信
  627B  FE3A          			CP		3AH              ;3AHであれば続行
  627D  280F          			JR		Z,MCNLOAD
  627F  215367        			LD		HL,MSG_F2        ;3AH以外ならエラー
  6282  CDED52        			CALL	MSGOUT
  6285  CDCA5F        			CALL	MONCLF
  6288  CD5003        			CALL	DISPBL
                      	;************************* MONHOTの扱い方を修正 2023.5.30 ****************************
  628B  C3D360        			JP		MONHOT
                      	;		JP		CMD1
                      			
  628E  21AB65        	MCNLOAD:LD		HL,MSG_LD        ;LOADING表示
  6291  CDED52        			CALL	MSGOUT
                      	
                      	; *********** LOAD中専用のスタックポインタとすることでCLEAR文でSPを変更していなくてもE800H以降に正常にLOAD出来るよう対処 2023.5.30 ********
  6294  ED73FFFF      			LD		(0FFFFH),SP
  6298  31FEFF        			LD		SP,0FFFEH
                      	
  629B  213EFF        			LD		HL,SADRS+1       ;SADRS取得
  629E  CD3660        			CALL	RCVBYTE
  62A1  77            			LD		(HL),A
  62A2  2B            			DEC		HL
  62A3  CD3660        			CALL	RCVBYTE
  62A6  77            			LD		(HL),A
  62A7  CD3660        			CALL	RCVBYTE          ;チェックサム廃棄
                      			
  62AA  2A3DFF        			LD		HL,(SADRS)
  62AD  CD3660        	MCLD1:	CALL	RCVBYTE          ;ヘッダー3AH受信、廃棄
  62B0  CD3660        			CALL	RCVBYTE          ;データ長
  62B3  A7            			AND		A
  62B4  280F          			JR		Z,MCLD3          ;データ長が0なら終了
  62B6  47            			LD		B,A
  62B7  CD3660        	MCLD2:	CALL	RCVBYTE          ;実データ受信
                      	; ************* オートラン機能の読み替えを追加 ****** 2022.8.21 ************
                      	;		LD		(HL),A
  62BA  CDD162        			CALL	AUTOCHK
                      	;****************************************************************
  62BD  23            			INC		HL
  62BE  10F7          			DJNZ	MCLD2
  62C0  CD3660        			CALL	RCVBYTE          ;チェックサム廃棄
  62C3  18E8          			JR		MCLD1
  62C5  CD3660        	MCLD3:	CALL	RCVBYTE          ;チェックサム廃棄
  62C8  21B465        			LD		HL,MSG_OK        ;OK表示
  62CB  CDED52        			CALL	MSGOUT
                      	;************************* MONHOTの扱い方を修正 2023.5.30 ****************************
  62CE  C3D360        			JP		MONHOT           ;SPを復帰してコマンド待ちへ
                      	;		JP		CMD1
                      	
                      	; ************ ファンクションキーエリアへの書込みなら書き換え ** 2022.8.21 **
                      	; HL : 書き込みアドレス
                      	; A  : 書き込みデータ
                      	; CTRL+B -> CTRL+C
                      	; CLOAD  -> LOAD
                      	; "xxxx" -> ""
  62D1  D5            	AUTOCHK:PUSH	DE
  62D2  C5            			PUSH	BC
  62D3  EB            			EX		DE,HL
  62D4  214015        			LD		HL,1540H         ;-(EAC0H)
  62D7  19            			ADD		HL,DE
  62D8  D27A63        			JP		NC,ACRET         ;EAC0H未満なら通常書込み
  62DB  21E414        			LD		HL,14E4H         ;-(EB1BH+1)
  62DE  19            			ADD		HL,DE
  62DF  DA7A63        			JP		C,ACRET          ;EB1CH以上なら通常書込み
  62E2  FE02          			CP		02H
  62E4  2005          			JR		NZ,ACHK1         ;CTRL+B -> CTRL+C
  62E6  3E03          			LD		A,03H
  62E8  C37A63        			JP		ACRET            ;CRTL+Cに書き換えてリターン
                      			
  62EB  47            	ACHK1:	LD		B,A
  62EC  2140FF        			LD		HL,ACFLGD        ;ダブルコーテーションフラグをCHK
  62EF  7E            			LD		A,(HL)
  62F0  A7            			AND		A
  62F1  280E          			JR		Z,ACHK11         ;フラグOFFなら次のCHKへ
  62F3  78            			LD		A,B
  62F4  FE22          			CP		'"'              ;フラグONならダブルコーテーションかCHK
  62F6  2004          			JR		NZ,ACHK10        ;ダブルコーテーションではないなら書き込まずにリターン
  62F8  AF            			XOR		A                ;ダブルコーテーションならフラグをリセット
  62F9  77            			LD		(HL),A
  62FA  187D          			JR		ACHK40           ;ダブルコーテーションを書き込んでリターン
                      	
  62FC  EB            	ACHK10:	EX		DE,HL
  62FD  2B            			DEC		HL               ;フラグONでダブルコーテーションでないなら書き込まない。書き込みアドレスもINCしないでリターン
  62FE  C37C63        			JP		ACRET2
                      	
  6301  78            	ACHK11:	LD		A,B
  6302  FE22          			CP		'"'              ;フラグOFFならダブルコーテーションかCHK
  6304  2008          			JR		NZ,ACHK20        ;ダブルコーテーションでないなら次のCHKへ
  6306  3E01          			LD		A,01H            ;ダブルコーテーションならフラグON
  6308  77            			LD		(HL),A
  6309  AF            			XOR		A
  630A  23            			INC		HL
  630B  77            			LD		(HL),A           ;CLOADフラグをリセット
  630C  186B          			JR		ACHK40           ;ダブルコーテーションを書き込んでリターン
                      	
  630E  2141FF        	ACHK20:	LD		HL,ACFLGC        ;CLOADフラグをCHK
  6311  7E            			LD		A,(HL)
  6312  FE00          			CP		00H              ;一致無し
  6314  282D          			JR		Z,ACHK30
  6316  FE01          			CP		01H              ;'C'まで一致
  6318  2837          			JR		Z,ACHK31
  631A  FE02          			CP		02H              ;'L'まで一致
  631C  2841          			JR		Z,ACHK32
  631E  FE03          			CP		03H              ;'0'まで一致
  6320  284B          			JR		Z,ACHK33
  6322  78            			LD		A,B              ;'A'まで一致
  6323  FE44          			CP		'D'
  6325  2804          			JR		Z,ACHK39
  6327  FE64          			CP		'd'
  6329  204E          			JR		NZ,ACHK40
  632B  AF            	ACHK39:	XOR		A                ;'CLOAD'が一致
  632C  77            			LD		(HL),A           ;CLOADフラグをリセット
  632D  EB            			EX		DE,HL            ;書き込みポインタを4つ戻して'LOAD'に上書き
  632E  2B            			DEC		HL
  632F  2B            			DEC		HL
  6330  2B            			DEC		HL
  6331  2B            			DEC		HL
  6332  3E4C          			LD		A,'L'
  6334  77            			LD		(HL),A
  6335  23            			INC		HL
  6336  3E4F          			LD		A,'O'
  6338  77            			LD		(HL),A
  6339  23            			INC		HL
  633A  3E41          			LD		A,'A'
  633C  77            			LD		(HL),A
  633D  23            			INC		HL
  633E  3E44          			LD		A,'D'
  6340  77            			LD		(HL),A
  6341  1839          			JR		ACRET2
                      	
  6343  78            	ACHK30:	LD		A,B
  6344  FE43          			CP		'C'
  6346  2804          			JR		Z,ACHK301
  6348  FE63          			CP		'c'
  634A  2033          			JR		NZ,ACHK41
  634C  3E01          	ACHK301:LD		A,01H            ;'C'が一致
  634E  77            			LD		(HL),A
  634F  1828          			JR		ACHK40
                      	
  6351  78            	ACHK31:	LD		A,B
  6352  FE4C          			CP		'L'
  6354  2804          			JR		Z,ACHK311
  6356  FE6C          			CP		'l'
  6358  2025          			JR		NZ,ACHK41
  635A  3E02          	ACHK311:LD		A,02H            ;'L'が一致
  635C  77            			LD		(HL),A
  635D  181A          			JR		ACHK40
                      	
  635F  78            	ACHK32:	LD		A,B
  6360  FE4F          			CP		'O'
  6362  2804          			JR		Z,ACHK321
  6364  FE6F          			CP		'o'
  6366  2017          			JR		NZ,ACHK41
  6368  3E03          	ACHK321:LD		A,03H            ;'O'が一致
  636A  77            			LD		(HL),A
  636B  180C          			JR		ACHK40
                      	
  636D  78            	ACHK33:	LD		A,B
  636E  FE41          			CP		'A'
  6370  2804          			JR		Z,ACHK331
  6372  FE61          			CP		'a'
  6374  2009          			JR		NZ,ACHK41
  6376  3E04          	ACHK331:LD		A,04H            ;'A'が一致
  6378  77            			LD		(HL),A
                      			
  6379  78            	ACHK40:	LD		A,B
                      	
  637A  EB            	ACRET:	EX		DE,HL
  637B  77            			LD		(HL),A           ;取り敢えず書き込み
  637C  C1            	ACRET2:	POP		BC
  637D  D1            			POP		DE
  637E  C9            			RET
                      	
  637F  AF            	ACHK41:	XOR		A                ;'CLOAD'と一致しなかったのでフラグをリセットして書き込み
  6380  77            			LD		(HL),A
  6381  18F6          			JR		ACHK40
                      	
                      	;********** 5F9EH READ ONE BYTE FROM CMTの代替 *********
  6383  3E72          	D_5F9E:	LD		A,72H            ;コマンド72Hを送信
  6385  CD0D64        			CALL	STCD
  6388  CD3660        			CALL	RCVBYTE          ;1Byteのみ受信
  638B  C9            			RET
                      	
                      	;**** コマンド、ファイル名送信 (IN:A コマンドコード HL:ファイルネームの先頭)****
  638C  23            	STCMD:	INC		HL
  638D  CD4664        			CALL	STFN             ;空白除去
  6390  E5            			PUSH	HL
  6391  CD0D64        			CALL	STCD             ;コマンドコード送信
  6394  E1            			POP		HL
  6395  A7            			AND		A                ;00以外ならERROR
  6396  C27464        			JP		NZ,SDERR
  6399  CD5A64        			CALL	STFS             ;ファイルネーム送信
  639C  A7            			AND		A                ;00以外ならERROR
  639D  C27464        			JP		NZ,SDERR
  63A0  C9            			RET
                      	
                      	;************ 5F3AH READ FROM TAPEの代替 (EMI PLAYER用)*******************
  63A1                	D_5F3A:
                      	;************ 連続してAUTO STARTをするためにフラグポイント再設定 (正解なのかは自信なし)************
  63A1  21C0EA        			LD		HL,FKDEF
  63A4  22C0ED        			LD		(FKPINT),HL
                      	;**************************************************************************************
                      			
                      	;ファイル名指定なしとしてMONITOR Lコマンド実行 *********************
  63A7  21B265        			LD		HL,DEFCR-1
  63AA  C35E62        			JP		MONLOAD
                      	
                      	;************ Wコマンド .CMT SAVE ********************************
  63AD  23            	MONSAVE:INC		HL
  63AE  CD4664        			CALL	STFN
  63B1  CD1464        			CALL	HLHEX            ;4桁の16進数であればSADRSにセットして続行
  63B4  D8            			RET		C
                      	;		JP		C,MONERR
  63B5  223DFF        			LD		(SADRS),HL       ;SARDS保存
  63B8  EB            			EX		DE,HL
                      	
  63B9  CD4664        			CALL	STFN
  63BC  CD1464        			CALL	HLHEX            ;4桁の16進数であればEADRSにセットして続行
  63BF  D8            			RET		C
                      	;		JP		C,MONERR
  63C0  223FFF        			LD		(EADRS),HL       ;EARDS保存
                      	
  63C3  D5            			PUSH	DE
  63C4  E5            			PUSH	HL
  63C5  ED5B3DFF      			LD		DE,(SADRS)
  63C9  ED52          			SBC		HL,DE
  63CB  E1            			POP		HL
  63CC  D1            			POP		DE
  63CD  D8            			RET		C
                      	;		JP		C,MONERR         ;EADRSがSADRSより小さければエラー
                      	
  63CE  EB            			EX		DE,HL
  63CF  2B            			DEC		HL
                      			
  63D0  3E70          			LD		A,70H            ;コマンド70Hを送信
  63D2  CD8C63        			CALL	STCMD
                      	;************************* MONHOTの扱い方を修正 2023.5.30 ****************************
  63D5  C2D360        			JP		NZ,MONHOT
                      	;		JP		NZ,CMD1
                      	
  63D8  21BA65        			LD		HL,MSG_WR        ;WRITING表示
  63DB  CDED52        			CALL	MSGOUT
                      	
  63DE  2A3DFF        			LD		HL,(SADRS)       ;SADRSを送信
  63E1  7D            			LD		A,L
  63E2  CD4960        			CALL	SNDBYTE
  63E5  7C            			LD		A,H
  63E6  CD4960        			CALL	SNDBYTE
  63E9  ED5B3FFF      			LD		DE,(EADRS)       ;送信する全体バイト数を算出するためにEADRSを送信
  63ED  7B            			LD		A,E
  63EE  CD4960        			CALL	SNDBYTE
  63F1  7A            			LD		A,D
  63F2  CD4960        			CALL	SNDBYTE
  63F5  7E            	MONSV1:	LD		A,(HL)           ;SADRSからEADRSまでを送信
  63F6  CD4960        			CALL	SNDBYTE
  63F9  7C            			LD		A,H
  63FA  BA            			CP		D
  63FB  2004          			JR		NZ,MONSV2
  63FD  7D            			LD		A,L
  63FE  BB            			CP		E
  63FF  2803          			JR		Z,MONSV3         ;HL = DE までLOOP
  6401  23            	MONSV2:	INC		HL
  6402  18F1          			JR		MONSV1
  6404  21B465        	MONSV3:	LD		HL,MSG_OK        ;OK表示
  6407  CDED52        			CALL	MSGOUT
                      	;************************* MONHOTの扱い方を修正 2023.5.30 ****************************
  640A  C3D360        			JP		MONHOT
                      	;		JP		CMD1
                      	
                      	;**** コマンド送信 (IN:A コマンドコード)****
  640D  CD4960        	STCD:	CALL	SNDBYTE          ;Aレジスタのコマンドコードを送信
  6410  CD3660        			CALL	RCVBYTE          ;状態取得(00H=OK)
  6413  C9            			RET
                      	
                      	;**** HLからの4Byteが16進数を表すアスキーコードであれば16進数に変換してHLに代入 **************
  6414  EB            	HLHEX:	EX		DE,HL
  6415  210000        			LD		HL,0000H
  6418  0604          			LD		B,04H
  641A  1A            	HLHEX1:	LD		A,(DE)
  641B  13            			INC		DE
  641C  CDC15F        			CALL	AZLCNV
  641F  CD395E        			CALL	HEXCHK
  6422  DA2B64        			JP		C,HLHEX2
  6425  CD4B5E        			CALL	BINCV4
  6428  10F0          			DJNZ	HLHEX1
  642A  AF            			XOR		A
  642B  C9            	HLHEX2:	RET
                      	
                      	;**** HLからの2Byteが16進数を表すアスキーコードであれば16進数に変換してAに代入 **************
  642C  E5            	TWOHEX:	PUSH	HL
  642D  210000        			LD		HL,0000H
  6430  0602          			LD		B,02H
  6432  1A            	TWHEX1:	LD		A,(DE)
  6433  13            			INC		DE
  6434  CDC15F        			CALL	AZLCNV
  6437  CD395E        			CALL	HEXCHK
  643A  DA4464        			JP		C,TWHEX2
  643D  CD4B5E        			CALL	BINCV4
  6440  10F0          			DJNZ	TWHEX1
  6442  AF            			XOR		A
  6443  7D            			LD		A,L
  6444                	TWHEX2:
  6444  E1            			POP		HL
  6445  C9            			RET
                      	
                      	;****** FILE NAMEが取得できるまでスペース、ダブルコーテーションを読み飛ばし (IN:HL コマンド文字の次の文字 OUT:HL ファイルネームの先頭)*********
  6446  F5            	STFN:	PUSH	AF
  6447  7E            	STFN1:	LD		A,(HL)
  6448  FE20          			CP		20H
  644A  2804          			JR		Z,STFN2
  644C  FE22          			CP		22H
  644E  2003          			JR		NZ,STFN3
  6450  23            	STFN2:	INC		HL               ;ファイルネームまでスペース読み飛ばし
  6451  18F4          			JR		STFN1
  6453  F1            	STFN3:	POP		AF
  6454  C9            			RET
                      	
  6455                	STSV2:                           ;ファイルネームの取得に失敗
  6455  213466        			LD		HL,MSG_FNAME
  6458  1856          			JR		ERRMSG
                      	
                      	;**** ファイルネーム送信(IN:HL ファイルネームの先頭) ******
  645A  0620          	STFS:	LD		B,20H
  645C  7E            	STFS1:	LD		A,(HL)           ;FNAME送信
  645D  FE22          			CP		22H
  645F  2003          			JR		NZ,STFS2
  6461  23            			INC		HL
  6462  18F8          			JR		STFS1
  6464  CD4960        	STFS2:	CALL	SNDBYTE
  6467  23            			INC		HL
  6468  05            			DEC		B
  6469  20F1          			JR		NZ,STFS1
  646B  3E0D          			LD		A,0DH
  646D  CD4960        			CALL	SNDBYTE
  6470  CD3660        			CALL	RCVBYTE          ;状態取得(00H=OK)
  6473  C9            			RET
                      	
                      	;************** エラー内容表示 *****************************
  6474                	SDERR:
  6474  F5            			PUSH	AF
  6475  FEF0          			CP		0F0H
  6477  2005          			JR		NZ,ERR3
  6479  212C67        			LD		HL,MSG_F0        ;SD-CARD INITIALIZE ERROR
  647C  1832          			JR		ERRMSG
  647E  FEF1          	ERR3:	CP		0F1H
  6480  2005          			JR		NZ,ERR4
  6482  214567        			LD		HL,MSG_F1        ;NOT FIND FILE
  6485  1829          			JR		ERRMSG
  6487  FEF3          	ERR4:	CP		0F3H
  6489  2005          			JR		NZ,ERR5
  648B  216367        			LD		HL,MSG_F3        ;FILE EXIST
  648E  1820          			JR		ERRMSG
  6490  FEF4          	ERR5:	CP		0F4H
  6492  2005          			JR		NZ,ERR6
  6494  214666        			LD		HL,MSG_CMD       ;COMMAND FAILED
  6497  1817          			JR		ERRMSG
  6499  FEF6          	ERR6:	CP		0F6H
  649B  2005          			JR		NZ,ERR99
  649D  213466        			LD		HL,MSG_FNAME     ;PARAMETER FAILED
  64A0  180E          			JR		ERRMSG
  64A2  CD835E        	ERR99:	CALL	MONBHX
  64A5  7A            			LD		A,D
  64A6  CD5702        			CALL	CONOUT
  64A9  7B            			LD		A,E
  64AA  CD5702        			CALL	CONOUT
  64AD  218F67        			LD		HL,MSG99         ;その他ERROR
  64B0  CDED52        	ERRMSG:	CALL	MSGOUT
  64B3  CDCA5F        			CALL	MONCLF
  64B6  CD5003        			CALL	DISPBL
  64B9  F1            			POP		AF
  64BA  C9            			RET
                      	
                      	;************** BASIC CMT LOAD *********************
  64BB                	CMDLOAD:
  64BB  2B            			DEC		HL
  64BC  3E73          			LD		A,73H            ;コマンド73Hを送信
  64BE  CD8C63        			CALL	STCMD
  64C1  C29D65        			JP		NZ,RETBC
                      	
  64C4  211801        			LD		HL,0118H         ;1文字目、24行目へカーソルを移動
  64C7  CDA903        			CALL	CSR
  64CA  CDCA5F        			CALL	MONCLF
                      			
  64CD  CD3660        			CALL	RCVBYTE          ;ヘッダー受信
  64D0  FED3          			CP		0D3H             ;D3Hでなければエラー
  64D2  280C          			JR		Z,CMDLD
  64D4  217B67        			LD		HL,MSG_F6        ;NOT BASIC PROGRAM
  64D7  CDED52        			CALL	MSGOUT
  64DA  CD5003        			CALL	DISPBL
  64DD  C39D65        			JP		RETBC
                      			
  64E0                	CMDLD:	
  64E0  213EEF        			LD		HL,FILNAM        ;CMTファイル中に記載のファイルネーム6文字を受信
  64E3  0606          			LD		B,06H
  64E5  CD3660        	CMDLD2:	CALL	RCVBYTE
  64E8  77            			LD		(HL),A
  64E9  23            			INC		HL
  64EA  10F9          			DJNZ	CMDLD2
                      	
  64EC  21AB65        			LD		HL,MSG_LD        ;LOADING表示
  64EF  CDED52        			CALL	MSGOUT
                      	
  64F2  213EEF        			LD		HL,FILNAM        ;ファイルネーム表示
  64F5  1196EC        			LD		DE,IBUF
  64F8  010600        			LD		BC,0006H
  64FB  EDB0          			LDIR
  64FD  AF            			XOR		A
  64FE  12            			LD		(DE),A
  64FF  2196EC        			LD		HL,IBUF
  6502  CDED52        			CALL	MSGOUT
  6505  CDCA5F        			CALL	MONCLF
                      			
  6508  2A54EB        			LD		HL,(BASSRT)      ;BASICプログラム格納開始位置をHLに設定
                      	
  650B  060C          	CMDLD3:	LD		B,0CH            ;00Hを12個連続で受信するまでループ
  650D  CD3660        	CMDLD4:	CALL	RCVBYTE
  6510  77            			LD		(HL),A
  6511  23            			INC		HL
                      	
  6512  A7            			AND		A
  6513  2802          			JR		Z,CMDLD5
  6515  18F4          			JR		CMDLD3
  6517  05            	CMDLD5:	DEC		B
  6518  2802          			JR		Z,CMDLD6
  651A  18F1          			JR		CMDLD4
                      	
  651C  010700        	CMDLD6:	LD		BC,0007H         ;HLの位置を7つ戻してBASICプログラム終了位置とする
  651F  ED42          			SBC		HL,BC
                      			
  6521  187D          			JR		RETBC2           ;BASICプログラムLOAD後処理
                      			
                      	;********************** BASIC CMT SAVE **********************
  6523                	CMDSAVE:
  6523  E5            			PUSH	HL
  6524  2A54EB        			LD		HL,(BASSRT)      ;BASICプログラム格納開始位置をHLに設定
  6527  7E            			LD		A,(HL)
  6528  23            			INC		HL
  6529  B6            			OR		(HL)
  652A  E1            			POP		HL
  652B  CA6E65        			JP		Z,CMDSV5         ;BASICプログラムが1行もなければエラー
                      	
  652E  2B            			DEC		HL
  652F  3E74          			LD		A,74H            ;コマンド74Hを送信
  6531  CD8C63        			CALL	STCMD
  6534  2036          			JR		NZ,CMDSV4
                      	
  6536  21BA65        			LD		HL,MSG_WR        ;WRITING表示
  6539  CDED52        			CALL	MSGOUT
                      	
  653C  CD865B        			CALL	RENUM9           ;BASICプログラムSAVE前処理
  653F  2A54EB        			LD		HL,(BASSRT)      ;BASSRTからVARBGN-1までを送信
  6542  7D            			LD		A,L
  6543  CD4960        			CALL	SNDBYTE
  6546  7C            			LD		A,H
  6547  CD4960        			CALL	SNDBYTE
  654A  ED5BA0EF      			LD		DE,(VARBGN)
  654E  1B            			DEC		DE
  654F  7B            			LD		A,E
  6550  CD4960        			CALL	SNDBYTE
  6553  7A            			LD		A,D
  6554  CD4960        			CALL	SNDBYTE
  6557  7E            	CMDSV1:	LD		A,(HL)
  6558  CD4960        			CALL	SNDBYTE
  655B  7C            			LD		A,H
  655C  BA            			CP		D
  655D  2004          			JR		NZ,CMDSV2
  655F  7D            			LD		A,L
  6560  BB            			CP		E
  6561  2803          			JR		Z,CMDSV3         ;HL = DE までLOOP
  6563  23            	CMDSV2:	INC		HL
  6564  18F1          			JR		CMDSV1
                      			
  6566                	CMDSV3:
  6566  21B465        			LD		HL,MSG_OK        ;OK表示
  6569  CDED52        			CALL	MSGOUT
                      	;2022.8.4  SYNTAX ERROR回避
  656C  182F          	CMDSV4:	JR		RETBC
                      	
                      	;2022.8.4  SYNTAX ERROR回避
  656E                	CMDSV5:
  656E  CD5003        			CALL	DISPBL
  6571  216E67        			LD		HL,MSG_F5        ;NO BASIC PROGRAM
  6574  1821          			JR		RETBC3
                      	
                      	;************ BASIC FILES ********************
  6576                	CMDFILES:
                      	;2022.7.24 SYNTAX ERROR回避により、廃止
                      	;		PUSH	HL
                      	;		LD		HL,FMSG          ;FILESの後ろにファイル名検索文字を指定すると構文エラーになるため、別行として入力
                      	;		CALL	MSGOUT
                      	;		CALL	LINPUT
                      	;		INC		HL
  6576  CD4664        			CALL	STFN             ;入力文字列取得
  6579  EB            			EX		DE,HL
  657A  219C67        			LD		HL,DEFDIR2       ;行頭に'LOAD 'を付けることでカーソルを移動させリターンで実行できるように
  657D  010600        			LD		BC,D2END-DEFDIR2
  6580  CDBD61        			CALL	DIRLIST          ;DIRLIST本体をコール
                      	;2022.8.4  SYNTAX ERROR回避
  6583  1818          			JR		RETBC
                      			
                      	;************ BASIC KILL ***************************
  6585                	CMDKILL:
  6585  2B            			DEC		HL
  6586  3E75          			LD		A,75H            ;コマンド75Hを送信
  6588  CD8C63        			CALL	STCMD
  658B  2010          			JR		NZ,RETBC         ;ファイル名が送信できなかった。
  658D  CD3660        			CALL	RCVBYTE
  6590  A7            			AND		A
  6591  C2A365        			JP		NZ,CMDKL1         ;ファイルが存在しない
  6594  21C365        			LD		HL,MSG_KL
  6597  CDED52        	RETBC3:	CALL	MSGOUT
  659A  CDCA5F        			CALL	MONCLF
                      	
                      	;2022.8.4 SYNTAX ERROR回避 
                      	;FILES SAVE KILLからBASICへ正しい戻り方が判らなかったため、LOAD後処理で戻ることとする。
  659D                	RETBC:
  659D  2AA0EF        			LD		HL,(VARBGN)
                      	;LOAD後処理
  65A0                	RETBC2:
  65A0  C38B1F        			JP		AFTLOAD          ;BASICプログラムLOAD後処理
                      			
                      	;2022.8.4  SYNTAX ERROR回避
  65A3                	CMDKL1:
  65A3  CD5003        			CALL	DISPBL
  65A6  214567        			LD		HL,MSG_F1        ;NOT FIND FILE
  65A9  18EC          			JR		RETBC3
                      	
  65AB                	MSG_LD:
  65AB  4C4F4144494E47			DB		'LOADING '
  65B3  00            	DEFCR:	DB		00H
                      	
  65B4                	MSG_OK:
  65B4  4F4B21        			DB		'OK!'
  65B7  0D0A00        			DB		0DH,0AH,00H
                      	
  65BA                	MSG_WR:
  65BA  57524954494E47			DB		'WRITING '
  65C2  00            			DB		00H
                      	
  65C3                	MSG_KL:
  65C3  46494C45204445			DB		'FILE DELETED!'
  65D0  00            			DB		00H
                      	
  65D1                	MSG_AD1:
  65D1  41445253202B30			DB		'ADRS +0 +1 +2 +3 +4 +5 +6 +7 01234567'
  65F6  0D0A00        			DB		0DH,0AH,00H
                      			
  65F9                	MSG_AD2:
  65F9  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:ESC'
  6612  0D0A00        			DB		0DH,0AH,00H
                      			
  6615                	MSG_KEY1:
  6615  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:'
  662B  00            			DB		00H
  662C                	MSG_KEY2:
  662C  204F5220455343			DB		' OR ESC'
  6633  00            			DB		00H
                      	
  6634                	MSG_FNAME:
  6634  504152414D4554			DB		'PARAMETAR FAILED!'
  6645  00            			DB		00H
                      			
  6646                	MSG_CMD:
  6646  434F4D4D414E44			DB		'COMMAND FAILED!',0DH,0AH
  6657  20422020202020			DB		' B             : Return Basic',0DH,0AH
  6676  2044206E6E6E6E			DB		' D nnnn        : Memory Dump',0DH,0AH
  6694  20462078202020			DB		' F x           : Find SD File',0DH,0AH
  66B3  2047206E6E6E6E			DB		' G nnnn        : Exec Program',0DH,0AH
  66D2  204C2078202020			DB		' L x           : Load From SD',0DH,0AH
  66F1  2053206E6E6E6E			DB		' S nnnn nn..   : Memory Write',0DH,0AH
  6710  2057206E6E6E6E			DB		' W nnnn nnnn x : Save To SD'
  672B  00            			DB		00H
                      			
  672C                	MSG_F0:
  672C  53442D43415244			DB		'SD-CARD INITIALIZE ERROR'
  6744  00            			DB		00H
                      			
  6745                	MSG_F1:
  6745  4E4F542046494E			DB		'NOT FIND FILE'
  6752  00            			DB		00H
                      			
  6753                	MSG_F2:
  6753  4E4F54204F424A			DB		'NOT OBJECT FILE'
  6762  00            			DB		00H
                      			
  6763                	MSG_F3:
  6763  46494C45204558			DB		'FILE EXIST'
  676D  00            			DB		00H
                      			
  676E                	MSG_F5:
  676E  4E4F2050524F47			DB		'NO PROGRAM!!'
  677A  00            			DB		00H
                      			
  677B                	MSG_F6:
  677B  4E4F5420424153			DB		'NOT BASIC PROGRAM'
  678C  0D0A00        			DB		0DH,0AH,00H
                      			
  678F                	MSG99:
  678F  204552524F52  			DB		' ERROR'
  6795  00            			DB		00H
                      			
  6796  2A53          	MSG_FDW:	DB		'*S'
  6798  00            			DB		00H
                      	
  6799                	DEFDIR:
  6799  2A4C20        			DB		'*L '
  679C                	DEND:
                      	
  679C                	DEFDIR2:
  679C  4C4F41442022  			DB		'LOAD ',22H
  67A2                	D2END:
                      	
                      	
  7FFC                			ORG		7FFCH
                      	
                      	;************* 代替MONITORへジャンプするための設定 *************
  7FFC  C37960        			JP		MONINI
  7FFF  55            			DB		55H
                      			
  8000                			END
