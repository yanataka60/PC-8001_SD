			  Z80 ASSEMBLER - ZASM VER 1.6
                      	;2022.7.24 FILESでSYNTAX ERRORとなる事象を回避、SERCH FILEと聞いてくる仕様を削除
                      	;2022.7.25 FILESを↑キーで打ち切ったとき時々Syntax Errorが発生するため2行戻す動きを廃止
                      	
  0257                	CONOUT		EQU		0257H
  03A9                	CSR			EQU		03A9H
  03F3                	CSRADR		EQU		03F3H
  0BE2                	DSPCSR		EQU		0BE2H
  0FAC                	KYSCAN		EQU		0FACH
  1B7E                	LINPUT		EQU		1B7EH
  1F8B                	AFTLOAD		EQU		1F8BH
  40A6                	CHROUT		EQU		40A6H
  52ED                	MSGOUT		EQU		52EDH
  5B86                	RENUM9		EQU		5B86H
  5E39                	HEXCHK		EQU		5E39H
  5E4B                	BINCV4		EQU		5E4BH
  5E83                	MONBHX		EQU		5E83H
  5EBD                	MONDME		EQU		5EBDH
  5EC0                	MONDHL		EQU		5EC0H
  5FC1                	AZLCNV		EQU		5FC1H
  5FCA                	MONCLF		EQU		5FCAH
  5FD4                	MONSPC		EQU		5FD4H
                      	
  EA63                	CUSPOS		EQU		0EA63H
  EB54                	BASSRT		EQU		0EB54H
  EC96                	IBUF		EQU		0EC96H
  EF3E                	FILNAM		EQU		0EF3EH
  EFA0                	VARBGN		EQU		0EFA0H
  F139                	TBLOAD		EQU		0F139H
  F142                	TBKILL		EQU		0F142H
  F14B                	TBSAVE		EQU		0F14BH
  F14E                	TBFILES		EQU		0F14EH
  FF34                	MONHL		EQU		0FF34H
  FF36                	MONSP		EQU		0FF36H
  FF3D                	SADRS		EQU		0FF3DH
  FF41                	EADRS		EQU		0FF41H
  FF66                	LBUF		EQU		0FF66H
                      	
                      	
                      	;8255 PORT アドレス FCH〜FFH
                      	;0FCH PORTA 送信データ(下位4ビット)
                      	;0FDH PORTB 受信データ(8ビット)
                      	;
                      	;0FEH PORTC Bit
                      	;7 IN  CHK
                      	;6 IN
                      	;5 IN
                      	;4 IN 
                      	;3 OUT
                      	;2 OUT FLG
                      	;1 OUT
                      	;0 OUT
                      	;
                      	;0FFH コントロールレジスタ
                      	
                      	
  6000                	        ORG		6000H
                      	
  6000  4142          			DB		041H,042H         ;拡張ROM認識コード
                      	
  6002  CD0960        			CALL	INIT              ;POWER ONで8255を初期化、LOAD、SAVE、FILES、KILLのジャンプ先を設定
  6005  C9            			RET
                      	
                      	;*********** OPENしているファイルから1BYTE読み出し転送 ****************
                      	;5F9EH代替ルーチン
  6006  C3A462        			JP		D_5F9E
                      	
                      	;**** 8255初期化 ****
                      	;PORTC下位BITをOUTPUT、上位BITをINPUT、PORTBをINPUT、PORTAをOUTPUT
  6009  3E8A          	INIT:	LD		A,8AH
  600B  D3FF          			OUT		(0FFH),A
                      	;出力BITをリセット
  600D  AF            			XOR		A                 ;PORTA <- 0
  600E  D3FC          			OUT		(0FCH),A
  6010  D3FE          			OUT		(0FEH),A          ;PORTC <- 0
                      	
                      	;LOAD、SAVE、FILES、KILLのジャンプ先を設定
  6012  21DF63        			LD		HL,CMDLOAD
  6015  2239F1        			LD		(TBLOAD),HL
                      	
  6018  214664        			LD		HL,CMDSAVE
  601B  224BF1        			LD		(TBSAVE),HL
                      	
  601E  219264        			LD		HL,CMDFILES
  6021  224EF1        			LD		(TBFILES),HL
                      	
  6024  21A664        			LD		HL,CMDKILL
  6027  2242F1        			LD		(TBKILL),HL
                      	
  602A  C9            			RET
                      	
                      	;**** 1BYTE受信 ****
                      	;受信DATAをAレジスタにセットしてリターン
  602B                	RCVBYTE:
  602B  CD6060        			CALL	F1CHK             ;PORTC BIT7が1になるまでLOOP
  602E  DBFD          			IN		A,(0FDH)          ;PORTB -> A
  6030  F5            			PUSH 	AF
  6031  3E05          			LD		A,05H
  6033  D3FF          			OUT		(0FFH),A          ;PORTC BIT2 <- 1
  6035  CD6760        			CALL	F2CHK             ;PORTC BIT7が0になるまでLOOP
  6038  3E04          			LD		A,04H
  603A  D3FF          			OUT		(0FFH),A          ;PORTC BIT2 <- 0
  603C  F1            			POP 	AF
  603D  C9            			RET
                      			
                      	;**** 1BYTE送信 ****
                      	;Aレジスタの内容をPORTA下位4BITに4BITずつ送信
  603E                	SNDBYTE:
  603E  F5            			PUSH	AF
  603F  1F            			RRA
  6040  1F            			RRA
  6041  1F            			RRA
  6042  1F            			RRA
  6043  E60F          			AND		0FH
  6045  CD4F60        			CALL	SND4BIT
  6048  F1            			POP		AF
  6049  E60F          			AND		0FH
  604B  CD4F60        			CALL	SND4BIT
  604E  C9            			RET
                      	
                      	;**** 4BIT送信 ****
                      	;Aレジスタ下位4ビットを送信する
  604F                	SND4BIT:
  604F  D3FC          			OUT		(0FCH),A
  6051  3E05          			LD		A,05H
  6053  D3FF          			OUT		(0FFH),A          ;PORTC BIT2 <- 1
  6055  CD6060        			CALL	F1CHK             ;PORTC BIT7が1になるまでLOOP
  6058  3E04          			LD		A,04H
  605A  D3FF          			OUT		(0FFH),A          ;PORTC BIT2 <- 0
  605C  CD6760        			CALL	F2CHK
  605F  C9            			RET
                      			
                      	;**** BUSYをCHECK(1) ****
                      	; 82H BIT7が1になるまでLOP
  6060  DBFE          	F1CHK:	IN		A,(0FEH)
  6062  E680          			AND		80H               ;PORTC BIT7 = 1?
  6064  28FA          			JR		Z,F1CHK
  6066  C9            			RET
                      	
                      	;**** BUSYをCHECK(0) ****
                      	; 82H BIT7が0になるまでLOOP
  6067  DBFE          	F2CHK:	IN		A,(0FEH)
  6069  E680          			AND		80H               ;PORTC BIT7 = 0?
  606B  20FA          			JR		NZ,F2CHK
  606D  C9            			RET
                      	
                      	;************* 代替MONITOR START *************************
  606E  2234FF        	MONINI:	LD		(MONHL),HL
  6071  ED7336FF      			LD		(MONSP),SP
                      			
                      	;		PUSH	HL                ;タイトルを表示するとBASICから戻ったときに自動実行が途切れてしまうため削除
                      	;		LD		HL,MONMSG
                      	;		CALL	MSGOUT
                      	;		POP		HL
                      			
  6075  01C960        			LD		BC,MONERR         ;BASICに戻るときPOPしているのでお約束らしい
  6078  C5            			PUSH	BC
  6079                	CMD1:
  6079  3E2A          			LD		A,'*'
  607B  CD5702        			CALL	CONOUT            ;プロンプト表示
  607E  CDE20B        			CALL	DSPCSR
  6081  CD7E1B        			CALL	LINPUT            ;スクリーンエディットを使いたいために使用、このためCTRL+BではBASICに戻れない
                      									  ;HLにIBUF-1が入って戻っている LD HL,IBUF-1
  6084                	CMD2:
  6084  3832          			JR		C,MONCTB          ;CTRL+Bの代替、CTRL+CでBASICに復帰
  6086  23            			INC		HL
  6087  7E            			LD		A,(HL)
  6088  FE2A          			CP		'*'               ;入力時とスクリーンエディット時でIBUFに入る位置が変わるための対処、「*」ならポインタを進める
  608A  28F8          			JR		Z,CMD2
  608C  7E            			LD		A,(HL)
  608D  CDC15F        			CALL	AZLCNV            ;入力文字を大文字へ変換
  6090  FE42          			CP		'B'
  6092  2824          			JR		Z,MONCTB          ;CTRL+Bの代替、BコマンドでもBASICに復帰
  6094  FE44          			CP		'D'
  6096  CAD160        			JP		Z,STMD            ;Dコマンド
  6099  FE53          			CP		'S'
  609B  CA4D61        			JP		Z,STMW            ;Sコマンド
  609E  FE46          			CP		'F'
  60A0  CA8E61        			JP		Z,STLT            ;Fコマンド
  60A3  FE4C          			CP		'L'
  60A5  CA4462        			JP		Z,MONLOAD         ;Lコマンド
  60A8  FE57          			CP		'W'
  60AA  CAC562        			JP		Z,MONSAVE         ;Wコマンド
  60AD  FE47          			CP		'G'
  60AF  280D          			JR		Z,GOCMD           ;Gコマンド
  60B1  3EF4          			LD		A,0F4H
  60B3  CD9263        			CALL	SDERR             ;コマンドエラー
  60B6  18C1          			JR		CMD1
                      			
                      	;MONMSG:	DEFB	0CH,'**PC-8001_SD Monitor **',0DH,0AH,00H
                      	
                      	;************ Bコマンド BASIC復帰 ***********************
  60B8  C1            	MONCTB:	POP		BC
  60B9  2A34FF        			LD		HL,(MONHL)
  60BC  FB            			EI
  60BD  C9            			RET
                      	
                      	;************ Gコマンド アドレスxxxxへジャンプ ***************
  60BE  23            	GOCMD:	INC		HL
  60BF  CD6463        			CALL	STFN
  60C2  CD3263        			CALL	HLHEX             ;4桁の16進数であればHLにセットして続行
  60C5  DAC960        			JP		C,MONERR
  60C8  E9            			JP		(HL)              ;HLの示すアドレスにジャンプ
                      	
                      	;パラメータエラー処理
  60C9  3EF6          	MONERR:	LD		A,0F6H
  60CB  CD9263        			CALL	SDERR
  60CE  C37960        			JP		CMD1
                      	
                      	;************ Dコマンド アドレスxxxxからのMEMORYをDUMP **********************
  60D1  23            	STMD:	INC		HL
  60D2  CD6463        			CALL	STFN
  60D5  CD3263        			CALL	HLHEX             ;4桁の16進数であればSADRSにセットして続行
  60D8  DAC960        			JP		C,MONERR
  60DB  223DFF        			LD		(SADRS),HL        ;SARDS保存
                      	
  60DE  21E864        	STMD6:	LD		HL,MSG_AD1        ;DUMP TITLE表示
  60E1  CDED52        			CALL	MSGOUT
  60E4  0E10          			LD		C,10H             ;16行(128Byte)を表示
  60E6  2A3DFF        	STMD7:	LD		HL,(SADRS)        ;アドレス表示
  60E9  CDC05E        			CALL	MONDHL
  60EC  CDD45F        			CALL	MONSPC
                      	
                      			
  60EF  0608          			LD		B,08H             ;一行(8Byte)のデータを16進数表示
  60F1  CDBD5E        	STMD0:	CALL	MONDME
  60F4  CDD45F        			CALL	MONSPC
  60F7  CDAC0F        			CALL	KYSCAN
  60FA  FE1B          			CP		1BH               ;表示途中でもESCで打ち切り
  60FC  284C          			JR		Z,STMD4
  60FE  23            			INC		HL
  60FF  05            			DEC		B
  6100  20EF          			JR		NZ,STMD0
                      	
  6102  2A3DFF        			LD		HL,(SADRS)
  6105  0608          			LD		B,08H             ;一行(8Byte)のデータをキャラクタ表示
  6107  7E            	STMD2:	LD		A,(HL)
  6108  FE20          			CP		20H               ;文字化けに対処 20H未満なら20Hに置き換え
  610A  3002          			JR		NC,STMD8
  610C  3E20          			LD		A,20H
  610E  CD5702        	STMD8:	CALL	CONOUT
  6111  CDAC0F        			CALL	KYSCAN
  6114  FE1B          			CP		1BH               ;表示途中でもESCで打ち切り
  6116  2832          			JR		Z,STMD4
  6118  23            			INC		HL
  6119  05            			DEC		B
  611A  20EB          			JR		NZ,STMD2
                      	
  611C  223DFF        			LD		(SADRS),HL
  611F  CDCA5F        			CALL	MONCLF
                      	
  6122  0D            			DEC		C
  6123  20C1          			JR		NZ,STMD7
                      			
  6125  211065        			LD		HL,MSG_AD2        ;入力待ちメッセージ表示
  6128  CDED52        			CALL	MSGOUT
  612B  CDAC0F        	STMD3:	CALL	KYSCAN            ;1文字入力待ち
  612E  A7            			AND		A
  612F  28FA          			JR		Z,STMD3
  6131  FE1B          			CP		1BH               ;ESCで打ち切り
  6133  2815          			JR		Z,STMD4
  6135  CDC15F        			CALL	AZLCNV
  6138  FE42          			CP		42H               ;BからBACK
  613A  200B          			JR		NZ,STMD5
  613C  2A3DFF        			LD		HL,(SADRS)
  613F  110001        			LD		DE,0100H
  6142  ED52          			SBC		HL,DE
  6144  223DFF        			LD		(SADRS),HL
  6147  C3DE60        	STMD5:	JP		STMD6
  614A  C37960        	STMD4:	JP		CMD1
                      	
                      	;************ Sコマンド アドレスxxxxからMEMORYに書き込み **********************
  614D  23            	STMW:	INC		HL
  614E  CD6463        			CALL	STFN
  6151  CD3263        			CALL	HLHEX             ;4桁の16進数であればHLにセットして続行
  6154  DAC960        			JP		C,MONERR
                      	
  6157  1A            	STSP1:	LD		A,(DE)
  6158  A7            			AND		A
  6159  2830          			JR		Z,STMW9           ;アドレスのみなら終了
  615B  FE20          			CP		20H
  615D  2003          			JR		NZ,STMW1
  615F  13            			INC		DE                ;空白は飛ばす
  6160  18F5          			JR		STSP1
                      	
  6162                	STMW1:
  6162  CD4A63        			CALL	TWOHEX
  6165  380D          			JR		C,STMW8
  6167  77            			LD		(HL),A            ;2桁の16進数があれば(HL)に書き込み
  6168  23            			INC		HL
                      	
  6169  1A            	STSP2:	LD		A,(DE)
  616A  A7            			AND		A               ;一行終了
  616B  2807          			JR		Z,STMW8
  616D  FE20          			CP		20H
  616F  20F1          			JR		NZ,STMW1
  6171  13            			INC		DE                ;空白は飛ばす
  6172  18F5          			JR		STSP2
                      	
  6174  E5            	STMW8:	PUSH	HL
  6175  21AB66        			LD		HL,MSG_FDW        ;行頭に'*S '
  6178  CDED52        			CALL	MSGOUT
  617B  E1            			POP		HL
  617C  E5            			PUSH	HL
  617D  CDC05E        			CALL	MONDHL            ;アドレス表示
  6180  CDD45F        			CALL	MONSPC
  6183  CD7E1B        			CALL	LINPUT            ;行入力繰り返し
                      	
  6186  D1            			POP		DE
  6187  23            			INC		HL
  6188  EB            			EX		DE,HL
  6189  18CC          			JR		STSP1
  618B  C37960        	STMW9:	JP		CMD1
                      	
                      	;************ Fコマンド DIRLIST **********************
  618E  23            	STLT:	INC		HL
  618F  CD6463        			CALL	STFN              ;検索文字列を送信
  6192  EB            			EX		DE,HL
  6193  21AE66        			LD		HL,DEFDIR         ;行頭に'*L 'を付けることでカーソルを移動させリターンで実行できるように
  6196  010300        			LD		BC,DEND-DEFDIR
  6199  CDA361        			CALL	DIRLIST           ;DIRLIST本体をコール
  619C  A7            			AND		A                 ;00以外ならERROR
  619D  C49263        			CALL	NZ,SDERR
  61A0  C37960        			JP		CMD1
                      	
                      	
                      	;**** DIRLIST本体 (HL=行頭に付加する文字列の先頭アドレス BC=行頭に付加する文字列の長さ) ****
                      	;****              戻り値 A=エラーコード ****
  61A3                	DIRLIST:
  61A3  3E83          			LD		A,83H             ;DIRLISTコマンド83Hを送信
  61A5  CD2B63        			CALL	STCD              ;コマンドコード送信
  61A8  A7            			AND		A                 ;00以外ならERROR
  61A9  C24362        			JP		NZ,DLRET
                      			
  61AC  C5            			PUSH	BC
  61AD  0621          			LD		B,21H             ;ファイルネーム検索文字列33文字分を送信
  61AF  1A            	STLT1:	LD		A,(DE)
  61B0  A7            			AND		A
  61B1  2001          			JR		NZ,STLT2
  61B3  AF            			XOR		A
  61B4  CDC15F        	STLT2:	CALL	AZLCNV            ;大文字に変換
  61B7  FE22          			CP		22H               ;ダブルコーテーション読み飛ばし
  61B9  2003          			JR		NZ,STLT3
  61BB  13            			INC		DE
  61BC  18F1          			JR		STLT1
  61BE  CD3E60        	STLT3:	CALL	SNDBYTE           ;ファイルネーム検索文字列を送信
  61C1  13            			INC		DE
  61C2  05            			DEC		B
  61C3  20EA          			JR		NZ,STLT1
  61C5  C1            			POP		BC
                      	
  61C6  CD2B60        			CALL	RCVBYTE           ;状態取得(00H=OK)
  61C9  A7            			AND		A                 ;00以外ならERROR
  61CA  C24362        			JP		NZ,DLRET
                      	
  61CD                	DL1:
  61CD  E5            			PUSH	HL
  61CE  C5            			PUSH	BC
  61CF  1166FF        			LD		DE,LBUF
  61D2  EDB0          			LDIR
  61D4  EB            			EX		DE,HL
  61D5  CD2B60        	DL2:	CALL	RCVBYTE           ;'00H'を受信するまでを一行とする
  61D8  A7            			AND		A
  61D9  280C          			JR		Z,DL3
  61DB  FEFF          			CP		0FFH              ;'0FFH'を受信したら終了
  61DD  281E          			JR		Z,DL4
  61DF  FEFE          			CP		0FEH              ;'0FEH'を受信したら一時停止して一文字入力待ち
  61E1  2821          			JR		Z,DL5
  61E3  77            			LD		(HL),A
  61E4  23            			INC		HL
  61E5  18EE          			JR		DL2
  61E7  3600          	DL3:	LD		(HL),00H
  61E9  2166FF        			LD		HL,LBUF           ;'00H'を受信したら一行分を表示して改行
  61EC  7E            	DL31:	LD		A,(HL)
  61ED  A7            			AND		A
  61EE  2806          			JR		Z,DL33
  61F0  CDA640        			CALL	CHROUT
  61F3  23            			INC		HL
  61F4  18F6          			JR		DL31
  61F6  CDCA5F        	DL33:	CALL	MONCLF
  61F9  C1            			POP		BC
  61FA  E1            			POP		HL
  61FB  18D0          			JR		DL1
  61FD  CD2B60        	DL4:	CALL	RCVBYTE           ;状態取得(00H=OK)
  6200  C1            			POP		BC
  6201  E1            			POP		HL
  6202  183F          			JR		DLRET
                      	
  6204  E5            	DL5:	PUSH	HL
  6205  212C65        			LD		HL,MSG_KEY1       ;HIT ANT KEY表示
  6208  CDED52        			CALL	MSGOUT
  620B  2A63EA        			LD		HL,(CUSPOS)
  620E  CDF303        			CALL	CSRADR
  6211  3E1E          			LD		A,1EH
  6213  77            			LD		(HL),A
  6214  3E1C          			LD		A,1CH
  6216  CD5702        			CALL	CONOUT
  6219  214365        			LD		HL,MSG_KEY2       ;HIT ANT KEY表示
  621C  CDED52        			CALL	MSGOUT
  621F  CDCA5F        			CALL	MONCLF
  6222  E1            			POP		HL
  6223  CDAC0F        	DL6:	CALL	KYSCAN            ;1文字入力待ち
  6226  CDC15F        			CALL	AZLCNV
  6229  A7            			AND		A
  622A  28F7          			JR		Z,DL6
  622C  FE1B          			CP		1BH               ;ESCで打ち切り
  622E  280B          			JR		Z,DL7
  6230  FE1E          			CP		1EH               ;カーソル↑で打ち切り
  6232  2807          			JR		Z,DL9
  6234  FE42          			CP		42H               ;「B」で前ページ
  6236  2805          			JR		Z,DL8
  6238  AF            			XOR		A                 ;それ以外で継続
  6239  1802          			JR		DL8
  623B                	DL9:
                      	;		LD		A,1EH             ;時々Syntax Errorが発生するため廃止
                      	;		CALL	CONOUT
                      	;		LD		A,1EH
                      	;		CALL	CONOUT            ;カーソル↑で打ち切ったときにカーソル2行上へ
  623B  3EFF          	DL7:	LD		A,0FFH            ;0FFH中断コードを送信
  623D  CD3E60        	DL8:	CALL	SNDBYTE
  6240  C3D561        			JP		DL2
                      			
  6243  C9            	DLRET:	RET
                      			
                      	
                      	;************ Lコマンド .CMT LOAD *************************
  6244  3E71          	MONLOAD:LD		A,71H            ;Lコマンド71Hを送信
  6246  CDAD62        			CALL	STCMD
  6249  C27960        			JP		NZ,CMD1
                      	
  624C  E5            			PUSH	HL
  624D  211801        			LD		HL,0118H         ;1文字目、24行目へカーソルを移動
  6250  CDA903        			CALL	CSR
  6253  CDCA5F        			CALL	MONCLF
  6256  E1            			POP		HL
  6257  CD2B60        			CALL	RCVBYTE          ;ヘッダー受信
  625A  FE3A          			CP		3AH              ;3AHであれば続行
  625C  280C          			JR		Z,MCNLOAD
  625E  216A66        			LD		HL,MSG_F2        ;3AH以外ならエラー
  6261  CDED52        			CALL	MSGOUT
  6264  CDCA5F        			CALL	MONCLF
  6267  C37960        			JP		CMD1
                      			
  626A  21C064        	MCNLOAD:LD		HL,MSG_LD        ;LOADING表示
  626D  CDED52        			CALL	MSGOUT
                      	
  6270  213EFF        			LD		HL,SADRS+1       ;SADRS取得
  6273  CD2B60        			CALL	RCVBYTE
  6276  77            			LD		(HL),A
  6277  2B            			DEC		HL
  6278  CD2B60        			CALL	RCVBYTE
  627B  77            			LD		(HL),A
  627C  CD2B60        			CALL	RCVBYTE          ;チェックサム廃棄
                      			
  627F  2A3DFF        			LD		HL,(SADRS)
  6282  CD2B60        	MCLD1:	CALL	RCVBYTE          ;ヘッダー3AH受信、廃棄
  6285  CD2B60        			CALL	RCVBYTE          ;データ長
  6288  A7            			AND		A
  6289  280D          			JR		Z,MCLD3          ;データ長が0なら終了
  628B  47            			LD		B,A
  628C  CD2B60        	MCLD2:	CALL	RCVBYTE          ;実データ受信
  628F  77            			LD		(HL),A
  6290  23            			INC		HL
  6291  10F9          			DJNZ	MCLD2
  6293  CD2B60        			CALL	RCVBYTE          ;チェックサム廃棄
  6296  18EA          			JR		MCLD1
  6298  CD2B60        	MCLD3:	CALL	RCVBYTE          ;チェックサム廃棄
  629B  21C964        			LD		HL,MSG_OK        ;OK表示
  629E  CDED52        			CALL	MSGOUT
  62A1  C37960        			JP		CMD1
                      	
                      	;********** 5F9EH READ ONE BYTE FROM CMTの代替 *********
  62A4  3E72          	D_5F9E:	LD		A,72H            ;コマンド72Hを送信
  62A6  CD2B63        			CALL	STCD
  62A9  CD2B60        			CALL	RCVBYTE          ;1Byteのみ受信
  62AC  C9            			RET
                      	
                      	;**** コマンド、ファイル名送信 (IN:A コマンドコード HL:ファイルネームの先頭)****
  62AD  23            	STCMD:	INC		HL
  62AE  CD6463        			CALL	STFN             ;空白除去
  62B1  E5            			PUSH	HL
  62B2  CD2B63        			CALL	STCD             ;コマンドコード送信
  62B5  E1            			POP		HL
  62B6  A7            			AND		A                ;00以外ならERROR
  62B7  2008          			JR		NZ,STCMD2
  62B9  CD7863        			CALL	STFS             ;ファイルネーム送信
  62BC  A7            			AND		A                ;00以外ならERROR
  62BD  C2C162        			JP		NZ,STCMD2
  62C0  C9            			RET
  62C1  CD9263        	STCMD2:	CALL	SDERR
  62C4  C9            	SRCMD3:	RET
                      	
                      	;************ Wコマンド .CMT SAVE ********************************
  62C5  23            	MONSAVE:INC		HL
  62C6  CD6463        			CALL	STFN
  62C9  CD3263        			CALL	HLHEX            ;4桁の16進数であればSADRSにセットして続行
  62CC  DAC960        			JP		C,MONERR
  62CF  223DFF        			LD		(SADRS),HL       ;SARDS保存
  62D2  EB            			EX		DE,HL
                      	
  62D3  CD6463        			CALL	STFN
  62D6  CD3263        			CALL	HLHEX            ;4桁の16進数であればEADRSにセットして続行
  62D9  DAC960        			JP		C,MONERR
  62DC  2241FF        			LD		(EADRS),HL       ;EARDS保存
                      	
  62DF  D5            			PUSH	DE
  62E0  E5            			PUSH	HL
  62E1  ED5B3DFF      			LD		DE,(SADRS)
  62E5  ED52          			SBC		HL,DE
  62E7  E1            			POP		HL
  62E8  D1            			POP		DE
  62E9  DAC960        			JP		C,MONERR         ;EADRSがSADRSより小さければエラー
                      	
  62EC  EB            			EX		DE,HL
  62ED  2B            			DEC		HL
                      			
  62EE  3E70          			LD		A,70H            ;コマンド70Hを送信
  62F0  CDAD62        			CALL	STCMD
  62F3  C27960        			JP		NZ,CMD1
                      	
  62F6  21CF64        			LD		HL,MSG_WR        ;WRITING表示
  62F9  CDED52        			CALL	MSGOUT
                      	
  62FC  2A3DFF        			LD		HL,(SADRS)       ;SADRSを送信
  62FF  7D            			LD		A,L
  6300  CD3E60        			CALL	SNDBYTE
  6303  7C            			LD		A,H
  6304  CD3E60        			CALL	SNDBYTE
  6307  ED5B41FF      			LD		DE,(EADRS)       ;送信する全体バイト数を算出するためにEADRSを送信
  630B  7B            			LD		A,E
  630C  CD3E60        			CALL	SNDBYTE
  630F  7A            			LD		A,D
  6310  CD3E60        			CALL	SNDBYTE
  6313  7E            	MONSV1:	LD		A,(HL)           ;SADRSからEADRSまでを送信
  6314  CD3E60        			CALL	SNDBYTE
  6317  7C            			LD		A,H
  6318  BA            			CP		D
  6319  2004          			JR		NZ,MONSV2
  631B  7D            			LD		A,L
  631C  BB            			CP		E
  631D  2803          			JR		Z,MONSV3         ;HL = DE までLOOP
  631F  23            	MONSV2:	INC		HL
  6320  18F1          			JR		MONSV1
  6322  21C964        	MONSV3:	LD		HL,MSG_OK        ;OK表示
  6325  CDED52        			CALL	MSGOUT
  6328  C37960        			JP		CMD1
                      	
                      	;**** コマンド送信 (IN:A コマンドコード)****
  632B  CD3E60        	STCD:	CALL	SNDBYTE          ;Aレジスタのコマンドコードを送信
  632E  CD2B60        			CALL	RCVBYTE          ;状態取得(00H=OK)
  6331  C9            			RET
                      	
                      	;**** HLからの4Byteが16進数を表すアスキーコードであれば16進数に変換してHLに代入 **************
  6332  EB            	HLHEX:	EX		DE,HL
  6333  210000        			LD		HL,0000H
  6336  0604          			LD		B,04H
  6338  1A            	HLHEX1:	LD		A,(DE)
  6339  13            			INC		DE
  633A  CDC15F        			CALL	AZLCNV
  633D  CD395E        			CALL	HEXCHK
  6340  DA4963        			JP		C,HLHEX2
  6343  CD4B5E        			CALL	BINCV4
  6346  10F0          			DJNZ	HLHEX1
  6348  AF            			XOR		A
  6349  C9            	HLHEX2:	RET
                      	
                      	;**** HLからの2Byteが16進数を表すアスキーコードであれば16進数に変換してAに代入 **************
  634A  E5            	TWOHEX:	PUSH	HL
  634B  210000        			LD		HL,0000H
  634E  0602          			LD		B,02H
  6350  1A            	TWHEX1:	LD		A,(DE)
  6351  13            			INC		DE
  6352  CDC15F        			CALL	AZLCNV
  6355  CD395E        			CALL	HEXCHK
  6358  DA6263        			JP		C,TWHEX2
  635B  CD4B5E        			CALL	BINCV4
  635E  10F0          			DJNZ	TWHEX1
  6360  AF            			XOR		A
  6361  7D            			LD		A,L
  6362                	TWHEX2:
  6362  E1            			POP		HL
  6363  C9            			RET
                      	
                      	;****** FILE NAMEが取得できるまでスペース、ダブルコーテーションを読み飛ばし (IN:HL コマンド文字の次の文字 OUT:HL ファイルネームの先頭)*********
  6364  F5            	STFN:	PUSH	AF
  6365  7E            	STFN1:	LD		A,(HL)
  6366  FE20          			CP		20H
  6368  2804          			JR		Z,STFN2
  636A  FE22          			CP		22H
  636C  2003          			JR		NZ,STFN3
  636E  23            	STFN2:	INC		HL               ;ファイルネームまでスペース読み飛ばし
  636F  18F4          			JR		STFN1
  6371  F1            	STFN3:	POP		AF
  6372  C9            			RET
                      	
  6373                	STSV2:                           ;ファイルネームの取得に失敗
  6373  214B65        			LD		HL,MSG_FNAME
  6376  185F          			JR		ERRMSG
                      	
                      	;**** ファイルネーム送信(IN:HL ファイルネームの先頭) ******
  6378  0620          	STFS:	LD		B,20H
  637A  7E            	STFS1:	LD		A,(HL)           ;FNAME送信
  637B  FE22          			CP		22H
  637D  2003          			JR		NZ,STFS2
  637F  23            			INC		HL
  6380  18F8          			JR		STFS1
  6382  CD3E60        	STFS2:	CALL	SNDBYTE
  6385  23            			INC		HL
  6386  05            			DEC		B
  6387  20F1          			JR		NZ,STFS1
  6389  3E0D          			LD		A,0DH
  638B  CD3E60        			CALL	SNDBYTE
  638E  CD2B60        			CALL	RCVBYTE          ;状態取得(00H=OK)
  6391  C9            			RET
                      	
                      	;************** エラー内容表示 *****************************
  6392                	SDERR:
  6392  F5            			PUSH	AF
  6393  FEF0          			CP		0F0H
  6395  2005          			JR		NZ,ERR3
  6397  214366        			LD		HL,MSG_F0        ;SD-CARD INITIALIZE ERROR
  639A  183B          			JR		ERRMSG
  639C  FEF1          	ERR3:	CP		0F1H
  639E  2005          			JR		NZ,ERR4
  63A0  215C66        			LD		HL,MSG_F1        ;NOT FIND FILE
  63A3  1832          			JR		ERRMSG
  63A5  FEF3          	ERR4:	CP		0F3H
  63A7  2005          			JR		NZ,ERR5
  63A9  217A66        			LD		HL,MSG_F3        ;FILE EXIST
  63AC  1829          			JR		ERRMSG
  63AE  FEF4          	ERR5:	CP		0F4H
  63B0  2005          			JR		NZ,ERR6
  63B2  215D65        			LD		HL,MSG_CMD       ;COMMAND FAILED
  63B5  1820          			JR		ERRMSG
  63B7  FEF5          	ERR6:	CP		0F5H
  63B9  2005          			JR		NZ,ERR7
  63BB  218566        			LD		HL,MSG_F5        ;NO BASIC PROGRAM
  63BE  1817          			JR		ERRMSG
  63C0  FEF6          	ERR7:	CP		0F6H
  63C2  2005          			JR		NZ,ERR99
  63C4  214B65        			LD		HL,MSG_FNAME     ;PARAMETER FAILED
  63C7  180E          			JR		ERRMSG
  63C9  CD835E        	ERR99:	CALL	MONBHX
  63CC  7A            			LD		A,D
  63CD  CD5702        			CALL	CONOUT
  63D0  7B            			LD		A,E
  63D1  CD5702        			CALL	CONOUT
  63D4  21A466        			LD		HL,MSG99         ;その他ERROR
  63D7  CDED52        	ERRMSG:	CALL	MSGOUT
  63DA  CDCA5F        			CALL	MONCLF
  63DD  F1            			POP		AF
  63DE  C9            			RET
                      	
                      	;************** BASIC CMT LOAD *********************
  63DF                	CMDLOAD:
  63DF  2B            			DEC		HL
  63E0  3E73          			LD		A,73H            ;コマンド73Hを送信
  63E2  CDAD62        			CALL	STCMD
  63E5  205E          			JR		NZ,CMDLD8
                      	
  63E7  211801        			LD		HL,0118H         ;1文字目、24行目へカーソルを移動
  63EA  CDA903        			CALL	CSR
  63ED  CDCA5F        			CALL	MONCLF
                      			
  63F0  CD2B60        			CALL	RCVBYTE          ;ヘッダー受信
  63F3  FED3          			CP		0D3H             ;D3Hでなければエラー
  63F5  280A          			JR		Z,CMDLD
  63F7  219266        			LD		HL,MSG_F6        ;NOT BASIC PROGRAM
  63FA  CDED52        			CALL	MSGOUT
  63FD  CDCA5F        			CALL	MONCLF
  6400  C9            			RET
                      			
  6401                	CMDLD:	
  6401  213EEF        			LD		HL,FILNAM        ;CMTファイル中に記載のファイルネーム6文字を受信
  6404  0606          			LD		B,06H
  6406  CD2B60        	CMDLD2:	CALL	RCVBYTE
  6409  77            			LD		(HL),A
  640A  23            			INC		HL
  640B  10F9          			DJNZ	CMDLD2
                      	
  640D  21C064        			LD		HL,MSG_LD        ;LOADING表示
  6410  CDED52        			CALL	MSGOUT
                      	
  6413  213EEF        			LD		HL,FILNAM        ;ファイルネーム表示
  6416  1196EC        			LD		DE,IBUF
  6419  010600        			LD		BC,0006H
  641C  EDB0          			LDIR
  641E  AF            			XOR		A
  641F  12            			LD		(DE),A
  6420  2196EC        			LD		HL,IBUF
  6423  CDED52        			CALL	MSGOUT
  6426  CDCA5F        			CALL	MONCLF
                      			
  6429  2A54EB        			LD		HL,(BASSRT)      ;BASICプログラム格納開始位置をHLに設定
                      	
  642C  060C          	CMDLD3:	LD		B,0CH            ;00Hを12個連続で受信するまでループ
  642E  CD2B60        	CMDLD4:	CALL	RCVBYTE
  6431  77            			LD		(HL),A
  6432  23            			INC		HL
                      	
  6433  A7            			AND		A
  6434  2802          			JR		Z,CMDLD5
  6436  18F4          			JR		CMDLD3
  6438  05            	CMDLD5:	DEC		B
  6439  2802          			JR		Z,CMDLD6
  643B  18F1          			JR		CMDLD4
                      	
  643D  010700        	CMDLD6:	LD		BC,0007H         ;HLの位置を7つ戻してBASICプログラム終了位置とする
  6440  ED42          			SBC		HL,BC
                      			
  6442  C38B1F        			JP		AFTLOAD          ;BASICプログラムLOAD後処理
  6445                	CMDLD8:
  6445  C9            			RET
                      			
                      	;********************** BASIC CMT SAVE **********************
  6446                	CMDSAVE:
  6446  E5            			PUSH	HL
  6447  2A54EB        			LD		HL,(BASSRT)      ;BASICプログラム格納開始位置をHLに設定
  644A  7E            			LD		A,(HL)
  644B  23            			INC		HL
  644C  B6            			OR		(HL)
  644D  E1            			POP		HL
  644E  3EF5          			LD		A,0F5H           ;BASICプログラムが1行もなければエラー
  6450  CA9263        			JP		Z,SDERR
                      	
  6453  2B            			DEC		HL
  6454  3E74          			LD		A,74H            ;コマンド74Hを送信
  6456  CDAD62        			CALL	STCMD
  6459  2030          			JR		NZ,CMDSV3
                      	
  645B  21CF64        			LD		HL,MSG_WR        ;WRITING表示
  645E  CDED52        			CALL	MSGOUT
                      	
  6461  CD865B        			CALL	RENUM9           ;BASICプログラムSAVE前処理
  6464  2A54EB        			LD		HL,(BASSRT)      ;BASSRTからVARBGN-1までを送信
  6467  7D            			LD		A,L
  6468  CD3E60        			CALL	SNDBYTE
  646B  7C            			LD		A,H
  646C  CD3E60        			CALL	SNDBYTE
  646F  ED5BA0EF      			LD		DE,(VARBGN)
  6473  1B            			DEC		DE
  6474  7B            			LD		A,E
  6475  CD3E60        			CALL	SNDBYTE
  6478  7A            			LD		A,D
  6479  CD3E60        			CALL	SNDBYTE
  647C  7E            	CMDSV1:	LD		A,(HL)
  647D  CD3E60        			CALL	SNDBYTE
  6480  7C            			LD		A,H
  6481  BA            			CP		D
  6482  2004          			JR		NZ,CMDSV2
  6484  7D            			LD		A,L
  6485  BB            			CP		E
  6486  2803          			JR		Z,CMDSV3         ;HL = DE までLOOP
  6488  23            	CMDSV2:	INC		HL
  6489  18F1          			JR		CMDSV1
                      			
  648B                	CMDSV3:
  648B  21C964        			LD		HL,MSG_OK        ;OK表示
  648E  CDED52        			CALL	MSGOUT
  6491  C9            			RET
                      	
                      	;************ BASIC FILES ********************
  6492                	CMDFILES:
                      	;2022.7.24 SYNTAX ERROR回避により、廃止
                      	;		PUSH	HL
                      	;		LD		HL,FMSG          ;FILESの後ろにファイル名検索文字を指定すると構文エラーになるため、別行として入力
                      	;		CALL	MSGOUT
                      	;		CALL	LINPUT
                      	;		INC		HL
  6492  CD6463        			CALL	STFN             ;入力文字列取得
  6495  EB            			EX		DE,HL
  6496  21B166        			LD		HL,DEFDIR2       ;行頭に'LOAD 'を付けることでカーソルを移動させリターンで実行できるように
  6499  010600        			LD		BC,D2END-DEFDIR2
  649C  CDA361        			CALL	DIRLIST          ;DIRLIST本体をコール
                      	;2022.7.24 SYNTAX ERROR回避
  649F  21BF64        			LD		HL,FMSG         ;SYNTAX ERROR回避
  64A2  CDED52        			CALL	MSGOUT
                      	;		POP		HL
  64A5  C9            			RET
                      			
                      	;************ BASIC KILL ***************************
  64A6                	CMDKILL:
  64A6  3E75          			LD		A,75H            ;コマンド75Hを送信
  64A8  CDAD62        			CALL	STCMD
  64AB  2011          			JR		NZ,CMDKL4        ;ファイル名が送信できなかった。
  64AD  CD2B60        			CALL	RCVBYTE
  64B0  A7            			AND		A
  64B1  2008          			JR		NZ,CMDKL3        ;ファイルが存在しない
  64B3  21D864        			LD		HL,MSG_KL
  64B6  CDED52        			CALL	MSGOUT
  64B9  1803          			JR		CMDKL4
  64BB  CD9263        	CMDKL3:	CALL	SDERR
  64BE                	CMDKL4:
  64BE  C9            			RET
                      			
                      	;FMSG	DEFB	'FILE SEARCH:'
  64BF  00            	FMSG:	DEFB	00H
                      	
  64C0                	MSG_LD:
  64C0  4C4F4144494E47			DB		'LOADING '
  64C8  00            			DB		00H
                      	
  64C9                	MSG_OK:
  64C9  4F4B21        			DB		'OK!'
  64CC  0D0A00        			DB		0DH,0AH,00H
                      	
  64CF                	MSG_WR:
  64CF  57524954494E47			DB		'WRITING '
  64D7  00            			DB		00H
                      	
  64D8                	MSG_KL:
  64D8  46494C45204445			DB		'FILE DELETED!'
  64E5  0D0A00        			DB		0DH,0AH,00H
                      	
  64E8                	MSG_AD1:
  64E8  41445253202B30			DB		'ADRS +0 +1 +2 +3 +4 +5 +6 +7 01234567'
  650D  0D0A00        			DB		0DH,0AH,00H
                      			
  6510                	MSG_AD2:
  6510  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:ESC'
  6529  0D0A00        			DB		0DH,0AH,00H
                      			
  652C                	MSG_KEY1:
  652C  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:'
  6542  00            			DB		00H
  6543                	MSG_KEY2:
  6543  204F5220455343			DB		' OR ESC'
  654A  00            			DB		00H
                      	
  654B                	MSG_FNAME:
  654B  504152414D4554			DB		'PARAMETAR FAILED!'
  655C  00            			DB		00H
                      			
  655D                	MSG_CMD:
  655D  434F4D4D414E44			DB		'COMMAND FAILED!',0DH,0AH
  656E  20422020202020			DB		' B             : Return Basic',0DH,0AH
  658D  2044206E6E6E6E			DB		' D nnnn        : Memory Dump',0DH,0AH
  65AB  20462078202020			DB		' F x           : Find SD File',0DH,0AH
  65CA  2047206E6E6E6E			DB		' G nnnn        : Exec Program',0DH,0AH
  65E9  204C2078202020			DB		' L x           : Load From SD',0DH,0AH
  6608  2053206E6E6E6E			DB		' S nnnn nn..   : Memory Write',0DH,0AH
  6627  2057206E6E6E6E			DB		' W nnnn nnnn x : Save To SD'
  6642  00            			DB		00H
                      			
  6643                	MSG_F0:
  6643  53442D43415244			DB		'SD-CARD INITIALIZE ERROR'
  665B  00            			DB		00H
                      			
  665C                	MSG_F1:
  665C  4E4F542046494E			DB		'NOT FIND FILE'
  6669  00            			DB		00H
                      			
  666A                	MSG_F2:
  666A  4E4F54204F424A			DB		'NOT OBJECT FILE'
  6679  00            			DB		00H
                      			
  667A                	MSG_F3:
  667A  46494C45204558			DB		'FILE EXIST'
  6684  00            			DB		00H
                      			
  6685                	MSG_F5:
  6685  4E4F2050524F47			DB		'NO PROGRAM!!'
  6691  00            			DB		00H
                      			
  6692                	MSG_F6:
  6692  4E4F5420424153			DB		'NOT BASIC PROGRAM'
  66A3  00            			DB		00H
                      			
  66A4                	MSG99:
  66A4  204552524F52  			DB		' ERROR'
  66AA  00            			DB		00H
                      			
  66AB  2A53          	MSG_FDW:	DB		'*S'
  66AD  00            			DB		00H
                      	
  66AE                	DEFDIR:
  66AE  2A4C20        			DB		'*L '
  66B1                	DEND:
                      	
  66B1                	DEFDIR2:
  66B1  4C4F41442022  			DB		'LOAD ',22H
  66B7                	D2END:
                      	
                      	
  7FFC                			ORG		7FFCH
                      	
                      	;************* 代替MONITORへジャンプするための設定 *************
  7FFC  C36E60        			JP		MONINI
  7FFF  55            			DB		55H
                      			
  8000                			END
